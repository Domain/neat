module cx.macros.either;

import package(compiler).backend.base;
import package(compiler).cx.base;
import package(compiler).cx.parser;
import package(compiler).cx.hash;
import package(compiler).cx.parser_base;
import package(compiler).cx.types;
import package(compiler).helpers;

class ASTEitherDecl : ASTType
{
    ASTType[] partTypes;

    this(ASTType[] partTypes) { this.partTypes = partTypes; }

    override Type compile(Context context)
    {
        Type[] partTypes = new Type[](this.partTypes.length);
        for (int i = 0; i < this.partTypes.length; i += 1) {
            partTypes[i] = this.partTypes[i].compile(context);
        }

        return new Either(partTypes);
    }
}

class EitherExpr : Expression
{
    Either either;

    Expression value;

    int offset;

    this(Either either, Expression value, int offset) {
        this.either = either;
        this.offset = offset;
        this.value = value;
    }

    override Type type()
    {
        return either;
    }

    override int emit(Generator generator)
    {
        BackendType eitherType = either.emit(generator.platform);
        int reg = generator.fun.alloca(eitherType);
        int offsetTarget = generator.fun.fieldOffset(eitherType, reg, 0);
        int offsetValue = generator.fun.intLiteral(offset + 1);
        int rawValueTarget = generator.fun.fieldOffset(eitherType, reg, 1);
        BackendType backendValueType = either.types[offset].emit(generator.platform);
        int valueTarget = generator.fun.bitcast(rawValueTarget, new BackendPointerType(backendValueType));
        int value = this.value.emit(generator);

        generator.fun.store(new BackendIntType, offsetTarget, offsetValue);
        generator.fun.store(backendValueType, valueTarget, value);

        return generator.fun.load(eitherType, reg);
    }

    override void hash(Hash hash) { hash.adds("EitherExpr"); value.hash(hash); hash.addl(offset); }
}

class EitherType : Expression
{
    Expression value;

    this(Expression value) { this.value = value; }

    override Type type()
    {
        return new Integer;
    }

    override int emit(Generator generator)
    {
        auto eitherType = this.value.type().emit(generator.platform);
        int eitherValue = this.value.emit(generator);
        return generator.fun.field(eitherType, eitherValue, 0);
    }

    override void hash(Hash hash) { hash.adds("EitherType"); value.hash(hash); }
}

class EitherMember : Expression
{
    Expression value;

    int offset;

    this(Expression value, int offset) { this.value = value; this.offset = offset; }

    override Type type()
    {
        return this.value.type().instanceOf(Either).types[this.offset];
    }

    override int emit(Generator generator)
    {
        int stack = generator.fun.saveStack();

        BackendType eitherType = this.value.type().emit(generator.platform);
        int eitherValuePtr = generator.fun.alloca(eitherType);

        int eitherValue = this.value.emit(generator);
        generator.fun.store(eitherType, eitherValuePtr, eitherValue);

        int eitherUnionPtr = generator.fun.fieldOffset(eitherType, eitherValuePtr, 1);
        // load the union value
        BackendType backendValueType = this.type().emit(generator.platform);
        int valuePtr = generator.fun.bitcast(eitherUnionPtr, new BackendPointerType(backendValueType));
        int result = generator.fun.load(backendValueType, valuePtr);

        generator.fun.restoreStack(stack);
        return result;
    }

    override void hash(Hash hash) { hash.adds("EitherMember"); value.hash(hash); hash.addl(offset); }
}

class Either : Type
{
    Type[] types;

    this(Type[] types) { this.types = types; }

    override BackendType emit(Platform platform) {
        int size = 0;
        int alignment = 1;
        for (int i = 0; i < this.types.length; i += 1) {
            auto llvmType = this.types[i].emit(platform);
            int memberSize = llvmType.size(platform);
            int memberAlignment = llvmType.alignment(platform);

            if (memberSize > size) size = memberSize;
            if (memberAlignment > alignment) alignment = memberAlignment;
        }
        auto parts = new BackendType[](2);
        parts[0] = new BackendIntType;
        parts[1] = new BackendSpacerType(size, alignment);
        return new BackendStructType(parts);
    }

    override Expression implicitConvertFrom(Expression source)
    {
        for (int i = 0; i < this.types.length; i += 1) {
            auto eitherType = this.types[i];
            if (eitherType.same(source.type()))
            {
                return new EitherExpr(this, source, i);
            }
        }
        return null;
    }

    override bool same(Type type) {
        auto other = type.instanceOf(Either);
        if (!other || types.length != other.types.length)
            return false;
        for (int i = 0; i < types.length; i += 1)
            if (!types[i].same(other.types[i]))
                return false;
        return true;
    }

    override string mangle() {
        string ret = "either";
        for (int i = 0; i < types.length; i += 1)
            ret ~= "_" ~ types[i].mangle();
        return ret;
    }

    override string repr() {
        string ret = "Either(";
        for (int i = 0; i < types.length; i += 1) {
            if (i) ret ~= ", ";
            ret ~= types[i].repr();
        }
        ret ~= ")";
        return ret;
    }

    override void hash(Hash hash) {
        hash.adds("Either");
        hash.addl(types.length);
        for (int i = 0; i < types.length; i += 1)
            types[i].hash(hash);
    }
}

ASTType parseEither(Parser parser, LexicalContext lexicalContext)
{
    parser.begin();
    if (!acceptIdentifier(parser, "Either") || !parser.accept("("))
    {
        parser.revert();
        return null;
    }
    parser.commit();

    ASTType[] types;
    while (!parser.accept(")"))
    {
        if (types.length > 0 && !parser.accept(","))
        {
            parser.fail("',' or ')' expected");
        }
        ASTType type = lexicalContext.compiler.parseType(parser, lexicalContext);
        parser.assert_(!!type, "argument type expected");
        types ~= type;
    }
    return new ASTEitherDecl(types);
}

/**
 * a.case(T t: 5, U u: 6)
 */

struct ASTEitherCaseExprCase
{
    Loc loc;

    ASTType type;

    string identifier;

    ASTSymbol expr;
}

class StatementExpression : Expression
{
    Statement statement;

    Expression expression;

    this(this.statement, this.expression) { }

    override Type type() { return this.expression.type(); }

    override int emit(Generator generator)
    {
        this.statement.emit(generator);
        return this.expression.emit(generator);
    }

    override void hash(Hash hash) {
        hash.adds("StatementExpression");
        statement.hash(hash);
        expression.hash(hash);
    }
}

class ASTEitherCaseExpr : ASTSymbol
{
    Loc loc;

    ASTSymbol base;

    ASTEitherCaseExprCase[] cases;

    this(Loc loc, ASTSymbol base, ASTEitherCaseExprCase[] cases) { this.loc = loc; this.base = base; this.cases = cases; }

    override Symbol compile(Context context) {
        auto baseExpr = beExpression2(base.compile(context), loc);
        auto either = baseExpr.type().instanceOf(Either);

        loc.assert2s(!!either, "Expected Either for case expression");

        bool[] handled = new bool[](either.types.length);

        Expression mergedExpr = null;
        // TODO merge types directly
        for (int i = 0; i < this.cases.length; i += 1) {
            auto case_ = this.cases[i];
            auto caseType = case_.type.compile(context);

            auto namespace = context.compiler.variableDeclaration(context.namespace, case_.identifier, caseType);
            auto subContext = context.withNamespace(namespace);

            Expression expr = beExpression2(case_.expr.compile(subContext), case_.loc);

            if (!mergedExpr) mergedExpr = expr;
            else {
                auto left = context.compiler.implicitConvertTo(expr, mergedExpr.type());
                auto right = context.compiler.implicitConvertTo(mergedExpr, expr.type());
                if (!left && !right) case_.loc.assert2s(false, "incompatible expression type");
                else if (left && right && !left.type().same(right.type())) case_.loc.assert2s(false, "ambiguous expression type");
                if (left) mergedExpr = left;
                else if (right) mergedExpr = right;
            }
        }

        auto mergeType = mergedExpr.type();
        auto resultNamespace = context.compiler.variableDeclaration(context.namespace, "", mergeType);
        auto resultContext = context.withNamespace(resultNamespace);
        Reference resultVar = resultNamespace.accessLocal();
        Statement ifTree;

        for (int i = 0; i < this.cases.length; i += 1) {
            auto case_ = this.cases[i];
            Type type = case_.type.compile(resultContext);

            int matched = -1;
            for (int k = 0; k < either.types.length; k += 1) {
                if (type.same(either.types[k])) {
                    case_.loc.assert2s(matched == -1, "either type " ~ type.repr() ~ " matched twice");
                    matched = k;
                }
            }
            case_.loc.assert2s(matched != -1, "Type is not listed in Either!");
            handled[matched] = true;

            auto namespace = context.compiler.variableDeclaration(resultContext.namespace, case_.identifier, type);
            Reference variable = namespace.accessLocal();
            Statement assignment = context.compiler.assignStatement(variable, new EitherMember(baseExpr, matched));
            Expression userExpr = beExpression2(case_.expr.compile(resultContext.withNamespace(namespace)), case_.loc);
            Expression typeCorrectedUserExpr = context.compiler.expectImplicitConvertTo(userExpr, mergeType, case_.loc);
            Statement userStmt = context.compiler.assignStatement(resultVar, typeCorrectedUserExpr);
            Statement then = context.compiler.sequenceStatement([assignment, userStmt]);
            Expression test = context.compiler.binaryOp2(
                "==", context, new EitherType(baseExpr), context.compiler.intLiteral(matched + 1), this.loc);

            ifTree = context.compiler.ifStatement(test, then, ifTree);
        }
        for (int i = 0; i < handled.length; i += 1) {
            loc.assert2s(handled[i], "Type " ~ either.types[i].repr() ~ " not handled!");
        }

        return new StatementExpression(ifTree, resultVar);
    }
}

ASTSymbol parseCaseExpr(Parser parser, LexicalContext lexicalContext, ASTSymbol current)
{
    parser.begin();
    if (!(parser.accept(".") && acceptIdentifier(parser, "case") && parser.accept("(")))
    {
        parser.revert();
        return null;
    }
    parser.commit();

    ASTEitherCaseExprCase[] cases;
    while (!parser.accept(")"))
    {
        if (cases.length) parser.expect(",");
        ASTType type = lexicalContext.compiler.parseType(parser, lexicalContext);
        parser.assert_(!!type, "case type expected");
        string identifier = parseIdentifier(parser);
        parser.expect(":");
        Loc loc = parser.loc();
        ASTSymbol expr = lexicalContext.compiler.parseExpression(parser, lexicalContext);
        cases ~= ASTEitherCaseExprCase(loc, type, identifier, expr);
    }
    return new ASTEitherCaseExpr(parser.loc(), current, cases);
}

/**
 * a.case {
 *   T t: foo;
 *   U u: { bar; }
 * }
 */

struct ASTEitherCaseStmtCase
{
    Loc loc;

    ASTType type;

    string identifier;

    ASTStatement stmt;
}

class ASTEitherCaseStmt : ASTStatement
{
    Loc loc;

    ASTSymbol base;

    ASTEitherCaseStmtCase[] cases;

    this(Loc loc, ASTSymbol base, ASTEitherCaseStmtCase[] cases) { this.loc = loc; this.base = base; this.cases = cases; }

    override StatementCompileResult compile(Context context) {
        auto baseExpr = beExpression2(base.compile(context), loc);
        auto baseType = baseExpr.type();
        auto either = baseType.instanceOf(Either);

        loc.assert2s(!!either, "Expected Either for case expression");

        bool[] handled = new bool[](either.types.length);
        Statement ifTree = null;
        for (int i = 0; i < this.cases.length; i += 1) {
            auto case_ = this.cases[i];
            Type type = case_.type.compile(context);

            int matched = -1;
            for (int k = 0; k < either.types.length; k += 1) {
                if (type.same(either.types[k])) {
                    case_.loc.assert2s(matched == -1, "either type " ~ type.repr() ~ " matched twice");
                    matched = k;
                }
            }
            case_.loc.assert2s(matched != -1, "Type is not listed in Either!");
            handled[matched] = true;

            auto namespace = context.compiler.variableDeclaration(context.namespace, case_.identifier, type);
            Reference variable = namespace.accessLocal();
            Statement assignment = context.compiler.assignStatement(variable, new EitherMember(baseExpr, matched));
            auto userStmtPair = case_.stmt.compile(context.withNamespace(namespace));
            Statement then = context.compiler.sequenceStatement([assignment, userStmtPair.statement]);
            Expression test = context.compiler.binaryOp2(
                "==", context, new EitherType(baseExpr), context.compiler.intLiteral(matched + 1), this.loc);

            ifTree = context.compiler.ifStatement(test, then, ifTree);
        }
        for (int i = 0; i < handled.length; i += 1) {
            loc.assert2s(handled[i], "Type " ~ either.types[i].repr() ~ " not handled!");
        }
        return StatementCompileResult(
            ifTree,
            context);
    }
}

ASTStatement parseCaseStmt(Parser parser, LexicalContext lexicalContext)
{
    parser.begin();
    auto expr = lexicalContext.compiler.parseExpressionLeaf(parser, lexicalContext);
    if (!expr) {
        parser.revert();
        return null;
    }
    auto member = expr.instanceOf(ASTMemberBase);
    if (!member || member.member != "case" || !parser.accept("{")) {
        parser.revert();
        return null;
    }
    parser.commit();

    ASTEitherCaseStmtCase[] cases;
    while (!parser.accept("}"))
    {
        ASTType type = lexicalContext.compiler.parseType(parser, lexicalContext);
        parser.assert_(!!type, "case type expected");
        string identifier = parseIdentifier(parser);
        parser.expect(":");
        Loc loc = parser.loc();
        ASTStatement stmt = lexicalContext.compiler.parseStatement(parser, lexicalContext);
        cases ~= ASTEitherCaseStmtCase(loc, type, identifier, stmt);
    }
    return new ASTEitherCaseStmt(parser.loc(), member.base, cases);
}

class EitherMacro : Macro
{
    this() { }

    override void apply(MacroArgs args) {
        auto typeArgs = args.instanceOf(TypeMacroArgs);
        if (typeArgs) {
            typeArgs.astType = parseEither(typeArgs.parser, typeArgs.lexicalContext);
        }
        auto propArgs = args.instanceOf(ParsePropertyArgs);
        if (propArgs) {
            propArgs.result = parseCaseExpr(propArgs.parser, propArgs.lexicalContext, propArgs.left);
        }
        auto stmtArgs = args.instanceOf(ParseStatementArgs);
        if (stmtArgs) {
            stmtArgs.statement = parseCaseStmt(stmtArgs.parser, stmtArgs.lexicalContext);
        }
    }
}

void addEitherMacro(MacroState macroState)
{
    macroState.addMacro(new EitherMacro);
}

macro(addEitherMacro);
