module cx.macros.listcomprehension;

macro import cx.macros.quasiquoting;

import package(compiler).cx.array;
import package(compiler).cx.base;
import package(compiler).cx.either;
import package(compiler).cx.parser;
import package(compiler).cx.parser_base;
import package(compiler).cx.statements;
import package(compiler).helpers;

class ASTListComprehension : ASTSymbol
{
    string iterationMode; // "", "any", "all", "first", "join", "count", "sum", "min", "max"

    ASTSymbol expr;

    ASTSymbol default_;

    ASTSymbol joinKey;

    string iname; // iteration variable

    string varname;

    ASTSymbol source;

    ASTSymbol where;

    Loc loc;

    this(this.iterationMode, this.expr, this.default_, this.joinKey,
         this.iname, this.varname, this.source, this.where, this.loc)
    {
        if (!iname.length) iname = "__i";
    }

    override Symbol compile(Context context) {
        auto compiler = context.compiler;

        auto sourceType = beExpression3(context, source.compile(context), loc).type;
        loc.assert2s(!!sourceType.instanceOf(Array), "expected array for source of list comprehension");

        auto i = compiler.astIdentifier(iname, loc);

        if (iterationMode == "count") {
            loc.assert2s(!expr, "no expr for count");
            mut auto test = compiler.$expr true;
            if (where) test = compiler.$expr $where && $test;
            return (compiler.$expr ({
                auto __source = $source;
                mut int __count = 0;
                for (mut size_t $iname = 0; $i < __source.length; $i += 1) {
                    auto $varname = __source[$i];
                    if ($test) __count += 1;
                }
                __count;
            })).compile(context);
        }

        auto astType = compiler.$type typeof(({ auto $varname = $source[0]; size_t $iname = 0; $expr; }));
        auto type = astType.compile(context);

        if (type.instanceOf(Void)) {
            loc.assert2s(iterationMode == "", "non-void expression expected");
            assert(!default_);
            if (where) {
                return (compiler.$expr ({
                    auto __source = $source;
                    for (mut size_t $iname = 0; $i < __source.length; $i += 1) {
                        auto $varname = __source[$i];
                        if ($where) $expr;
                    }
                    0;
                })).compile(context);
            }
            return (compiler.$expr ({
                auto __source = $source;
                for (mut size_t $iname = 0; $i < __source.length; $i += 1) {
                    auto $varname = __source[$i];
                    $expr;
                }
                0;
            })).compile(context);
        } else if (iterationMode == "any") {
            assert(!default_);
            mut auto test = expr;
            if (where) test = compiler.$expr $where && $test;
            return (compiler.$expr ({
                auto __source = $source;
                mut bool __result = false;
                for (mut size_t $iname = 0; $i < __source.length; $i += 1) {
                    auto $varname = __source[$i];
                    if ($test) { __result = true; break; }
                }
                __result;
            })).compile(context);
        } else if (iterationMode == "all") {
            assert(!default_);
            mut auto test = compiler.$expr !$expr;
            if (where) test = compiler.$expr $where && $test;
            return (compiler.$expr ({
                auto __source = $source;
                mut bool __result = true;
                for (mut size_t $iname = 0; $i < __source.length; $i += 1) {
                    auto $varname = __source[$i];
                    if ($test) { __result = false; break; }
                }
                __result;
            })).compile(context);
        } else if (iterationMode == "join") {
            assert(!default_);
            mut auto test = compiler.$expr true;
            if (where) test = compiler.$expr $where && $test;
            return (compiler.$expr ({
                auto __source = $source;
                mut string __result;
                mut bool __first = true;
                for (mut size_t $iname = 0; $i < __source.length; $i += 1) {
                    auto $varname = __source[$i];
                    if ($test) {
                        if (__first) {
                            __first = false;
                        } else {
                            __result ~= $joinKey;
                        }
                        __result ~= $expr;
                    }
                }
                __result;
            })).compile(context);
        } else if (iterationMode == "first") {
            assert(!!default_);
            mut auto test = compiler.$expr true;
            if (where) test = where;
            return (compiler.$expr ({
                auto __source = $source;
                mut auto __found = false;
                mut $astType __result;
                for (mut size_t $iname = 0; $i < __source.length; $i += 1) {
                    auto $varname = __source[$i];
                    __result = $expr;
                    if ($test) { __found = true; break; }
                }
                // TODO for (...) { } finally { }
                if (!__found) __result = $default_;
                __result;
            })).compile(context);
        } else if (iterationMode == "sum") {
            assert(!default_);
            mut auto test = compiler.$expr true;
            if (where) test = where;
            return (compiler.$expr ({
                auto __source = $source;
                mut $astType __sum = 0;
                for (mut size_t $iname = 0; $i < __source.length; $i += 1) {
                    auto $varname = __source[$i];
                    if ($test) { __sum += $expr; }
                }
                __sum;
            })).compile(context);
        } else if (iterationMode == "min") {
            mut auto test = compiler.$expr true;
            if (where) test = where;
            mut ASTSymbol init;
            if (default_) {
                init = default_;
            } else {
                init = compiler.$expr ({ assert(false); $astType __unreachable; __unreachable; });
            }
            return (compiler.$expr ({
                auto __source = $source;
                mut (:initial | $astType) __min = :initial;
                for (mut size_t $iname = 0; $i < __source.length; $i += 1) {
                    auto $varname = __source[$i];
                    if ($test) {
                        auto __value = $expr;
                        __min.case {
                            (:initial): __min = __value;
                            $astType current: {
                                if (__value < current)
                                    __min = __value;
                            }
                        }
                    }
                }
                __min.case(
                    (:initial): $init,
                    $astType v: v);
            })).compile(context);
        } else if (iterationMode == "max") {
            mut auto test = compiler.$expr true;
            if (where) test = where;
            mut ASTSymbol init;
            if (default_) {
                init = default_;
            } else {
                init = compiler.$expr ({ assert(false); $astType __unreachable; __unreachable; });
            }
            return (compiler.$expr ({
                auto __source = $source;
                mut (:initial | $astType) __max = :initial;
                for (mut size_t $iname = 0; $i < __source.length; $i += 1) {
                    auto $varname = __source[$i];
                    if ($test) {
                        auto __value = $expr;
                        __max.case {
                            (:initial): __max = __value;
                            $astType current: {
                                if (__value > current)
                                    __max = __value;
                            }
                        }
                    }
                }
                __max.case(
                    (:initial): $init,
                    $astType v: v);
            })).compile(context);
        } else {
            assert(iterationMode == "");
            if (where) {
                return (compiler.$expr ({
                    auto __source = $source;
                    mut $astType[] __result;
                    for (mut auto $iname = 0; $i < __source.length; $i += 1) {
                        auto $varname = __source[$i];
                        if ($where) __result ~= $expr;
                    }
                    __result;
                })).compile(context);
            }
            return (compiler.$expr ({
                auto __source = $source;
                auto __result = new $astType[](__source.length);
                for (mut auto $iname = 0; $i < __source.length; $i += 1) {
                    auto $varname = __source[$i];
                    __result[$i] = $expr;
                }
                __result;
            })).compile(context);
        }
    }

    override ASTSymbol quote(Quoter quoter) { print("cannot quote 'ASTListComprehension'"); assert(false); }
}

class ListComprehension : Macro
{
    this() { }
    override void apply(MacroArgs args) {
        auto args = args.instanceOf(ParseExpressionBaseArgs);
        if (args) {
            args.symbol = this.parse(args.parser, args.lexicalContext);
        }
    }

    ASTSymbol parse(Parser parser, LexicalContext lexicalContext)
    {
        auto compiler = lexicalContext.compiler;
        auto loc = parser.loc();

        parser.begin();
        if (!parser.accept("[")) {
            parser.revert();
            return null;
        }
        mut string iterationMode;
        mut ASTSymbol default_;
        mut ASTSymbol joinKey;
        if (acceptIdentifier(parser, "any")) iterationMode = "any";
        else if (acceptIdentifier(parser, "all")) iterationMode = "all";
        else if (acceptIdentifier(parser, "first")) iterationMode = "first";
        else if (acceptIdentifier(parser, "join")) {
            iterationMode = "join";
            joinKey = compiler.parseExpression(parser, lexicalContext);
            auto loc = parser.loc;
            loc.assert2s(!!joinKey, "join key expected");
        }
        else if (acceptIdentifier(parser, "count")) {
            iterationMode = "count";
        }
        else if (acceptIdentifier(parser, "sum")) {
            iterationMode = "sum";
        }
        else if (acceptIdentifier(parser, "min")) iterationMode = "min";
        else if (acceptIdentifier(parser, "max")) iterationMode = "max";
        mut ASTSymbol expr;
        // count is expressionless
        if (iterationMode != "count") {
            expr = compiler.parseExpression(parser, lexicalContext);
            if (!expr) {
                parser.revert();
                return null;
            }
            if (!acceptIdentifier(parser, "for")) {
                parser.revert();
                return null;
            }
        }
        mut string iname;
        mut string varname = parseIdentifier(parser);
        if (!varname.length) {
            parser.fail("variable name expected");
        }
        if (parser.accept(",")) {
            iname = varname;
            varname = parseIdentifier(parser);
            if (!varname.length) {
                parser.fail("variable name expected");
            }
        }
        parser.expect("in");
        auto source = compiler.parseExpression(parser, lexicalContext);
        if (!source) {
            parser.fail("source expression expected");
        }
        mut ASTSymbol where;
        if (acceptIdentifier(parser, "where")) {
            where = compiler.parseExpression(parser, lexicalContext);
            if (!where) {
                parser.fail("where expression expected");
            }
        }
        if (iterationMode == "first") {
            if (!acceptIdentifier(parser, "else"))
                parser.fail("'else' expected");
            default_ = compiler.parseExpression(parser, lexicalContext);
        }
        else if (iterationMode == "min" || iterationMode == "max") {
            if (acceptIdentifier(parser, "base"))
                default_ = compiler.parseExpression(parser, lexicalContext);
        }
        parser.expect("]");
        parser.commit();
        return new ASTListComprehension(iterationMode, expr, default_, joinKey, iname, varname, source, where, loc);
    }
}

void addListComprehensionMacro(MacroState macroState)
{
    macroState.addMacro(new ListComprehension);
}

macro(addListComprehensionMacro);
