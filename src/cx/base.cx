module cx.base;

import backend.base;
import cx.hash;
import cx.parser_base;
import helpers;

void assert2s(int test, string msg) { if (!test) { print(msg); assert(false); } }
void assert2s2(int test, string a, string b) { if (!test) { print(a ~ b); assert(false); } }
void assert2s3(int test, string a, string b, string c) { if (!test) { print(a ~ b ~ c); assert(false); } }
void assert2s4(int test, string a, string b, string c, string d) {
    if (!test) { print(a ~ b ~ c ~ d); assert(false); } }
void assert2s5(int test, string a, string b, string c, string d, string e) {
    if (!test) { print(a ~ b ~ c ~ d ~ e); assert(false); } }

// something that can be referenced by a name
abstract class Symbol
{
}

/**
 * A namespace is a lexical environment that supports identifier lookup.
 * Note that this means "foo", not "a.foo".
 */
abstract class Namespace
{
    Namespace parent; // lexical parent

    this(Namespace parent) { this.parent = parent; }

    // frame is a pointer to the stack frame or what was in the 'this'/'context' parameter.
    abstract Symbol lookup(string name, Expression frame) { assert(false); }

    string mangle() { return this.parent.mangle(); }
}

class LexicalContext
{
    MacroState macroState;

    this(MacroState macroState) { this.macroState = macroState; }
}

/**
 * Delineates a region of control flow in which scoped variables may be allocated and freed again.
 * Not currently used.
 */
class DeclarationScope
{
    DeclarationScope parent;

    Statement cleanup(Context context) { return null; }
}

/**
 * Delineates a region of control flow in which 'break;' and 'continue;' may be used.
 */
abstract class LoopScope : DeclarationScope
{
    abstract Statement breakFrom(Context context) { assert(false); }
    abstract Statement continueFrom(Context context) { assert(false); }
}

LoopScope findLoopScope(DeclarationScope scope_)
{
    if (!scope_) return null;
    auto loopScope = scope_.instanceOf(LoopScope);
    if (loopScope) return loopScope;
    if (scope_.parent) return findLoopScope(scope_.parent);
    return null;
}

abstract class ASTSymbol
{
    abstract Symbol compile(Context context) { assert(false); }
}

abstract class Statement
{
    abstract void emit(Generator output) { assert(false); }
    abstract void hash(Hash hash) { assert(false); }
}

abstract class Type : Symbol
{
    abstract BackendType emit(Platform platform) { assert(false); }
    abstract bool same(Type type) { assert(false); }
    abstract string repr() { assert(false); }
    abstract void hash(Hash hash) { assert(false); }

    Expression implicitConvertFrom(Expression source)
    {
        return null;
    }

    Expression implicitConvertTo(Expression source, Type target)
    {
        return null;
    }

    Expression truthy(Expression expr, Loc loc)
    {
        return null;
    }

    /**
     * Called when a scoped expression goes out of scope.
     */
    Statement endLifetime(Expression expr)
    {
        return null;
    }
}

abstract class ASTType
{
    abstract Type compile(Context context) { assert(false); }
}

class Void : Type
{
    BackendType type;
    this() { this.type = new BackendVoidType; }

    override BackendType emit(Platform platform) { return this.type; }

    override bool same(Type other) { return !!other.instanceOf(Void); }

    override string repr() { return "void"; }

    override void hash(Hash hash) { hash.adds("void"); }
}

class Pointer : Type
{
    Type target;

    this(Type target) { this.target = target; }

    override BackendType emit(Platform platform)
    {
        return new BackendPointerType(this.target.emit(platform));
    }

    override bool same(Type other)
    {
        Pointer otherPtr = other.instanceOf(Pointer);

        return otherPtr && this.target.same(otherPtr.target);
    }

    override string repr() { return this.target.repr() ~ "*"; }

    override void hash(Hash hash) { hash.adds("pointer"); target.hash(hash); }
}

class Generator
{
    BackendModule mod;

    BackendFunction fun;

    Platform platform;

    int frameReg;

    this(Platform platform, BackendModule mod)
    {
        this.platform = platform;
        this.mod = mod;
        this.fun = null;
    }

    bool once(string name)
    {
        return this.mod.once(name);
    }
}

abstract class Expression : Symbol
{
    abstract Type type() { assert(false); }
    abstract int emit(Generator output) { assert(false); }
    abstract void hash(Hash hash) { assert(false); }
}

Expression beExpression(Symbol symbol)
{
    Expression expr = symbol.instanceOf(Expression);
    assert(!!expr);
    return expr;
}

Expression beExpression2(Symbol symbol, Loc loc)
{
    Expression expr = symbol.instanceOf(Expression);
    loc.assert2s(!!expr, "expression expected");
    return expr;
}

abstract class Reference : Expression
{
    int emitLocation(Generator output) { assert(false); }
}

class RegExpr : Expression
{
    Type type_;

    int reg;

    this(Type type_, int reg) { this.type_ = type_; this.reg = reg; }

    override Type type()
    {
        return this.type_;
    }

    override int emit(Generator output)
    {
        return this.reg;
    }

    override void hash(Hash hash) { hash.adds("reg"); type_.hash(hash); hash.addl(reg); }
}

class PointerCast : Expression
{
    Type target;

    Expression value;

    this(Type target, Expression value) {
        this.target = target;
        this.value = value;
    }

    override Type type()
    {
        return this.target;
    }

    override int emit(Generator output)
    {
        int reg = this.value.emit(output);
        return output.fun.bitcast(reg, this.target.emit(output.platform));
    }

    override void hash(Hash hash) { hash.adds("ptrcast"); target.hash(hash); value.hash(hash); }
}

struct Argument
{
    string name;

    Type type;
}

class FunctionDeclaration : Symbol
{
    string name;

    Type ret;

    Argument[] args;

    Type thisType; // must be a pointer

    this(string name, Type ret, Argument[] args)
    {
        this.name = name;
        this.ret = ret;
        this.args = args;
        this.thisType = null;
    }

    size_t numArgs()
    {
        if (!!thisType) return this.args.length + 1;
        return this.args.length;
    }

    string mangle() { return this.name; } // TODO extern

    void declare(Generator generator)
    {
        if (generator.mod.declared(this.mangle()))
            return;

        Type voidp = new Pointer(new Void);
        int extra = 0;
        if (this.thisType) extra = 1;
        auto argTypes = new BackendType[](this.numArgs());
        if (this.thisType) argTypes[0] = voidp.emit(generator.platform);
        for (int i = 0; i < this.args.length; i += 1)
            argTypes[extra + i] = this.args[i].type.emit(generator.platform);

        generator.mod.declare(
            this.mangle(),
            this.ret.emit(generator.platform),
            argTypes
        );
    }
}

/**
 * This is in base because it's circular with Context.
 */
class MacroState
{
    Macro[] macros;

    MacroState[] imports;

    this() { }

    MacroState dup() {
        auto newState = new MacroState;
        newState.macros = new Macro[](this.macros.length);
        for (int i = 0; i < this.macros.length; i += 1)
            newState.macros[i] = this.macros[i];
        newState.imports = new MacroState[](this.imports.length);
        for (int i = 0; i < this.imports.length; i += 1)
            newState.imports[i] = this.imports[i];
        return newState;
    }

    void applyMacro(MacroArgs args) {
        this.applyMacroExported(args);
        if (args.done()) return;
        for (int i = 0; i < this.imports.length; i += 1) {
            this.imports[i].applyMacroExported(args);
            if (args.done()) return;
        }
    }

    void applyMacroExported(MacroArgs args) {
        for (int i = 0; i < this.macros.length; i += 1) {
            this.macros[i].apply(args);
            if (args.done()) return;
        }
    }

    void addImport(MacroState target) {
        this.imports ~= target;
    }

    void addMacro(Macro macro_) {
        this.macros ~= macro_;
    }
}

struct Context
{
    // all members should be treated as immutable

    Platform platform;

    Namespace namespace;

    DeclarationScope scope_;

    MacroState macroState;

    int* idCounter;

    Context withNamespace(Namespace namespace)
    {
        return Context(this.platform, namespace, this.scope_, this.macroState, this.idCounter);
    }

    Context withDeclScope(DeclarationScope scope_)
    {
        return Context(this.platform, this.namespace, scope_, this.macroState, this.idCounter);
    }

    string getLabel() { int i = *this.idCounter; *this.idCounter += 1; return "uniq" ~ itoa(i); }
}

abstract class Macro
{
    abstract void apply(MacroArgs args) { assert(false); }
}

// macro subclasses are differentiated by args
abstract class MacroArgs
{
    abstract bool done() { assert(false); }
}

// hook the ASTCall (with ASTIdentifier) -> Call transform
class CallMacroArgs : MacroArgs
{
    string name;
    ASTSymbol[] symbols;
    Context context;
    Loc loc;
    Expression transformed; // out
    this(string name, ASTSymbol[] symbols, Context context, Loc loc) {
        this.name = name;
        this.symbols = symbols;
        this.context = context;
        this.loc = loc;
        this.transformed = null;
    }
    override bool done() { return !!this.transformed; }
}

// hook parseExpressionBase
class ParseExpressionBaseArgs : MacroArgs
{
    Parser parser;
    LexicalContext lexicalContext;
    ASTSymbol symbol; // out
    this(Parser parser, LexicalContext lexicalContext) {
        this.parser = parser;
        this.lexicalContext = lexicalContext;
        this.symbol = null;
    }
    override bool done() { return !!this.symbol; }
}

// hook parseProperties
class ParsePropertyArgs : MacroArgs
{
    Parser parser;
    LexicalContext lexicalContext;
    ASTSymbol left;
    ASTSymbol result; // out
    this(Parser parser, LexicalContext lexicalContext, ASTSymbol left) {
        this.parser = parser;
        this.lexicalContext = lexicalContext;
        this.left = left;
        this.result = null;
    }
    override bool done() { return !!this.result; }
}

/**
 * Helper class to break dependency cycles.
 */
class Compiler
{
    // parser
}

class Package
{
    string name;
    string path;
    Package[] deps;

    this(string name, string path) {
        this.name = name;
        this.path = path;
    }

    void addDependency(Package dep) {
        this.deps ~= dep;
    }
}

class BasicObjectCache : IBasicObjectCache
{
    this(ASTType astvoid, ASTType astchar, ASTType astshort,
         ASTType astint, ASTType astlong, ASTType astfloat) {
        this.astvoid = astvoid;
        this.astchar = astchar;
        this.astshort = astshort;
        this.astint = astint;
        this.astlong = astlong;
        this.astfloat = astfloat;
    }
    ASTType astvoid;
    ASTType astchar;
    ASTType astshort;
    ASTType astint;
    ASTType astlong;
    ASTType astfloat;
}
