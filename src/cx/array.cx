module cx.array;

macro import cx.macros.hash;
macro import cx.macros.once;
macro import cx.macros.quasiquoting;

import backend.base;
import cx.base;
import cx.hash;
import cx.parser_base;
import cx.types;
import helpers;

/**
 * There are two things called "array": T[] and the data structure it references.
 * We will call the first an "array value" and the second the "array object".
 *
 * Array values have the following layout:
 * {
 *      size_t length;
 *      T* ptr;
 *      T* base;
 * }
 *
 * Array objects have the following layout:
 * {
 *      size_t references;
 *      size_t capacity; // total length
 *      size_t used; // length in use (used for realloc)
 * }
 * They are then followed directly by the array data.
 */
class Array : Type
{
    Type elementType;

    BackendType backendType;

    this(this.elementType) { assert(!!this.elementType); }

    override BackendType emit(Platform platform)
    {
        if (!this.backendType)
        {
            BackendType[] types = new BackendType[](3);
            types[0] = platform.nativeWordType;
            types[1] = new BackendPointerType(this.elementType.emit(platform));
            types[2] = platform.voidp;
            this.backendType = new BackendStructType(types);
        }
        return this.backendType;
    }

    override bool same(Type other)
    {
        Array otherArray = other.instanceOf(Array);

        return otherArray && otherArray.elementType.same(this.elementType);
    }

    override string repr() { return this.elementType.repr ~ "[]"; }

    override string mangle() { return "array_" ~ this.elementType.mangle; }

    override void hash(Hash hash) { hash.adds("array"); this.elementType.hash(hash); }

    override Statement copyInto(Context context, Reference target, Expression source)
    {
        auto compiler = context.compiler;

        Statement assign(Expression source) {
            mut auto context = context.withNamespace(context.compiler.exprAlias(context.namespace, "source", source));
            auto refCountIncFun = once new FunctionDeclaration(
                "cxruntime_refcount_inc", new Void, [
                    Parameter(false, "label", false, new Array(new Character)),
                    Parameter(false, "ptr", false, new Pointer(new Void))]);
            context = context.withNamespace(context.compiler.exprAlias(
                        context.namespace, "cxruntime_refcount_inc", refCountIncFun));
            // count up reference
            auto stmt = compiler.$stmt if (source.base) {
                cxruntime_refcount_inc("array acquire", source.base);
            };
            auto result = stmt.compile(context);
            return compiler.sequenceStatement([
                result.statement,
                compiler.assignStatement(target, source)]);
        }
        return compiler.stmtWithTemporaryExpr(source, &assign);
    }

    override Statement endLifetime(Context context, Reference reference)
    {
        return context.compiler.exprStatement(releaseArray(context, reference));
    }

    override Symbol accessMember(Loc loc, Context context, Expression base, string field)
    {
        if (field == "length")
            return new ArrayLength(base, context.nativeWordType);
        if (field == "ptr")
            return new ArrayPointer(this.elementType, base);
        if (field == "base")
            return new ArrayBase(base);
        return null;
    }
}

class ASTArray : ASTSymbol
{
    ASTSymbol elementType;

    Loc loc;

    this(this.elementType, this.loc) { }

    override Type compile(Context context)
    {
        return new Array(beType(this.loc, this.elementType.compile(context)));
    }

    override ASTSymbol quote(Quoter quoter) {
        return quoter.compilerCall("astArray", [ elementType.quote(quoter) ], loc);
    }
}

int getArrayLen(Generator output, Type arrayType, int arrayReg)
{
    return output.fun.field(arrayType.emit(output.platform), arrayReg, 0);
}

class ArrayLength : Expression
{
    Expression arrayValue;

    this(this.arrayValue, this.type) { }

    override int emit(Generator output)
    {
        int arrayReg = this.arrayValue.emit(output);

        return getArrayLen(output, this.arrayValue.type, arrayReg);
    }

    override ExprInfo info() { return ExprInfo(false, Ownership.global); }

    override void hash(Hash hash) { hash.adds("ArrayLength"); this.arrayValue.hash(hash); }
}

int getArrayPtr(Generator output, Type arrayType, int arrayReg)
{
    return output.fun.field(arrayType.emit(output.platform), arrayReg, 1);
}

class ArrayPointer : Expression
{
    Type elementType;

    Expression arrayValue;

    this(this.elementType, this.arrayValue) {
        this.type = new Pointer(this.elementType);
    }

    override int emit(Generator output)
    {
        int arrayReg = this.arrayValue.emit(output);

        return getArrayPtr(output, this.arrayValue.type, arrayReg);
    }

    override ExprInfo info() { return ExprInfo(false, Ownership.global); }

    override void hash(Hash hash) { hash.adds("ArrayPointer"); this.arrayValue.hash(hash); }
}

int getArrayBase(Generator output, Type arrayType, int arrayReg)
{
    return output.fun.field(arrayType.emit(output.platform), arrayReg, 2);
}

class ArrayBase : Expression
{
    Expression arrayValue;

    this(this.arrayValue) {
        this.type = once new Pointer(new Void);
    }

    override int emit(Generator output)
    {
        int arrayReg = this.arrayValue.emit(output);

        return getArrayBase(output, this.arrayValue.type, arrayReg);
    }

    override ExprInfo info() { return ExprInfo(false, Ownership.owned); }

    override void hash(Hash hash) { hash.adds("ArrayBase"); this.arrayValue.hash(hash); }
}

int makeArray(Generator output, Type elementType, int lenReg, int ptrReg, int baseReg)
{
    BackendType structType = (new Array(elementType)).emit(output.platform);

    return output.fun.structLiteral(structType, [lenReg, ptrReg, baseReg]);
}

// TODO use this as a basis for all array-allocating expressions
class ArrayAllocation : Expression
{
    Type elementType;

    (Expression | size_t) length;

    this(this.elementType, this.length) {
        this.type = new Array(this.elementType);
    }

    override int emit(Generator output)
    {
        auto backendSizeT = output.platform.nativeWordType;
        auto voidp = output.platform.voidp;
        auto backendElementType = this.elementType.emit(output.platform);
        int elementSizeReg = output.fun.wordLiteral(output.platform, backendElementType.size(output.platform));
        int threeWordSizeReg = output.fun.wordLiteral(output.platform, backendSizeT.size(output.platform) * 3);
        int oneReg = output.fun.wordLiteral(output.platform, 1);
        int twoReg = output.fun.wordLiteral(output.platform, 2);
        int threeReg = output.fun.wordLiteral(output.platform, 3);
        int arrayLength = this.length.case(
            Expression ex: ex.emit(output),
            size_t sz: output.fun.wordLiteral(output.platform, sz));
        int arrayBytes = output.fun.binop("*", backendSizeT, arrayLength, elementSizeReg);
        int totalArrayBytes = output.fun.binop("+", backendSizeT, arrayBytes, threeWordSizeReg);
        int arrayBase = output.fun.call(voidp, "cxruntime_alloc", [totalArrayBytes]);
        // ptr = base + 2
        int arrayPtr = output.fun.bitcast(output.fun.ptrOffset(backendSizeT, arrayBase, threeReg), voidp);
        // base[0] = refs (1)
        // base[1] = allocated length (in elements)
        // base[2] = used length (in elements, inited to allocated length)
        output.fun.store(backendSizeT, output.fun.bitcast(arrayBase, new BackendPointerType(backendSizeT)), oneReg);
        output.fun.store(backendSizeT, output.fun.ptrOffset(backendSizeT, arrayBase, oneReg), arrayLength);
        output.fun.store(backendSizeT, output.fun.ptrOffset(backendSizeT, arrayBase, twoReg), arrayLength);

        int arrayPtrBc = output.fun.bitcast(arrayPtr, new BackendPointerType(backendElementType));
        return makeArray(output, this.elementType, arrayLength, arrayPtrBc, arrayBase);
    }

    override void hash(Hash hash) {
        hash.adds("ArrayAllocation");
        this.length.case {
            Expression ex: ex.hash(hash);
            size_t sz: hash.addl(sz);
        }
    }

    override ExprInfo info() { return ExprInfo(false, Ownership.gifted); }
}

class ArraySlice : Expression
{
    Expression base;

    Expression lower;

    Expression upper;

    Loc loc;

    Array arrayType;

    this(this.base, this.lower, this.upper, this.loc) {
        auto baseType = this.base.type;
        if (baseType.instanceOf(Array)) {
            this.arrayType = baseType.instanceOf(Array);
        } else if (baseType.instanceOf(Pointer)) {
            this.arrayType = new Array(baseType.instanceOf(Pointer).target);
        } else {
            this.loc.assert2s(false, "slice of non-array/non-pointer");
        }
        this.type = this.arrayType;
    }

    override int emit(Generator output)
    {
        BackendType voidp = output.platform.voidp;

        auto elementType = this.arrayType.elementType.emit(output.platform);

        int baseReg = this.base.emit(output);
        int lowerReg = this.lower.emit(output);
        int upperReg = this.upper.emit(output);
        mut int base, ptr;
        if (this.base.type.instanceOf(Array)) {
            base = getArrayBase(output, this.arrayType, baseReg);
            ptr = getArrayPtr(output, this.arrayType, baseReg);
        } else {
            base = output.fun.bitcast(output.fun.wordLiteral(output.platform, 0), voidp);
            ptr = baseReg;
        }
        // ptr = ptr + lower
        int newPtr = output.fun.ptrOffset(elementType, ptr, lowerReg);
        // len = upper - lower
        int newLen = output.fun.binop(
            "-", output.platform.nativeWordType,
            upperReg, lowerReg);

        // TODO acquire ref
        return makeArray(output, this.arrayType.elementType, newLen, newPtr, base);
    }

    override ExprInfo info() { return base.info; }

    override void hash(Hash hash) {
        hash.adds("ArraySlice");
        base.hash(hash);
        lower.hash(hash);
        upper.hash(hash);
    }
}

Expression arrayEqual(Context context, Expression left, Expression right) {
    auto compiler = context.compiler;

    Array leftArray = left.type.instanceOf(Array);
    Array rightArray = right.type.instanceOf(Array);
    assert(leftArray && rightArray);
    assert(leftArray.same(rightArray));

    Parameter[] params = [
        Parameter(false, "left", false, leftArray),
        Parameter(false, "right", false, rightArray),
    ];

    ASTStatement body_()
    {
        return compiler.$stmt {
            if (left.length != right.length) return false;
            for (int i <- 0 .. left.length) {
                if (left[i] != right[i]) return false;
            }
            return true;
        };
    }

    auto fn = compiler.createRuntimeFunction(context, "array_eq", new Integer, params, &body_);

    return context.compiler.mkCall(fn, [left, right], __HERE__);
}

Expression releaseArray(Context context, Expression value) {
    auto compiler = context.compiler;

    Array array = value.type.instanceOf(Array);
    assert(!!array);
    Parameter[] params = [Parameter(false, "value", false, array)];

    ASTStatement body_()
    {
        return compiler.$stmt {
            if (!value.base) return;
            if (!cxruntime_refcount_dec("array release", value.base))
                return;
            for (int i <- 0 .. value.length) {
                __destroy(value[i]);
            }
            free(value.base);
        };
    }

    auto fn = compiler.createRuntimeFunction(context, "arrayrelease", new Void, params, &body_);

    return context.compiler.mkCall(fn, [value], __HERE__);
}

Expression arrayCat(Context context, Expression left, Expression right, Loc loc) {
    auto compiler = context.compiler;

    Array leftArray = left.type.instanceOf(Array);
    assert(!!leftArray);
    Parameter[] params = [
        Parameter(false, "left", false, leftArray),
        Parameter(false, "right", false, right.type)];

    ASTStatement body_() {
        if (leftArray.elementType.same(right.type) || leftArray.elementType.implicitConvertFrom(right)) {
            return compiler.$stmt {
                if (left.base) {
                    size_t prevCap = (cast(size_t*) left.base)[1];
                    size_t* prevUsed = &(cast(size_t*) left.base)[2];
                    auto prevFrontier = &(cast(typeof(left.ptr)) &(cast(size_t*) left.base)[3])[*prevUsed];
                    if (&left.ptr[left.length] is prevFrontier && *prevUsed < prevCap) {
                        *prevUsed += 1;
                        prevFrontier[0] = right;
                        return left[0 .. $ + 1];
                    }
                }
                auto result = new typeof(left)(left.length * 2 + 1);
                for (size_t i <- 0 .. left.length)
                    result[i] = left[i];
                result[left.length] = right;
                // used
                (cast(size_t*) result.base)[2] = left.length + 1;
                return result[0 .. left.length + 1];
            };
        } else if (leftArray.same(right.type)) {
            return compiler.$stmt {
                auto newLength = left.length + right.length;
                if (left.base) {
                    size_t prevCap = (cast(size_t*) left.base)[1];
                    size_t* prevUsed = &(cast(size_t*) left.base)[2];
                    auto prevFrontier = &(cast(typeof(left.ptr)) &(cast(size_t*) left.base)[3])[*prevUsed];
                    if (&left.ptr[left.length] is prevFrontier && *prevUsed + right.length <= prevCap) {
                        *prevUsed += right.length;
                        for (size_t i <- 0 .. right.length)
                            prevFrontier[i] = right[i];
                        return left[0 .. newLength];
                    }
                }
                auto result = new typeof(left)(left.length * 2 + right.length);
                for (size_t i <- 0 .. left.length)
                    result[i] = left[i];
                for (size_t i <- 0 .. right.length)
                    result[left.length + i] = right[i];
                (cast(size_t*) result.base)[2] = newLength;
                return result[0 .. newLength];
            };
        } else {
            loc.fail("incompatible types for concat: " ~ leftArray.repr ~ " and " ~ right.type.repr);
        }
    }
    auto fn = compiler.createRuntimeFunction(context, "arrayappend", leftArray, params, &body_);
    auto astFn = new ASTSymbolHelper(fn);
    auto astLeft = new ASTSymbolHelper(left);
    auto astRight = new ASTSymbolHelper(right);

    return compiler.call(context, astFn, [ASTArgument(astLeft, ""), ASTArgument(astRight, "")], loc, false);
}

ASTSymbol parseArrayLiteral(Parser parser, LexicalContext lexicalContext)
{
    parser.begin;
    auto loc = parser.loc;
    if (!parser.accept("[")) {
        parser.revert;
        return null;
    }
    mut ASTArrayLiteralElement[] values;
    while (!parser.accept("]")) {
        auto loc = parser.loc;
        ASTSymbol value = lexicalContext.compiler.parseExpression(parser, lexicalContext);
        values ~= ASTArrayLiteralElement(value, loc);
        parser.accept(",");
    }
    parser.commit;
    return new ASTArrayLiteral(values);
}

// marker: type that converts to any array type.
// type of [].
class EmptyArrayType : Type
{
    this() { }

    override BackendType emit(Platform platform) {
        assert(false);
    }

    override bool same(Type other) {
        return !!other.instanceOf(EmptyArrayType);
    }

    override Expression implicitConvertTo(Context context, Expression source, Type target)
    {
        if (target.instanceOf(Array)) {
            return new EmptyArrayLiteral(target);
        }
        return null;
    }

    override string repr() { return "typeof([])"; }

    override void hash(Hash hash) { hash.adds("EmptyArrayType"); }
}

class EmptyArrayLiteral : Expression
{
    Type type_; // may be null

    this(this.type_) {
        this.type = this.type_;
        if (!this.type) {
            this.type = new EmptyArrayType;
        }
    }

    override int emit(Generator generator) {
        assert(!!this.type_);
        auto elementType = this.type_.instanceOf(Array).elementType;
        auto voidp = generator.platform.voidp;
        auto elementp = new BackendPointerType(elementType.emit(generator.platform));

        int len = generator.fun.wordLiteral(generator.platform, 0);
        int ptr = generator.fun.bitcast(generator.fun.intLiteral(0), elementp);
        int base = generator.fun.bitcast(generator.fun.intLiteral(0), voidp);

        return makeArray(generator, elementType, len, ptr, base);
    }

    override ExprInfo info() { return ExprInfo(false, Ownership.owned); }

    override void hash(Hash hash) {
        hash.adds("EmptyArrayLiteral");
        if (this.type_)
            this.type_.hash(hash);
	else
	    hash.adds(" generic");
    }
}

struct ASTArrayLiteralElement
{
    ASTSymbol value;

    Loc loc;
}

class ASTArrayLiteral : ASTSymbol
{
    ASTArrayLiteralElement[] elements;

    this(this.elements) { }

    override Expression compile(Context context) {
        if (!elements.length)
            return new EmptyArrayLiteral(null);
        mut Expression[] values;
        for (auto element <- this.elements)
            values ~= beExpression3(context, element.value.compile(context), element.loc);
        mut Expression mergedReference;
        for (int i <- 0 .. this.elements.length) {
            if (!mergedReference) mergedReference = values[i];
            else {
                // TODO centralize somewhere
                auto value = values[i];
                auto left = context.compiler.implicitConvertTo(context, value, mergedReference.type);
                auto right = context.compiler.implicitConvertTo(context, mergedReference, value.type);
                if (!left && !right)
                    this.elements[i].loc.assert2s(false, "incompatible expression type");
                else if (left && right && !left.type.same(right.type))
                    this.elements[i].loc.assert2s(false, "ambiguous expression type");
                if (left) mergedReference = left;
                else if (right) mergedReference = right;
            }
        }
        auto mergeType = mergedReference.type;
        for (int i <- 0 .. this.elements.length) {
            assert(!!values[i]);
            values[i] = context.compiler.implicitConvertTo(context, values[i], mergeType);
            assert(!!values[i]);
        }
        auto target = new ArrayAllocation(mergeType, values.length);
        Expression do_(Expression target) {
            mut Statement[] moves;
            auto base = new ArrayPointer(mergeType, target);
            for (int i <- 0 .. values.length) {
                auto target = context.compiler.dereference(
                    context.compiler.pointerOffset(base, context.compiler.intLiteral(i)));
                moves ~= context.compiler.move(context, target, values[i]);
            }
            return context.compiler.statementExpression(
                context.compiler.sequenceStatement(moves), target);
        }
        return context.compiler.exprWithTemporaryExpr(target, &do_);
    }

    override ASTSymbol quote(Quoter quoter) { print("cannot quote 'ASTArrayLiteral'"); assert(false); }
}

/**
 * $ for length in an array index context.
 */
class ASTArrayLength : ASTSymbol
{
    Loc loc;

    this(this.loc) { }

    override Symbol compile(Context context) {
        auto surroundingArraySymbol = context.compiler.lookup(context, __HERE__, "__array");
        loc.assert2s(
            surroundingArraySymbol && surroundingArraySymbol.instanceOf(Expression),
            "'$' can only be used inside an array index context");

        return new ArrayLength(
            surroundingArraySymbol.instanceOf(Expression),
            context.nativeWordType);
    }

    override ASTSymbol quote(Quoter quoter) {
        return quoter.compiler.astNewExpression(
            quoter.compiler.astIdentifier("ASTArrayLength", loc), [
                quoter.quoteLoc(loc)], loc);
    }
}
