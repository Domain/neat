module cx.array;

import backend.base;
// import backend.proxy;
import cx.base;
import cx.types;

int getArrayLen(Generator output, Type arrayType, int arrayReg)
{
    return output.fun.field(arrayType.emit(output.platform), arrayReg, 0);
}

class ArrayLength : Expression
{
    Expression arrayValue;

    Type type_;

    this(Expression arrayValue, Type type_) {
        this.arrayValue = arrayValue;
        this.type_ = type_;
    }

    override Type type()
    {
        return this.type_;
    }

    override int emit(Generator output)
    {
        int arrayReg = this.arrayValue.emit(output);

        return getArrayLen(output, this.arrayValue.type(), arrayReg);
    }
}

int getArrayPtr(Generator output, Type arrayType, int arrayReg)
{
    return output.fun.field(arrayType.emit(output.platform), arrayReg, 1);
}

class ArrayPointer : Expression
{
    Type elementType;

    Expression arrayValue;

    this(Type elementType, Expression arrayValue) { this.elementType = elementType; this.arrayValue = arrayValue; }

    override Type type()
    {
        return new Pointer(this.elementType);
    }

    override int emit(Generator output)
    {
        int arrayReg = this.arrayValue.emit(output);

        return getArrayPtr(output, this.arrayValue.type(), arrayReg);
    }
}

int makeArray(Generator output, Type elementType, int lenReg, int ptrReg)
{
    BackendType elemp = (new Pointer(elementType)).emit(output.platform);
    BackendType wordType = output.platform.nativeWordType();

    // TODO allocaless
    BackendType structType = (new Array(elementType)).emit(output.platform);
    int structReg = output.fun.alloca(structType);
    int lenField = output.fun.fieldOffset(structType, structReg, 0);
    int ptrField = output.fun.fieldOffset(structType, structReg, 1);

    output.fun.store(wordType, lenField, lenReg);
    output.fun.store(elemp, ptrField, ptrReg);
    return output.fun.load(structType, structReg);
}

class ArrayExpression : Expression
{
    Expression pointer;

    Expression length;

    this(Expression pointer, Expression length) { this.pointer = pointer; this.length = length; }

    override Type type()
    {
        Pointer ptrType = this.pointer.type().instanceOf(Pointer);
        assert(!!ptrType);
        return new Array(ptrType.target);
    }

    override int emit(Generator output)
    {
        int pointer = this.pointer.emit(output);
        int length = this.length.emit(output);

        return makeArray(output, this.type().instanceOf(Array).elementType, length, pointer);
    }
}

class ArraySlice : Expression
{
    Expression array;

    Expression lower;

    Expression upper;

    this(Expression array, Expression lower, Expression upper)
    {
        this.array = array;
        this.lower = lower;
        this.upper = upper;
    }

    override Type type() { return this.array.type(); }

    override int emit(Generator output)
    {
        BackendType voidp = new BackendPointerType(new BackendVoidType);

        Array arrayType = this.array.type().instanceOf(Array);
        assert(!!arrayType); // , "slice of non-array");
        int elementSize = arrayType.elementType.emit(output.platform).size(output.platform);

        int arrayReg = this.array.emit(output);
        int lowerReg = this.lower.emit(output);
        int upperReg = this.upper.emit(output);
        int ptr = getArrayPtr(output, arrayType, arrayReg);
        ptr = output.fun.bitcast(ptr, voidp);
        // ptr = ptr + lower
        int lowerOffset = output.fun.binop(
            "*", output.platform.nativeWordType(),
            lowerReg, output.fun.wordLiteral(output.platform, elementSize));
        int newPtr = output.fun.call(voidp, "ptr_offset", new int[](0) ~ ptr ~ lowerOffset);
        // len = upper - lower
        int newLen = output.fun.binop(
            "-", output.platform.nativeWordType(),
            upperReg, lowerReg);

        newPtr = output.fun.bitcast(newPtr, (new Pointer(arrayType.elementType)).emit(output.platform));
        return makeArray(output, arrayType.elementType, newLen, newPtr);
    }
}

class ArrayEqual : Expression
{
    Expression left;

    Expression right;

    this(Expression left, Expression right)
    {
        this.left = left;
        this.right = right;
    }

    override Type type() { return new Integer; }

    override int emit(Generator output)
    {
        Array leftArray = this.left.type().instanceOf(Array);
        Array rightArray = this.right.type().instanceOf(Array);
        assert(leftArray && rightArray);
        // TODO temp expr once array properties work on nonreferences
        assert(leftArray.same(rightArray));
        int left = this.left.emit(output);
        int right = this.right.emit(output);
        int leftLen = getArrayLen(output, leftArray, left);
        int rightLen = getArrayLen(output, rightArray, right);
        int leftPtr = getArrayPtr(output, leftArray, left);
        int rightPtr = getArrayPtr(output, rightArray, right);
        int leftSize = output.fun.wordLiteral(
            output.platform,
            leftArray.elementType.emit(output.platform).size(output.platform));
        return output.fun.call(new BackendIntType, "_arraycmp", new int[](0)
            ~ leftPtr ~ rightPtr ~ leftLen ~ rightLen ~ leftSize);
    }
}
