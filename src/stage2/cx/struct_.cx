module cx.struct_;

import backend.base;
import cx.base;

class StructMemberDeclaration
{
    string name;

    Type type;

    this(string name, Type type)
    {
        this.name = name;
        this.type = type;
    }
}

class Struct : Type
{
    string name;

    StructMemberDeclaration[] members;

    this(string name, StructMemberDeclaration[] members)
    {
        this.name = name;
        this.members = members;
    }

    override bool same(Type other) {
        Struct otherStruct = other.instanceOf(Struct);
        if (!otherStruct || otherStruct.members.length != this.members.length) return false;
        for (int i = 0; i < this.members.length; i += 1)
        {
            if (!this.members[i].type.same(otherStruct.members[i].type)) return false;
        }
        return true;
    }

    override BackendType emit(Platform platform)
    {
        auto memberTypes = new BackendType[](this.members.length);
        for (int i = 0; i < this.members.length; i += 1)
        {
            memberTypes[i] = this.members[i].type.emit(platform);
        }
        return new BackendStructType(memberTypes);
    }

    override string repr() { return this.name; }
}

class ASTStructMember
{
    string name;

    ASTType type;

    this(string name, ASTType type) { this.name = name; this.type = type; }
}

class ASTStructDecl : ASTType
{
    string name;

    ASTStructMember[] members;

    this(string name, ASTStructMember[] members) { this.name = name; this.members = members; }

    override Struct compile(Context context)
    {
        auto members = new StructMemberDeclaration[](this.members.length);

        for (int i = 0; i < this.members.length; i += 1)
            members[i] = new StructMemberDeclaration(this.members[i].name, this.members[i].type.compile(context));

        return new Struct(this.name, members);
    }
}

class StructMember : Reference
{
    Reference base;

    int index;

    this(Reference base, int index)
    {
        this.base = base;
        Struct structType = this.base.type().instanceOf(Struct);
        assert(!!structType);
        this.index = index;
    }

    override Type type()
    {
        Type type = this.base.type();
        Struct structType = type.instanceOf(Struct);
        assert(!!structType);
        return structType.members[this.index].type;
    }

    override int emit(Generator output)
    {
        int locationReg = this.emitLocation(output);

        return output.fun.load(this.type().emit(output.platform), locationReg);
    }

    override int emitLocation(Generator output)
    {
        int reg = this.base.emitLocation(output);

        return output.fun.fieldOffset(this.base.type().emit(output.platform), reg, this.index);
    }
}
