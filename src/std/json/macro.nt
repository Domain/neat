module std.json.macro;

macro import std.macro.assert;
macro import std.macro.listcomprehension;

import package(compiler).neat.base;
import package(compiler).neat.parser;

import std.json;

class JsonSyntax : Macro
{
    this() { }
    override void apply(MacroArgs args) {
        auto args = args.instanceOf(ParseExpressionBaseArgs);
        if (args) {
            args.symbol = this.parse(args.parser, args.lexicalContext);
        }
    }

    (nullable ASTSymbol | fail Error) parse(Parser parser, LexicalContext lexicalContext)
    {
        auto loc = parser.loc();
        parser.begin;
        if (!parser.acceptIdentifier("JSONValue")? || !parser.accept("(")?)
        {
            parser.revert;
            return null;
        }
        auto json = jsonParseImpl(parser.instanceOf(ParserImpl).notNull)?;
        parser.expect(")")?;
        parser.commit;
        return quoteJson(lexicalContext.compiler, json);
    }
}

ASTSymbol quoteJson(CompilerBase compiler, JSONValue v) {
    ASTSymbol jv(ASTSymbol sym) {
        return compiler.astCall(
            compiler.astIdentifier("JSONValue", __RANGE__),
            [sym],
            __RANGE__);
    }
    v.value.case {
        (:false): return compiler.astIdentifier("false", __RANGE__).jv;
        (:true): return compiler.astIdentifier("true", __RANGE__).jv;
        int i: return compiler.astIntLiteral(i, __RANGE__).jv;
        string s: return compiler.astStringLiteral(s, __RANGE__).jv;
        JSONValue[] arr: return compiler.astArrayLiteral(
            [quoteJson(compiler, a) for a in arr],
            __RANGE__).jv;
        (string key, JSONValue value)[] obj:
            return compiler.astArrayLiteral(
                [compiler.astTupleLiteral([
                    compiler.astStringLiteral(a.key, __RANGE__),
                    quoteJson(compiler, a.value)], __RANGE__)
                for a in obj], __RANGE__).jv;
    }
}

void jsonMacro(MacroState macroState)
{
    macroState.addMacro(new JsonSyntax);
}

macro(jsonMacro);

unittest
{
    auto value = JSONValue({"Hello": "World", "five": 5, "array": [6]});
    assert(value.str == "{\"Hello\": \"World\", \"five\": 5, \"array\": [6]}");
}
