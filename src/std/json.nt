module std.json;

macro import neat.macros.assert;
macro import neat.macros.listcomprehension;

import package(compiler).neat.parser;
import package(compiler).neat.parser_base;

import std.string;

/**
 * This type represents a JSON value.
 */
struct JSONValue
{
    // TODO: Write dedicated JSON parser instead of hijacking neat's parsing.
    (:false | :true | int | string | JSONValue[] | (string key, JSONValue value)[]) value;

    /**
     * Parse a JSON value from a string.
     */
    static JSONValue parse(string str) {
        auto parser = new Parser("", str);
        (JSONValue | fail Error) result() {
            auto ret = jsonParseImpl(parser)?;
            if (!parser.eof?) return parser.fail("text after json");
            return ret;
        }
        result.case {
            JSONValue value: return value;
            Error err: err.exitWithError;
        }
    }

    /**
     * Load a JSON value from a file.
     */
    static JSONValue loadFile(string file) {
        import std.file : readText;

        auto str = readText(file);
        auto parser = new Parser(file, str);
        (JSONValue | fail Error) result() {
            auto ret = jsonParseImpl(parser)?;
            if (!parser.eof?) return parser.fail("text after json");
            return ret;
        }
        result.case {
            JSONValue value: return value;
            Error err: err.exitWithError;
        }
    }

    /// Interprets a JSON value as an object.
    (string key, JSONValue value)[] object() {
        value
            .case(
                (:false): :wrongType, (:true): :wrongType, int: :wrongType, string: :wrongType, JSONValue[]: :wrongType,
                (string key, JSONValue value)[] obj: obj)
            .case {
                (:wrongType): assert(false);
                (string key, JSONValue value)[] obj: return obj;
            }
    }

    /// Returns true if the JSON value is an object.
    bool isObject() {
        value.case {
            (string key, JSONValue value)[] obj: return true;
            default: return false;
        }
    }

    /// Returns the JSON value for a key.
    JSONValue get(string key) {
        auto obj = object;
        assert([any a.key == key for a in obj]);
        return [first a.value for a in obj where a.key == key else ({ assert(false); JSONValue(:false); })];
    }

    /// Returns true if the JSON value, which must be an object, contains the key.
    bool has(string key) {
        return [any a.key == key for a in object];
    }

    /**
     * Sets the given key to a value.
     */
    void set(string key, JSONValue value) {
        auto obj = object;
        for (i, k in obj) {
            if (k.key == key) {
                this.value = obj[0 .. i] ~ (key, value) ~ obj[i + 1 .. $];
                return;
            }
        }
        this.value = object ~ (key, value);
    }

    /// Given the value is an object, returns an array of string keys.
    string[] keys() {
        return [a.key for a in object];
    }
}

unittest
{
    auto value = JSONValue([("Hello", JSONValue("World"))]);
    assert(value.str == "{\"Hello\": \"World\"}");
    assert(JSONValue.parse("{\"Hello\": \"World\"}").str == "{\"Hello\": \"World\"}");
    // TODO
    // auto value = JSONValue({ "Hello": "World" });
}

/// For internal (re)use!
(JSONValue | fail Error) jsonParseImpl(Parser parser) {
    if (parser.accept("\"")?) {
        return JSONValue(parseStringLiteral(parser)?);
    }
    parser.parseNumber?.case {
        (:failure): {}
        int value: return JSONValue(value);
    }
    if (parser.accept("[")?) {
        mut JSONValue[] entries;
        if (!parser.accept("]")?) while (true) {
            entries ~= jsonParseImpl(parser)?;
            if (parser.accept("]")?) break;
            parser.expect(",")?;
        }
        return JSONValue(entries);
    }
    if (parser.accept("{")?) {
        mut (string key, JSONValue val)[] entries;
        if (!parser.accept("}")?) while (true) {
            parser.expect("\"")?;
            auto key = parseStringLiteral(parser)?;
            parser.expect(":")?;
            auto value = jsonParseImpl(parser)?;
            entries ~= (key, value);
            if (parser.accept("}")?) break;
            parser.expect(",")?;
        }
        return JSONValue(entries);
    }
    return parser.fail("unexpected input: " ~ parser.text);
}

private (string | fail Error) parseStringLiteral(Parser parser)
{
    mut int matchLen;
    auto loc = parser.loc;
    string start = parser.text;
    while (parser.text[0 .. 1] != "\"") {
        if (parser.text.length == 0) {
            return parser.fail("expected end of string, got end of file");
        }
        if (parser.text[0 .. 1] == "\\") {
            matchLen = matchLen + 1;
            parser.drop(1);
        }
        matchLen = matchLen + 1;
        parser.drop(1);
    }
    string str = start[0 .. matchLen];
    if (!parser.accept("\"")?) {
        return parser.fail("this should never happen");
    }

    return replaceEscapes(str);
}

private (:failure | int | fail Error) parseNumber(Parser parser)
{
    parser.begin;
    mut bool negative = parser.accept("-")?;
    if (parser.accept("-")?)
        negative = true;
    parser.strip?;
    if (parser.hard_eof || !isDigit(parser.text[0]))
    {
        parser.revert;
        return :failure;
    }
    mut string number;
    while (!parser.hard_eof && isDigit(parser.text[0]))
    {
        number ~= parser.text[0];
        parser.drop(1);
    }
    parser.commit;
    mut int i = atoi(number);
    if (negative) i = -i;
    return i;
}

/**
 * Converts a JSON value to a string.
 */
string str(JSONValue jv) {
    jv.value.case {
        (:false): return "false";
        (:true): return "true";
        int i: return itoa(i);
        string s: return quote(s);
        JSONValue[] array:
            return "[" ~ [join with ", " v.str for v in array] ~ "]";
        (string key, JSONValue value)[] obj:
            return "{" ~ [join with ", " quote(e.key) ~ ": " ~ e.value.str for e in obj] ~ "}";
    }
}

private string quote(string s) {
    mut string quoted;
    for (ch in s) {
        if (ch == "\""[0]) quoted ~= "\\\"";
        else quoted ~= ch;
    }
    return "\"" ~ quoted ~ "\"";
}

private string replaceEscapes(string text) {
    mut string result;
    mut int i;
    while (i < text.length) {
        string ch = text[i .. i + 1];
        i += 1;
        if (ch == "\\") {
            string ctl = text[i .. i + 1];
            i += 1;
            if (ctl == "\"") {
                result ~= "\"";
            } else if (ctl == "\\") {
                result ~= "\\";
            } else {
                print("Unknown control sequence \\" ~ ctl);
                assert(false);
            }
        } else {
            result ~= ch;
        }
    }
    return result;
}
