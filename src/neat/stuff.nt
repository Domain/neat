// TODO distribute into more appropriate modules
module neat.stuff;

macro import neat.macros.hash;
macro import neat.macros.listcomprehension;
macro import neat.macros.once;
macro import neat.macros.quasiquoting;

import backend.base;
import helpers;
import neat.array;
import neat.base;
import neat.class_;
import neat.decl;
import neat.either;
import neat.enums;
import neat.expr;
import neat.formatstring;
import neat.function_;
import neat.hash;
import neat.hashmap;
import neat.parser;
import neat.pragmas;
import neat.parser_base;
import neat.statements;
import neat.struct_;
import neat.templ;
import neat.traits;
import neat.tuples;
import neat.types;
import neat.unittest_;
import neat.vectors;
import neat.with_;
import std.thread;

// TODO clear all this up
class DefineClassIntf : FinishedSymbol
{
    (Class | Intf) obj;

    this(this.obj) { }

    override void emit(Generator generator)
    {
        obj.case {
            Class class_: class_.define(generator);
            Intf intf: intf.define(generator);
        }
    }

    override void hash(Hash hash)
    {
        this.obj.case {
            Class class_: {
                // FIXME nested function mangling
                void mangle1(typeof(class_.vtable[0]) entry) {
                    entry.case {
                        ClassMethod method: hash.adds(method.mangle);
                        (:baseOffset, size_t offs): hash.addl(offs);
                    }
                }
                // only the actual vtable symbol, the methods are also hashed separately
                [mangle1(method) for method in class_.vtable];
            }
            Intf intf: {
                void mangle2(typeof(intf.vtable[0]) entry) {
                    entry.case {
                        IntfMethod method: hash.adds(method.mangle);
                        (:baseOffset): hash.adds("baseOffset");
                    }
                }
                // only the actual vtable symbol, the methods are also hashed separately
                [mangle2(method) for method in intf.vtable];
            }
        }
    }
}

nullable ASTSymbol parseType(Parser parser, LexicalContext lexicalContext)
{
    mut auto current <- parser.parseLeafType(lexicalContext);
    while (true)
    {
        auto loc = parser.loc;
        if (parser.accept("*"))
        {
            current = new ASTPointer(current, loc);
            continue;
        }
        parser.begin;
        string identifier = parser.parseIdentifier;
        if (identifier == "function" || identifier == "delegate")
        {
            parser.expect("(");
            mut ASTSymbol[] args;
            while (!parser.accept(")"))
            {
                if (args.length)
                {
                    if (!parser.accept(","))
                    {
                        parser.fail("',' or ')' expected");
                    }
                }
                ASTSymbol argType = parser.parseType(lexicalContext).notNull;

                args ~= argType;
            }
            parser.commit;
            if (identifier == "function")
                current = new ASTFunctionPointer(current, args, loc);
            else
                current = new ASTNestedFunctionPointer(current, args, loc);
            continue;
        }
        parser.revert;
        parser.begin;
        if (parser.accept("[")) {
            if (parser.accept("]")) {
                parser.commit;

                current = new ASTArray(current, parser.loc);
                continue;
            }
            // hashmap? might be assignment intro
            auto keyType = parser.parseType(lexicalContext);
            if (!keyType || !parser.accept("]")) {
                parser.revert;
                return null;
            }
            parser.commit;
            current = new ASTHashMap(key=keyType.notNull, value=current, parser.loc);
            continue;
        }
        if (parser.accept("[") && parser.accept("]"))
        {
        }
        parser.revert;
        // what about templates that don't contain types? instead of duplicating this,
        // there should be a function to parse an ast symbol and that symbol should
        // be able to compile to a Type (Symbol).
        // But for now, templates only have types.
        // (this will also be needed to have things like Foo.Bar for nested types.)
        {
            parser.begin;
            auto instanceLoc = parser.loc;
            // TODO better way to exclude all the negated operators
            if (parser.accept("!") && !parser.accept("=") && !acceptIdentifier(parser, "is"))
            {
                mut ASTSymbol[] templateArgs;
                void getTemplateArg() {
                    auto templateArg = parser.parseType(lexicalContext);
                    parser.assert_(!!templateArg, "template argument expected");
                    templateArgs ~= templateArg.notNull;
                }

                if (parser.accept("(")) {
                    // ( [a [, a]*]? )
                    if (!parser.accept(")")) {
                        getTemplateArg;
                        while (!parser.accept(")")) {
                            parser.expect(",");
                            getTemplateArg;
                        }
                    }
                } else {
                    getTemplateArg;
                }
                parser.commit;
                current = new ASTTemplateInstance(current, templateArgs, instanceLoc);
                continue;
            }
            parser.revert;
        }
        return current;
    }
}

/**
 * Tuples:
 * - (int, float): int and float
 * - (int | float): int or float
 * - (int, int | int): (int and int) or int
 */
nullable ASTSymbol parseTupleType(Parser parser, LexicalContext lexicalContext)
{
    parser.begin;
    auto loc = parser.loc;
    if (!parser.accept("(")) {
        parser.revert;
        return null;
    }
    mut ASTEitherDeclEntry[] declEntries;
    mut (string name, Loc loc, ASTSymbol sym)[] members;
    mut bool fail = false;
    if (parser.accept("fail")) fail = true;
    void flush() {
        assert(members.length > 0);

        if (members.length == 1 && members[0].name == "") {
            declEntries ~= ASTEitherDeclEntry(members[0].loc, fail, members[0].sym);
        } else {
            declEntries ~= ASTEitherDeclEntry(
                members[0].loc,
                fail,
                new ASTTupleType(loc, [(a.name, a.sym) for a in members]));
        }
        members = [];
        fail = false;
    }
    while (!parser.accept(")"))
    {
        if (members.length) {
            mut bool tuplesep, eithersep;
            if (parser.accept(",")) tuplesep = true;
            else if (parser.accept("|")) eithersep = true;
            else {
                // be a bit lenient for (bla bla)
                if (members.length == 1) {
                    parser.revert;
                    return null;
                }
                loc.assert2s(false, "tuple: ',' or '|' expected");
            }
            if (eithersep) {
                flush;
                if (parser.accept("fail")) fail = true;
            }
        }
        auto loc = parser.loc;
        auto type = parser.parseType(lexicalContext);
        if (!type) {
            // (bla bla) still
            if (members.length <= 1) {
                parser.revert;
                return null;
            }
            loc.assert2s(false, "tuple: type expected");
        }
        string name = parser.parseIdentifier;
        members ~= (name, loc, type.notNull);
    }
    flush;
    parser.commit;
    if (declEntries.length == 1) return declEntries[0].symbol;
    return new ASTEitherDecl(declEntries);
}

nullable ASTSymbol parseLeafType(Parser parser, LexicalContext lexicalContext)
{
    import std.string : startsWith;

    // FIXME parser.startLoc
    auto loc = parser.loc;
    if (parser.acceptIdentifier("nullable")) {
        auto next = parseLeafType(parser, lexicalContext).notNull;
        return new ASTNullable(loc, next, true);
    }
    if (parser.acceptIdentifier("nonnullable")) {
        auto next = parseLeafType(parser, lexicalContext).notNull;
        return new ASTNullable(loc, next, false);
    }

    {
        auto typeMacroArgs = new TypeMacroArgs(parser, lexicalContext);
        lexicalContext.macroState.applyMacro(typeMacroArgs);
        if (ASTSymbol ret = typeMacroArgs.astType) return ret;
    }

    // must be before any () parsing for types we may add.
    if (auto symbol = parser.parseTupleType(lexicalContext))
        return symbol;

    auto loc = parser.loc;
    parser.begin;

    if (parser.acceptIdentifier("Vector") && parser.accept("(")) {
        auto element = lexicalContext.compiler.parseType(parser, lexicalContext);
        parser.assert_(!!element, "element type expected");
        parser.expect(",");
        auto count = lexicalContext.compiler.parseExpression(parser, lexicalContext);
        parser.assert_(count && count.instanceOf(ASTIntLiteral), "integer expected");
        parser.expect(")");
        parser.commit;
        return new ASTVectorType(loc, element.notNull, count.instanceOf(ASTIntLiteral).value);
    }

    if (parser.accept("typeof"))
    {
        parser.expect("(");
        auto expr = lexicalContext.compiler.parseExpression(parser, lexicalContext);
        if (!expr) parser.fail("expression expected");
        parser.expect(")");
        parser.commit;
        return new ASTTypeof(expr.notNull, loc);
    }

    {
        string symbolIdentifier = parser.parseIdentifier2(":", allowKeywords=false);
        if (symbolIdentifier.startsWith(":")) {
            parser.commit;
            return new ASTSymbolIdentifier(symbolIdentifier[1 .. $]);
        }
        parser.revert;
        parser.begin;
    }

    string identifier = parser.parseIdentifier;

    if (!identifier.length)
    {
        parser.revert;
        return null;
    }

    auto loc = __HERE__;
    if (identifier == "long")
    {
        parser.commit;
        return once new ASTBasicType("long", loc);
    }

    if (identifier == "int")
    {
        parser.commit;
        return once new ASTBasicType("int", loc);
    }

    if (identifier == "short")
    {
        parser.commit;
        return once new ASTBasicType("short", loc);
    }

    if (identifier == "char")
    {
        parser.commit;
        return once new ASTBasicType("char", loc);
    }

    if (identifier == "ubyte")
    {
        parser.commit;
        return once new ASTBasicType("ubyte", loc);
    }

    if (identifier == "bool")
    {
        parser.commit;
        return once new ASTBasicType("bool", loc);
    }

    if (identifier == "void")
    {
        parser.commit;
        return once new ASTBasicType("void", loc);
    }

    if (identifier == "float")
    {
        parser.commit;
        return once new ASTBasicType("float", loc);
    }

    if (identifier == "double")
    {
        parser.commit;
        return once new ASTBasicType("double", loc);
    }

    parser.commit;
    return lexicalContext.compiler.astIdentifier(identifier, parser.loc);
}

class FuncPtrCall : Expression
{
    Expression funcPtr;

    Expression[] args;

    Loc loc;

    this(this.funcPtr, this.args, this.loc) {
        this.type = this.funcPtr.type.instanceOf(FunctionPointer).ret;
    }

    override int emit(Generator output)
    {
        FunctionPointer type = this.funcPtr.type.instanceOf(FunctionPointer).notNull;

        this.loc.assert2s4(
            type.params.length == this.args.length,
            "expected ", ltoa(type.params.length), " args, not ", ltoa(this.args.length));

        int[] regs = new int[](this.args.length);
        for (i, arg in this.args)
        {
            regs[i] = arg.emit(output);
        }
        return output.fun.callFuncPtr(
            type.emit(output.platform), this.funcPtr.emit(output), regs);
    }

    // we take over the returned value
    override ExprInfo info() { return ExprInfo(false, Ownership.gifted); }

    override void hash(Hash hash) {
        hash.adds("FuncPtrCall");
        funcPtr.hash(hash);
        hash.addl(args.length);
        [arg.hash(hash) for arg in args];
    }
}

class DelegateExpr : Expression
{
    Expression funcPtr;

    Expression thisPtr;

    this(this.funcPtr, this.thisPtr) {
        FunctionPointer type = this.funcPtr.type.instanceOf(FunctionPointer).notNull;

        // drop thisptr - implicit!
        assert(type.params.length >= 1);
        // TODO this smells.
        this.type = new NestedFunctionPointer(type.ret, [a.type for a in type.params[1 .. $]]);
    }

    override int emit(Generator output)
    {
        int thisReg = this.thisPtr.emit(output);
        int funcReg = this.funcPtr.emit(output);
        auto voidp = output.platform.voidp;
        auto thisType = this.type.emit(output.platform);
        int nullReg = output.fun.bitcast(output.fun.wordLiteral(output.platform, 0), voidp);
        return output.fun.structLiteral(thisType, [nullReg, thisReg, funcReg]);
    }

    override ExprInfo info() { return ExprInfo(false, Ownership.owned); }

    override void hash(Hash hash) { hash.adds("DelegateExpr"); this.funcPtr.hash(hash); this.thisPtr.hash(hash); }
}

class DelegateDataPtr : Expression
{
    Expression dg;

    this(this.dg) {
        this.type = once new Pointer(new Void);
    }

    override int emit(Generator output)
    {
        int reg = this.dg.emit(output);

        return output.fun.field(this.dg.type.emit(output.platform), reg, 1);
    }

    override ExprInfo info() { return ExprInfo(false, Ownership.owned); }

    override void hash(Hash hash) { hash.adds("DelegateDataPtr"); this.dg.hash(hash); }
}

class DelegateFuncPtr : Expression
{
    Expression dg;

    this(this.dg) {
        auto nestfp = this.dg.type.instanceOf(NestedFunctionPointer);
        assert(!!nestfp);
        auto params = [Parameter.fromType(once new Pointer(new Void))]
            ~ [Parameter.fromType(a) for a in nestfp.params];
        this.type = new FunctionPointer(nestfp.ret, params);
    }

    override int emit(Generator output)
    {
        int reg = this.dg.emit(output);

        return output.fun.field(this.dg.type.emit(output.platform), reg, 2);
    }

    override ExprInfo info() { return ExprInfo(false, Ownership.owned); }

    override void hash(Hash hash) { hash.adds("DelegateFuncPtr"); this.dg.hash(hash); }
}

/**
 * "Plain" call without lifetime handling.
 */
nullable Expression plainCall(Context context, Symbol target, mut Expression[] args, Loc loc, bool isImplCall)
{
    if (auto fundecl = target.instanceOf(FunctionDeclaration))
    {
        // skip variadic args
        args = [
            expectImplicitConvertTo(context, arg, fundecl.params[i].type, loc)
            for i, arg in args[0 .. fundecl.params.length]] ~ args[fundecl.params.length .. $];
        return new Call(fundecl, args, loc);
    }
    if (ClassMethodPtr method = target.instanceOf(ClassMethodPtr))
    {
        if (auto funcptr = method.funcPtr.type.instanceOf(FunctionPointer)) {
            // this ptr
            if (args.length != funcptr.params.length - 1)
                loc.fail("wrong number of arguments: expected $(funcptr.params.length - 1) but got $(args.length)");
            args = [expectImplicitConvertTo(context, arg, funcptr.params[i+1].type, loc) for i, arg in args];
        } else {
            loc.fail("function reference or funcptr expected? but is '$(method.funcPtr.type.repr)'");
        }
        nullable Expression doPtr(Expression thisPtr) {
            auto voidp = once new Pointer(new Void);
            mut Expression[] combinedArgs;
            combinedArgs ~= new PointerCast(voidp, thisPtr);
            combinedArgs ~= args;
            return new FuncPtrCall(method.funcPtr, combinedArgs, loc);
        }
        nullable Expression doRef(Reference thisValue) {
            return doPtr(context.compiler.reference(thisValue));
        }
        if (method.valueIsPtr) {
            return context.compiler.exprWithTemporaryExpr(method.thisValue, &doPtr);
        } else if (auto ref_ = method.thisValue.instanceOf(Reference)) {
            auto ptr = context.compiler.reference(ref_);
            return context.compiler.exprWithTemporaryExpr(ptr, &doPtr);
        } else {
            return context.compiler.exprWithTemporary(method.thisValue, &doRef);
        }
    }
    if (isImplCall) return null;
    nullable Expression expr = target.instanceOf(Expression);
    // TODO
    // auto funcptr = expr?.type.instanceOf(FunctionPointer);
    mut nullable FunctionPointer funcptr;
    if (expr) funcptr = expr.type.instanceOf(FunctionPointer);
    if (expr && funcptr)
    {
        args = [expectImplicitConvertTo(context, arg, funcptr.params[i].type, loc) for i, arg in args];
        return new FuncPtrCall(expr.notNull, args, loc);
    }
    mut nullable NestedFunctionPointer nestedfuncptr;
    if (expr) nestedfuncptr = expr.type.instanceOf(NestedFunctionPointer);
    if (expr && nestedfuncptr)
    {
        auto expr = expr.notNull;
        args = [expectImplicitConvertTo(context, arg, nestedfuncptr.params[i], loc) for i, arg in args];
        // TODO cache delegate
        auto funcPtr = new DelegateFuncPtr(expr);
        auto thisPtr = new DelegateDataPtr(expr);
        mut Expression[] combinedArgs;
        combinedArgs ~= thisPtr;
        combinedArgs ~= args;
        return new FuncPtrCall(funcPtr, combinedArgs, loc);
    }
    loc.assert2s(false, "unknown call target");
}

class WrapExpression : Expression
{
    nullable Statement pre;
    Expression value;
    nullable Statement post;

    this(this.pre, this.value, this.post) {
        this.type = value.type;
    }

    override int emit(Generator generator)
    {
        if (pre) pre.emit(generator);
        int result = value.emit(generator);
        if (post) post.emit(generator);
        return result;
    }

    override ExprInfo info() { return value.info; }

    override void hash(Hash hash) {
        hash.adds("WrapExpression");
        if (pre) pre.hash(hash);
        else hash.adds("null");
        value.hash(hash);
        if (post) post.hash(hash);
        else hash.adds("null");
    }
}

/**
 * This type is a bit risky.
 * Because result is returned after post() is called,
 * `WrapReference` can leak the location of a value that
 * has no references. As such, it should only be used when
 * `reference` doesn't yield an address managed by `pre`/`post`.
 */
class WrapReference : Reference
{
    nullable Statement pre;
    Reference reference;
    nullable Statement post;

    this(this.pre, this.reference, this.post) {
        this.type = reference.type;
    }

    override int emit(Generator output)
    {
        int reg = this.emitLocation(output);

        return output.fun.load(this.type.emit(output.platform), reg);
    }

    override int emitLocation(Generator output)
    {
        if (pre) pre.emit(output);
        int result = reference.emitLocation(output);
        if (post) post.emit(output);
        return result;
    }

    override ExprInfo info() { return reference.info; }

    override void hash(Hash hash) {
        hash.adds("WrapReference");
        if (pre) pre.hash(hash);
        else hash.adds("null");
        reference.hash(hash);
        if (post) post.hash(hash);
        else hash.adds("null");
    }
}

nullable Expression call(Context context, ASTSymbol astTarget, ASTArgument[] astArgs, Loc loc, bool autoCall) {
    if (!context.namespace) return null;
    if (!autoCall) {
        auto callMacroArgs = once new CallMacroArgs;
        callMacroArgs.set(astTarget, astArgs, context, loc);
        context.macroState.applyMacro(callMacroArgs);
        if (auto transformed = callMacroArgs.transformed) return transformed;

        /// Direct implementation of `foo.bar(baz)`.
        /// Reduces need for `LateSymbol`.
        if (auto memberAccess = astTarget.instanceOf(ASTMemberBase)) {
            auto base = memberAccess.base.compile(context);
            if (auto type = base.instanceOf(Type)) {
                auto protection = type.protectionAt(context.namespace.notNull);
                if (auto ret = type.callMember(
                        context, null, memberAccess.member, astArgs, protection, memberAccess.loc))
                    return ret;
            } else if (auto baseExpr = base.isExpressionImplCall(context, loc)) {
                auto protection = baseExpr.type.protectionAt(context.namespace.notNull);
                if (auto ret = baseExpr.type.callMember(
                        context, baseExpr, memberAccess.member, astArgs, protection, memberAccess.loc))
                    return ret;
            }
        }
    }

    mut auto target = astTarget.compile(context);

    if (!autoCall) {
        if (auto type_ = target.instanceOf(Type)) {
            if (auto ret = type_.call(context, loc, astArgs))
                return ret;
        }
        if (auto expr = target.instanceOf(Expression)) {
            if (auto ret = expr.type.callExpr(context, loc, expr, astArgs))
                return ret;
        }
    }

    if (auto fundecl = target.instanceOf(FunctionDeclaration)) {
        validateCall(fundecl.params, astArgs, fundecl.name, variadic=fundecl.variadic, loc);
    }
    if (auto method = target.instanceOf(ClassMethodPtr)) {
        if (auto funcptr = method.funcPtr.type.instanceOf(FunctionPointer)) {
            // skip "this"
            validateCall(funcptr.params[1 .. $], astArgs, method.name, variadic=false, loc);
        }
    }

    auto args = [
        beExpression3(context, arg.sym.compile(context), loc)
        for arg in astArgs];

    if (auto templ = target.instanceOf(ScopeBoundTemplateDecl)) {
        // Simplest conceivable IFTI
        auto types = [arg.type for arg in args];
        // Some types, like Lambda, generate different types every time they're compiled.
        auto repackagedArgs = [
            ASTArgument(new ASTSymbolHelper(arg), astArgs[i].name)
            for i, arg in args];
        auto instance = templ.instantiate(context, loc, types);

        return call(context, new ASTSymbolHelper(instance), repackagedArgs, loc, autoCall);
    }

    mut nullable Statement pre, post;
    Expression[] args_ = new Expression[](args.length);
    for (i in 0 .. args.length) {
        mut Expression arg = args[i];
        // we must free arg after the call, so make a copy
        if (arg.info.ownership == Ownership.gifted) {
            auto temp = new PairedTemporary(arg.type, Ownership.owned);
            // no copyInto() because we're taking ownership.
            auto set = new InitializeTemporaryStatement(temp, arg);
            if (auto destructor = arg.type.endLifetime(context, temp)) {
                pre = sequence2(pre, set);
                post = sequence(destructor, post);
                arg = temp;
            }
        }
        args_[i] = arg;
    }
    auto args = args_;

    if (auto lateSym = target.instanceOf(LateSymbol)) {
        // completely bypass plainCall with a FuncPtrCall
        nullable Expression do1(Expression baseExpr) {
            auto funcPtr = lateSym.resolve(context, baseExpr);
            mut Expression[] args = args;
            if (auto funcPtr = funcPtr.type.instanceOf(FunctionPointer)) {
                validateCall(funcPtr.params[1 .. $], astArgs, "function pointer", variadic=false, loc);
                args = [expectImplicitConvertTo(context, arg, funcPtr.params[i+1].type, loc) for i, arg in args];
            } else {
                loc.fail("function reference or funcptr expected? but is '$(funcPtr.type.repr)'");
            }
            auto voidp = once new Pointer(new Void);
            mut Expression[] combinedArgs;
            combinedArgs ~= lateSym.patchContext(context, new PointerCast(voidp, baseExpr));
            combinedArgs ~= args;
            return new FuncPtrCall(funcPtr, combinedArgs, loc);
        }
        Expression do2(Expression baseExpr)
        {
            return context.compiler.exprWithTemporaryExpr(baseExpr, &do1).notNull;
        }
        auto fpCall = context.compiler.consume(context, lateSym.baseExpr, &do2);

        return new WrapExpression(pre, fpCall, post);
    }

    nullable ClassMethodPtr method = target.instanceOf(ClassMethodPtr);
    if (method && method.thisValue.info.ownership == Ownership.gifted) { // foo().bar()
        mut nullable Statement setRef;
        void doRef(Reference tempThis) {
            if (Statement cleanupThis = method.thisValue.type.endLifetime(context, tempThis)) {
                if (Statement setRef = setRef) pre = sequence2(pre, setRef);
                post = sequence(cleanupThis, post);
                target = new ClassMethodPtr(method.funcPtr, tempThis, method.valueIsPtr, method.name);
            }
        }
        if (auto ref_ = method.thisValue.instanceOf(Reference)) doRef(ref_);
        else {
            // create reference for endLifetime call
            auto tempThis = new PairedTemporary(method.thisValue.type, Ownership.gifted);
            setRef = new InitializeTemporaryStatement(tempThis, method.thisValue);
            doRef(tempThis);
        }
    }
    if (auto callResult = plainCall(context, target, args, loc, autoCall))
        return new WrapExpression(pre, callResult, post);
    return null;
}

/**
 * The sort of thing that can be implicitly called.
 * Shortcuts the call() logic to speed up `isExpressionImplCall`.
 */
bool previewCanCallImplicit(Symbol target) {
    return target.instanceOf(FunctionDeclaration)
        || target.instanceOf(ClassMethodPtr)
        || target.instanceOf(LateSymbol);
}

class ASTCall : ASTSymbol
{
    ASTSymbol target;

    ASTArgument[] args;

    Loc loc;

    this(this.target, this.args, this.loc) { }

    override Expression compile(Context context)
    {
        // FIXME this goes better in call()
        if (auto astMemberAccess = this.target.instanceOf(ASTMember))
        {
            /**
             * UFCS:
             * Syntax is foo.bar(baz).
             * Check if "foo" has a property "bar".
             * If not, call bar(foo, baz) instead.
             */
            auto base = astMemberAccess.base.compile(context).isExpressionImplCall(context, this.loc);
            if (base) {
                auto member = accessMember(context, base, astMemberAccess.member, astMemberAccess.loc, true);
                if (!member) {
                    auto stackframe = once new ReferenceExpression(new StackFrame(new Void));
                    auto callable = context.namespace.lookup(astMemberAccess.member, context, this.loc, stackframe);
                    // if `foo` has no `bar` but there is a `bar` symbol:
                    if (callable) {
                        // rewrite foo.bar(baz) to bar(foo, baz)
                        auto target = new ASTIdentifier(astMemberAccess.member, false, astMemberAccess.loc);
                        auto args = [ASTArgument(astMemberAccess.base, "")] ~ this.args;
                        return (new ASTCall(target, args, this.loc)).compile(context);
                    }
                }
            }
        }

        return call(context, this.target, this.args, this.loc, false).notNull;
    }

    override ASTSymbol quote(Quoter quoter) {
        auto args = [arg.sym.quote(quoter) for arg in this.args];
        return quoter.compilerCall("astCall", [
            this.target.quote(quoter),
            quoter.compiler.astArrayLiteral(args, this.loc)
        ], this.loc);
    }

    override string repr() {
        return this.target.repr ~ "(" ~ [join ", " a.sym.repr for a in this.args] ~ ")";
    }
}

ASTArgument[] parseArgumentList(Parser parser, LexicalContext lexicalContext)
{
    mut ASTArgument[] args;
    while (!parser.accept(")"))
    {
        if (args.length > 0)
            parser.expect(",");
        // foo(a, b, c, )
        if (parser.accept(")")) break;

        mut string name;
        parser.begin;
        string n = parser.parseIdentifier;
        if (n.length > 0 && parser.acceptButNot("=", "==")) {
            name = n;
            parser.commit;
        } else parser.revert;
        ASTSymbol arg = parser.parseExpression(lexicalContext).notNull;
        args ~= ASTArgument(arg, name);
    }
    return args;
}

nullable ASTCall parseCall(Parser parser, LexicalContext lexicalContext, ASTSymbol base)
{
    parser.begin;
    if (!parser.accept("("))
    {
        parser.revert;
        return null;
    }
    mut ASTArgument[] args = parser.parseArgumentList(lexicalContext);
    parser.commit;
    return new ASTCall(base, args, parser.loc);
}

class ASTIndexAccess : ASTSymbol
{
    ASTSymbol base;

    ASTSymbol index;

    Loc loc;

    this(this.base, this.index, this.loc) { }

    override Expression compile(Context context)
    {
        auto base = beExpression3(context, this.base.compile(context), this.loc);
        bool saveIndexValue = base.info.ownership == Ownership.gifted;

        nullable Expression do_(Reference base) {
            mut uninitialized Expression indexExpr;
            mut uninitialized Expression ptrBase;

            auto type = base.type;
            if (auto array_ = type.instanceOf(Array)) {
                auto context = context.withNamespace(
                    context.compiler.exprAlias(context.namespace, "__array", base));

                indexExpr = beExpression3(context, this.index.compile(context), this.loc);
                // TODO bounds check
                ptrBase = new ArrayPointer(array_.elementType, base);
            } else if (type.instanceOf(Pointer)) {
                indexExpr = beExpression3(context, this.index.compile(context), this.loc);
                ptrBase = base;
            } else {
                indexExpr = beExpression3(context, this.index.compile(context), this.loc);
                if (auto result = type.index(context, base, indexExpr, this.loc)) {
                    return result;
                } else {
                    this.loc.fail("index access requires pointer, array or hashmap");
                }
            }

            indexExpr = expectImplicitConvertTo(context, indexExpr, context.nativeWordType, this.loc);

            assert(indexExpr.type.same(context.nativeWordType));
            auto deref = new Dereference(new PointerOffset(ptrBase, indexExpr));
            if (saveIndexValue)
            {
                // This is a very silly idiom.
                auto temp = new PairedTemporary(deref.type, Ownership.gifted);
                auto init = new UninitializeTemporaryStatement(temp);
                Statement copyDeref() {
                    if (auto stmt = deref.type.copyInto(context, temp, deref))
                        return stmt;
                    return new AssignStatement(temp, deref);
                }
                return context.compiler.wrap(sequence(init, copyDeref), temp, null);
            }
            return deref;
        }
        return context.compiler.consumeTemporary(
                context, allowRefReturn=!saveIndexValue, mayUnwind=false, base, &do_)
            .notNull;
    }

    override ASTSymbol quote(Quoter quoter) {
        return quoter.compilerCall("astIndex", [base.quote(quoter), index.quote(quoter)], loc);
    }
}

class ASTArraySlice : ASTSymbol
{
    ASTSymbol array;

    ASTSymbol lower;

    ASTSymbol upper;

    Loc loc;

    this(this.array, this.lower, this.upper, this.loc) { }

    override Symbol compile(Context context)
    {
        Type sizeT = context.nativeWordType;
        auto array = beExpression3(context, this.array.compile(context), this.loc);

        nullable Expression do_(Reference array)
        {
            auto context = context.withNamespace(
                context.compiler.exprAlias(context.namespace, "__array", array));

            return new ArraySlice(
                array,
                expectImplicitConvertTo(
                    context, beExpression3(context, this.lower.compile(context), this.loc), sizeT, this.loc),
                expectImplicitConvertTo(
                    context, beExpression3(context, this.upper.compile(context), this.loc), sizeT, this.loc),
                this.loc);
        }
        return context.compiler.consumeTemporary(
                context, false, false, array, &do_)
            .notNull;
    }

    override ASTSymbol quote(Quoter quoter) {
        return quoter.compilerCall("astArraySlice", [
            this.array.quote(quoter),
            this.lower.quote(quoter),
            this.upper.quote(quoter)], loc);
    }
}

nullable ASTSymbol parseIndex(Parser parser, LexicalContext lexicalContext, ASTSymbol base)
{
    auto loc = parser.loc;
    parser.begin;
    if (!parser.accept("["))
    {
        parser.revert;
        return null;
    }
    nullable ASTSymbol index = parseExpression(parser, lexicalContext);
    loc.assert2s(!!index, "index expected");
    auto index = index.notNull;
    parser.expect("]");
    parser.commit;
    if (auto iota = index.instanceOf(ASTIota)) {
        return new ASTArraySlice(base, iota.from, iota.to, loc);
    }
    return new ASTIndexAccess(base, index, parser.loc);
}

nullable Symbol accessMemberWithLifetime(Context context, Symbol base, string member, Loc loc, bool allowUndefined)
{
    if (base.instanceOf(Type))
    {
        return accessMember(context, base, member, loc, allowUndefined);
    }
    if (auto super_ = base.instanceOf(Super))
    {
        return super_.lookup(context, member, loc);
    }
    Expression baseExpr = beExpression3(context, base, loc);
    Type baseType = baseExpr.type;

    if (baseType.instanceOf(Pointer)) {
        // cannot trace lifetimes through pointers (TODO?)
        return accessMember(context, base, member, loc, allowUndefined);
    }

    // speculative lookup :-(
    // TODO exprWithTemporary that handles symbol returns
    auto ret = accessMember(context, baseExpr, member, loc, allowUndefined);
    if (!ret || !ret.instanceOf(Expression)) return ret;
    /**
     * - temporize lhs
     * - copyInto member
     * - destroy lhs
     */
    nullable Expression do_(Reference baseExpr) {
        auto ret = accessMember(
                context, baseExpr, member, loc, allowUndefined)
            .instanceOf(Expression)
            .notNull;
        auto tempRet = new PairedTemporary(ret.type, Ownership.gifted); // temporary since we copyInto
        auto initTemp = new UninitializeTemporaryStatement(tempRet);
        Statement copyTemp() {
            if (auto stmt = ret.type.copyInto(context, tempRet, ret))
                return stmt;
            return new AssignStatement(tempRet, ret);
        }

        nullable Statement endLifetime = baseType.endLifetime(context, baseExpr);

        return new WrapExpression(sequence(initTemp, copyTemp), tempRet, endLifetime);
    }
    // SHIT
    // we need a reference on the lhs for assignments
    // that's fine if it's a struct, but for a class? always force nontemporary, because it's contained in the
    // class lifetime?
    // TODO handle member access of temporaries
    // for instance with an ExprReturnType analogous to StatementReturnType, that gathers temp destructors

    // if lhs is nontemp, just use StructReference
    if (baseExpr.info.ownership != Ownership.gifted) {
        return accessMember(context, baseExpr, member, loc, allowUndefined)
            .instanceOf(Expression);
    }
    return context.compiler.exprWithTemporary(baseExpr, &do_);
}

nullable Symbol accessMember(Context context, Symbol base, string member, Loc loc, bool allowUndefined) {
    if (!context.namespace) return null;
    if (auto type = base.instanceOf(Type)) {
        auto protection = type.protectionAt(context.namespace.notNull);
        return type.accessMember(loc, context, null, member, protection);
    }

    loc.assert2s(!!base.instanceOf(Expression), "expected expression for member access");
    mut Expression baseExpr = beExpression3(context, base, loc);

    while (baseExpr.type.instanceOf(Pointer)) {
        baseExpr = new Dereference(baseExpr);
    }

    auto protection = baseExpr.type.protectionAt(context.namespace.notNull);
    if (auto field = baseExpr.type.accessMember(loc, context, baseExpr, member, protection))
        return field;

    if (allowUndefined) return null;
    loc.assert2s4(false, baseExpr.type.repr, ": no such field '", member, "'");
}

class ASTMember : ASTMemberBase
{
    override Symbol compile(Context context)
    {
        // UFCS part two
        // support `foo.bar` as implicit `bar(foo)` if `foo` has no member `bar`
        // TODO merge with ASTCall UFCS code (they should be the same)
        if (auto base = this.base.compile(context).isExpressionImplCall(context, this.loc)) {
            auto member = accessMember(context, base, this.member, this.loc, true);
            if (!member) {
                auto stackframe = once new ReferenceExpression(new StackFrame(new Void));
                auto callable = context.namespace.lookup(this.member, context, this.loc, stackframe);
                // if `foo` has no `bar` but there is a `bar` symbol:
                if (callable) {
                    auto target = new ASTIdentifier(this.member, false, this.loc);
                    // bar(foo)
                    auto args = [ASTArgument(this.base, "")];
                    return (new ASTCall(target, args, this.loc)).compile(context);
                }
            }
        }

        if (auto ret = accessMemberWithLifetime(context, this.base.compile(context), this.member, this.loc, false))
        {
            return ret;
        }
        this.loc.fail("Member '$(this.member)' not found");
    }

    override ASTSymbol quote(Quoter quoter) {
        return quoter.compilerCall("astMember", [
            this.base.quote(quoter),
            quoter.compiler.astStringLiteral(this.member, loc)], loc);
    }
}

nullable ASTMember parseMember(Parser parser, ASTSymbol base)
{
    parser.begin;
    if (parser.accept("..") || !parser.accept(".")) // don't accept '..'
    {
        parser.revert;
        return null;
    }
    string name = parser.parseIdentifier;
    parser.assert_(name.length > 0, "member expected");
    parser.commit;
    return new ASTMember(base, name, parser.loc);
}

class ASTParenPropertyExpression : ASTSymbol
{
    ASTSymbol base;

    ASTSymbol prop;

    Loc loc;

    this(this.base, this.prop, this.loc) { }

    override Symbol compile(Context context) {
        auto baseExpr = this.base.compile(context).beExpressionImplCall(context, this.loc);
        Expression do2(Expression baseExpr) {
            nullable Expression do1(Expression baseExpr) {
                auto context = context.withNamespace(
                    context.compiler.exprAlias(context.namespace, "that", baseExpr));
                return this.prop.compile(context).beExpressionImplCall(context, this.loc);
            }
            // FIXME this should be <-
            if (auto ret = context.compiler.exprWithTemporaryExpr(baseExpr, &do1)) return ret;
            assert(false);
        }
        return context.compiler.consume(context, baseExpr, &do2);
    }

    override ASTSymbol quote(Quoter quoter) {
        return quoter.compiler.astNewExpression(
            quoter.compiler.astIdentifier("ASTParenPropertyExpression", __HERE__), [
                this.base.quote(quoter),
                this.prop.quote(quoter),
                quoter.quoteLoc(this.loc)], __HERE__);
    }

    override string repr() {
        return "$(this.base.repr).($(this.prop.repr))";
    }
}

nullable ASTParenPropertyExpression parseParenPropertyExpression(
    Parser parser, LexicalContext lexicalContext, ASTSymbol base
) {
    parser.begin;
    auto loc = parser.loc;
    if (parser.accept("..") || !parser.accept(".") || !parser.accept("(")) // don't accept '..'
    {
        parser.revert;
        return null;
    }
    auto sym = lexicalContext.compiler.parseExpression(parser, lexicalContext);
    if (!sym) parser.fail("expression expected");
    auto sym <- sym;
    if (!parser.accept(")")) parser.fail("closing parenthesis expected");
    parser.commit;
    return new ASTParenPropertyExpression(base, sym, loc);
}

ASTSymbol parseProperties(Parser parser, LexicalContext lexicalContext, mut ASTSymbol current)
{
    while (true)
    {
        if (ASTSymbol instanceOf = parser.parseInstanceOf(lexicalContext, current))
        {
            current = instanceOf;
            continue;
        }
        auto macroArgs = new ParsePropertyArgs(parser, lexicalContext, current);

        lexicalContext.macroState.applyMacro(macroArgs);
        if (ASTSymbol result = macroArgs.result)
        {
            current = result;
            continue;
        }

        if (ASTSymbol caseExpr = parser.parseEitherCaseExpr(lexicalContext, current))
        {
            current = caseExpr;
            continue;
        }
        if (ASTSymbol call = parser.parseCall(lexicalContext, current))
        {
            current = call;
            continue;
        }
        if (ASTSymbol prop = parser.parseParenPropertyExpression(lexicalContext, current))
        {
            current = prop;
            continue;
        }
        if (ASTSymbol memberAccess = parser.parseMember(current))
        {
            current = memberAccess;
            continue;
        }
        if (ASTSymbol indexAccess = parser.parseIndex(lexicalContext, current))
        {
            current = indexAccess;
            continue;
        }
        if (ASTSymbol templateInstance = parser.parseTemplateInstantiation(lexicalContext, current))
        {
            current = templateInstance;
            continue;
        }
        return current;
    }
}

class ASTDereference : ASTSymbol
{
    ASTSymbol base;

    Loc loc;

    this(this.base, this.loc) { }

    override Symbol compile(Context context)
    {
        return new Dereference(beExpression(this.base.compile(context)));
    }

    override ASTSymbol quote(Quoter quoter) {
        return quoter.compilerCall(
            "astDereference", [this.base.quote(quoter)], loc);
    }
}

class ASTReference : ASTSymbol
{
    ASTSymbol base;

    Loc loc;

    this(this.base, this.loc) { }

    override Expression compile(Context context)
    {
        auto base = this.base.compile(context);
        // &function
        if (Function fun = base.instanceOf(Function))
            return new FunctionReference(fun);
        if (LateSymbol lateSym = base.instanceOf(LateSymbol)) {
            nullable Expression do1(Expression baseExpr) {
                auto funcPtr = lateSym.resolve(context, baseExpr);
                auto voidp = once new Pointer(new Void);
                auto contextPtr = lateSym.patchContext(context, new PointerCast(voidp, baseExpr));
                return new DelegateExpr(funcPtr, contextPtr);
            }
            Expression do2(Expression baseExpr)
            {
                return context.compiler.exprWithTemporaryExpr(baseExpr, &do1).notNull;
            }
            return context.compiler.consume(context, lateSym.baseExpr, &do2);
        }
        if (auto nestfn = base.instanceOf(ClassMethodPtr)) {
            nullable Expression doExpr(Expression thisPtr) {
                return new DelegateExpr(nestfn.funcPtr, thisPtr);
            }
            nullable Expression doRef(Reference thisValue) {
                return doExpr(context.compiler.reference(thisValue));
            }
            if (nestfn.valueIsPtr) {
                return context.compiler.exprWithTemporaryExpr(nestfn.thisValue, &doExpr).notNull;
            } else {
                return context.compiler.exprWithTemporary(nestfn.thisValue, &doRef).notNull;
            }
        }
        Expression baseExpression = beExpression2(base, this.loc);

        loc.assert2s(!!baseExpression.instanceOf(Reference), "&expr: expr not a reference");

        return new ReferenceExpression(baseExpression.instanceOf(Reference).notNull);
    }

    override ASTSymbol quote(Quoter quoter) {
        return quoter.compilerCall(
            "astReference", [this.base.quote(quoter)], loc);
    }
}

class Lambda : Type
{
    TemplateDecl decl;

    string repr_;

    this(this.decl, this.repr_) {
        this.zeroInitializable = true;
    }

    override BackendType emit(Platform platform) {
        return once (new Pointer(new Void)).emit(platform);
    }

    override nullable Expression callExpr(Context context, Loc loc, Expression base, ASTArgument[] args)
    {
        // Reconstruct a ScopeBoundTemplateDecl using `base`
        auto scopeTempl = new ScopeBoundTemplateDecl(this.decl, base);
        // Instantiate with `args`' types
        Expression[] argExprs = [
            arg.sym.compile(context).beExpressionImplCall(context, loc) for arg in args];
        auto instance = scopeTempl.instantiate(context, loc, [arg.type for arg in argExprs]);
        // call(`args`)
        return .call(context, new ASTSymbolHelper(instance), args, loc, autoCall=true);
    }

    override bool same(Type other) {
        if (auto otherLambda = other.instanceOf(Lambda)) {
            return this.decl.same(otherLambda.decl);
        }
        return false;
    }

    override string mangle() {
        return "__lambda";
    }

    override string repr() {
        return "typeof($(this.repr_))";
    }

    override void hash(Hash hash) {
        hash.adds("lambda");
        hash.adds(this.decl.mangle);
    }
}

class LambdaExpr : Expression
{
    ScopeBoundTemplateDecl templ;

    string repr_;

    this(this.templ, this.repr_) {
        this.type = new Lambda(this.templ.latent, this.repr_);
    }

    override int emit(Generator output) {
        int reg = this.templ.frame.emit(output);

        return output.fun.bitcast(reg, this.type.emit(output.platform));
    }
    override ExprInfo info() {
        return this.templ.frame.info;
    }
    override string repr() {
        return this.repr_;
    }
    override void hash(Hash hash) {
        hash.adds("lambdaexpr");
        hash.adds(this.templ.latent.mangle);
        this.templ.frame.hash(hash);
    }
}

class ASTLambdaExpr : ASTSymbol
{
    string paramName;

    ASTSymbol value;

    MacroState macroState;

    Loc loc;

    this(this.paramName, this.value, this.macroState, this.loc) { }

    override Symbol compile(Context context) {
        /**
         * ({
         *     template lambda__(T) {
         *          typeof(({ mut uninitialized T $paramName; $value; })) lambda__(T $paramName) {
         *              return $value;
         *          }
         *      }
         *      __lambda(lambda__);
         * })
         */
        ASTStatement declareUninitializedT = new ASTVarDeclStatement(
            this.paramName, mutable=true, infer=false, uninitialized=true,
            type=new ASTIdentifier("T", false, __HERE__), initial=null, __HERE__);
        auto typeofProbe = new ASTStatementExpression([declareUninitializedT], value, __HERE__);
        auto typeofLambda = new ASTTypeof(typeofProbe, __HERE__);
        auto returnLambdaExpr = new ASTReturnStatement(value, __HERE__);
        // Hax: Every compilation is unique!
        string lambdaName = "__lambda_$(this.loc.row)_$(context.getLabel)";
        auto lambdaFun = new ASTFunction(
            __HERE__, lambdaName, typeofLambda, [
                ASTParameter(paramName, mutable=false, new ASTIdentifier("T", false, __HERE__), __HERE__)],
            isDeclaration=false, statement=returnLambdaExpr, this.macroState);
        auto astTemplateDecl = new ASTTemplateDecl(lambdaName, ["T"], lambdaFun);
        auto stackframe = once new ReferenceExpression(new StackFrame(new Void));
        auto templateDecl = new ScopeBoundTemplateDecl(
            new TemplateDecl(context.namespace, astTemplateDecl),
            stackframe);
        string repr = "$paramName -> $(this.value.repr)";

        return new LambdaExpr(templateDecl, repr);
    }
}

nullable ASTSymbol parseLambdaExpression(Parser parser, LexicalContext lexicalContext)
{
    auto loc = parser.loc;
    parser.begin;
    auto arg = parser.parseIdentifier;
    if (!arg.length || !parser.accept("->")) {
        parser.revert;
        return null;
    }
    parser.commit;
    auto expr = parser.parseExpression(lexicalContext);
    loc.assert2s(!!expr, "lambda expression expected");
    return new ASTLambdaExpr(arg, expr.notNull, lexicalContext.macroState, loc);
}

nullable ASTSymbol parseExpressionLeaf(Parser parser, LexicalContext lexicalContext)
{
    auto loc = parser.loc;
    if (parser.accept("*"))
    {
        ASTSymbol next = parseExpressionLeaf(parser, lexicalContext).notNull;

        return new ASTDereference(next, loc);
    }
    if (parser.accept("&"))
    {
        ASTSymbol next = parseExpressionLeaf(parser, lexicalContext).notNull;

        return new ASTReference(next, loc);
    }
    if (parser.acceptIdentifier("new"))
    {
        auto type = parser.parseType(lexicalContext);
        if (!type) {
            parser.fail("type expected");
        }
        mut ASTArgument[] args;
        if (parser.accept("("))
        {
            args = parseArgumentList(parser, lexicalContext);
        }

        return new ASTNewExpression(type.notNull, args, loc);
    }
    if (parser.acceptIdentifier("sizeof"))
    {
        parser.expect("(");
        parser.begin;
        mut nullable ASTSymbol sym = lexicalContext.compiler.parseType(parser, lexicalContext);
        if (!sym || !parser.accept(")")) {
            parser.revert;
            parser.begin;
            sym = lexicalContext.compiler.parseExpression(parser, lexicalContext);
            if (!sym) parser.fail("expression expected");
            parser.expect(")");
        }
        parser.commit;

        return new ASTSizeOf(sym.notNull);
    }
    if (parser.acceptIdentifier("cast"))
    {
        parser.expect("(");
        nullable ASTSymbol target = parser.parseType(lexicalContext);
        if (!target) {
            parser.fail("type expected");
        }
        parser.expect(")");
        ASTSymbol value = parseExpressionLeaf(parser, lexicalContext).notNull;

        return new ASTCastExpr(target.notNull, value, loc);
    }
    if (auto trait = parser.parseTrait(lexicalContext)) {
        return trait;
    }
    if (auto lambda = parser.parseLambdaExpression(lexicalContext)) {
        return lambda;
    }
    if (auto tuple = parser.parseTupleExpression(lexicalContext))
    {
        return tuple;
    }
    if (parser.accept("!"))
    {
        ASTSymbol next = parser.parseExpressionLeaf(lexicalContext).notNull;

        return new ASTNegation(next, loc);
    }
    if (parser.acceptButNot("-", "--"))
    {
        nullable ASTSymbol next = parser.parseExpressionLeaf(lexicalContext);

        loc.assert2s(!!next, "expression expected for negation");
        return new ASTBinaryOp("-", new ASTIntLiteral(0, loc), next.notNull, loc);
    }
    if (ASTSymbol expr = parser.parseExpressionIncDec(lexicalContext))
        return parseProperties(parser, lexicalContext, expr);
    return null;
}

ASTSymbol parseStringLiteral(Parser parser, LexicalContext lexicalContext, string endMarker)
{
    auto loc = parser.loc;
    mut string current;
    mut ASTSymbol[] parts;
    void flush() {
        if (current != "") parts ~= new ASTStringLiteral(current, loc);
        current = "";
    }
    while (parser.text.length < endMarker.length || parser.text[0 .. endMarker.length] != endMarker)
    {
        if (parser.text.length == 0) {
            parser.fail("expected end of string, got end of file");
        }
        string ch = parser.text[0 .. 1];
        parser.drop(1);
        if (ch == "\\") {
            string ctl = parser.text[0 .. 1];
            parser.dropOneCharNonNewline(1);
            if (ctl == "r") {
                current ~= "\r";
            } else if (ctl == "\$") {
                current ~= "\$";
            } else if (ctl == "n") {
                current ~= "\n";
            } else if (ctl == "t") {
                current ~= "\t";
            } else if (ctl == "\"") {
                current ~= "\"";
            } else if (ctl == "\\") {
                current ~= "\\";
            } else if (ctl == "0") {
                current ~= "\x00"; // TODO \0
            } else if (ctl == "x") {
                char ch1 = parser.text[0], ch2 = parser.text[1];
                int decodeHex(char ch) {
                    if (ch >= "0"[0] && ch <= "9"[0])
                        return cast(int) (ch - "0"[0]);
                    if (ch >= "a"[0] && ch <= "F"[0])
                        return cast(int) ch + 10 - cast(int) "a"[0];
                    if (ch >= "A"[0] && ch <= "F"[0])
                        return cast(int) ch + 10 - cast(int) "A"[0];
                    print("Unknown hex digit '" ~ ch ~ "'");
                    assert(false);
                }
                char ord = cast(char) (decodeHex(ch1) * 16 + decodeHex(ch2));
                current ~= ord;
                parser.drop(2);
            } else {
                print("Unknown control sequence \\$ctl");
                assert(false);
            }
        } else if (ch == "\$") {
            flush;
            if (parser.accept("(")) {
                nullable ASTSymbol expr = parser.parseExpression(lexicalContext);
                loc.assert2s(!!expr, "Expression expected.");
                parser.expect(")");
                parts ~= expr.notNull;
            } else {
                auto varLoc = parser.loc;
                auto var = parser.parseIdentifier;
                loc.assert2s(var != "", "Identifier expected.");
                parts ~= new ASTIdentifier(var, moduleLevel=false, varLoc);
            }
        } else {
            current ~= ch;
        }
    }
    if (!parser.accept(endMarker))
    {
        parser.fail("this should never happen");
    }
    flush;
    if (parts.length == 0) return new ASTStringLiteral("", loc);
    if (parts.length == 1) {
        if (auto literal = parts[0].instanceOf(ASTStringLiteral))
            return literal;
    }

    return new ASTFormatString(loc, parts);
}

// i++, i--, ++i, --i
class ASTPrePostIncDec : ASTSymbol
{
    Loc loc;

    ASTSymbol target;

    ((:pre | :post) when, (:inc | :dec) what) op;

    this(this.loc, this.op, this.target) { }

    override Symbol compile(Context context) {
        auto target = target.compile(context).beExpressionImplCall(context, loc);
        loc.assert2s5(
            !!target.instanceOf(Reference),
            "cannot ", op.when.case((:pre): "pre", (:post): "post"), "-",
            op.what.case((:inc): "inc", (:dec): "dec"), "rement non-reference target");
        nullable Expression do_(Expression refptr) {
            auto context = context.withNamespace(
                context.compiler.exprAlias(context.namespace, "ref_", context.compiler.dereference(refptr)));

            return op.when.case(
                (:pre): op.what.case(
                    (:inc): context.compiler.$expr ({ ref_ += 1; ref_; }),
                    (:dec): context.compiler.$expr ({ ref_ -= 1; ref_; })
                ),
                (:post): op.what.case(
                    (:inc): context.compiler.$expr ({ auto ret = ref_; ref_ += 1; ret; }),
                    (:dec): context.compiler.$expr ({ auto ret = ref_; ref_ -= 1; ret; })))
                .compile(context).beExpression;
        }
        auto refptr = context.compiler.reference(target.instanceOf(Reference).notNull);
        return context.compiler.exprWithTemporaryExpr(refptr, &do_).notNull;
    }

    override ASTSymbol quote(Quoter quoter) { print("cannot quote 'ASTPreOp'!"); assert(false); }
    override string repr() {
        auto opText = op.what.case((:inc): "++", (:dec): "--");
        return op.when.case((:pre): opText ~ target.repr, (:post): target.repr ~ opText);
    }
}

nullable ASTSymbol parseExpressionIncDec(Parser parser, LexicalContext lexicalContext)
{
    mut (:inc | :dec | :none) pre = :none, post = :none;
    auto loc1 = parser.loc;
    if (parser.accept("++")) pre = :inc;
    else if (parser.accept("--")) pre = :dec;
    // TODO?
    // ASTSymbol ret <- parseExpressionBase(parser, lexicalContext);
    mut auto ret <- parseExpressionBase(parser, lexicalContext);
    auto loc2 = parser.loc;
    if (parser.accept("++")) post = :inc;
    else if (parser.accept("--")) post = :dec;
    pre.case {
        (:inc): ret = new ASTPrePostIncDec(loc1, (:pre, :inc), ret);
        (:dec): ret = new ASTPrePostIncDec(loc1, (:pre, :dec), ret);
        (:none): {}
    }
    post.case {
        (:inc): ret = new ASTPrePostIncDec(loc2, (:post, :inc), ret);
        (:dec): ret = new ASTPrePostIncDec(loc2, (:post, :dec), ret);
        (:none): {}
    }
    return ret;
}

// the AST of a Loc literal recreating the passed loc.
ASTSymbol hereExpr(Loc loc, LexicalContext lexicalContext)
{
    with (lexicalContext.compiler)
        return astCall(
            astIdentifier("Loc", __HERE__), [
                astStringLiteral(loc.filename, __HERE__),
                astIntLiteral(loc.row, __HERE__),
                astIntLiteral(loc.column, __HERE__),
            ], __HERE__);
}

nullable ASTSymbol parseExpressionBase(Parser parser, LexicalContext lexicalContext)
{
    {
        auto args = new ParseExpressionBaseArgs(parser, lexicalContext);
        lexicalContext.macroState.applyMacro(args);
        if (ASTSymbol ret = args.symbol) return ret;
    }
    {
        if (ASTSymbol destroy_ = parseDestroy(parser, lexicalContext))
            return destroy_;
    }
    parser.begin;
    parser.strip;
    auto loc = parser.loc;
    if (parser.accept(".")) {
        // .name is module level
        string name = parser.parseIdentifier;
        if (name.length) {
            parser.commit;
            return new ASTIdentifier(name, true, loc);
        }
    }
    parser.revert;
    string name = parser.parseIdentifier;
    if (name.length)
    {
        if (name == "__HERE__") return hereExpr(loc, lexicalContext);
        if (name == "\$") return new ASTArrayLength(loc);
        if (name == "super") return new ASTSuper(loc);
        return new ASTIdentifier(name, false, loc);
    }
    {
        parser.begin;
        string symbolIdentifier = parser.parseIdentifier2(":", allowKeywords=false);
        if (symbolIdentifier.startsWith(":")) {
            parser.commit;
            return new ASTSymbolIdentifier(symbolIdentifier[1 .. $]);
        }
        parser.revert;
    }
    parseFloat(parser).case {
        (:failure): {}
        (:success, double d):
            return new ASTDoubleLiteral(d);
        (:success, float f):
            return new ASTFloatLiteral(f);
    }
    parseNumber(parser).case {
        (:failure): {}
        (:success, int i):
            return new ASTIntLiteral(i, loc);
    }
    if (parser.accept("\""))
    {
        return parseStringLiteral(parser, lexicalContext, "\"");
    }
    if (auto statementExpr = parseStatementExpr(parser, lexicalContext))
        return statementExpr;
    if (parser.accept("("))
    {
        ASTSymbol result = parseExpression(parser, lexicalContext).notNull;

        parser.expect(")");
        return result;
    }
    {
        if (auto result = parseArrayLiteral(parser, lexicalContext))
            return result;
    }
    return null;
    /*parser.fail("Base expression expected.");
    assert(false);*/
}

ASTSymbol parseBitAnd(Parser parser, LexicalContext lexicalContext, mut ASTSymbol left, int myLevel)
{
    while (true)
    {
        parser.begin;
        if (parser.acceptButNot("&", "&&"))
        {
            parser.commit;
            ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

            left = new ASTBinaryOp("&", left, right, parser.loc);
        }
        else
        {
            parser.revert;
            return left;
        }
    }
}

ASTSymbol parseBitXor(Parser parser, LexicalContext lexicalContext, mut ASTSymbol left, int myLevel)
{
    while (true)
    {
        parser.begin;
        if (parser.accept("^"))
        {
            parser.commit;
            ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

            left = new ASTBinaryOp("^", left, right, parser.loc);
        }
        else
        {
            parser.revert;
            return left;
        }
    }
}

ASTSymbol parseBitOr(Parser parser, LexicalContext lexicalContext, mut ASTSymbol left, int myLevel)
{
    while (true)
    {
        parser.begin;
        if (parser.acceptButNot("|", "||"))
        {
            parser.commit;
            ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

            left = new ASTBinaryOp("|", left, right, parser.loc);
        }
        else
        {
            parser.revert;
            return left;
        }
    }
}

ASTSymbol parseBitShift(Parser parser, LexicalContext lexicalContext, mut ASTSymbol left, int myLevel)
{
    while (true)
    {
        if (parser.accept("<<"))
        {
            ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

            left = new ASTBinaryOp("<<", left, right, parser.loc);
        }
        else if (parser.accept(">>>"))
        {
            ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

            left = new ASTBinaryOp(">>>", left, right, parser.loc);
        }
        else if (parser.accept(">>"))
        {
            ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

            left = new ASTBinaryOp(">>", left, right, parser.loc);
        }
        else return left;
    }
}

ASTSymbol parseAddSubCat(Parser parser, LexicalContext lexicalContext, mut ASTSymbol left, int myLevel)
{
    while (true)
    {
        if (parser.accept("+"))
        {
            ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

            left = new ASTBinaryOp("+", left, right, parser.loc);
        }
        else if (parser.accept("-"))
        {
            ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

            left = new ASTBinaryOp("-", left, right, parser.loc);
        }
        else if (parser.accept("~"))
        {
            ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

            left = new ASTBinaryOp("~", left, right, parser.loc);
        }
        else return left;
    }
}

ASTSymbol parseMulDiv(Parser parser, LexicalContext lexicalContext, mut ASTSymbol left, int myLevel)
{
    while (true)
    {
        if (parser.accept("*"))
        {
            ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

            left = new ASTBinaryOp("*", left, right, parser.loc);
        }
        else if (parser.accept("/"))
        {
            ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

            left = new ASTBinaryOp("/", left, right, parser.loc);
        }
        else if (parser.accept("%"))
        {
            ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

            left = new ASTBinaryOp("%", left, right, parser.loc);
        }
        else return left;
    }
}

ASTSymbol parseBoolAnd(Parser parser, LexicalContext lexicalContext, mut ASTSymbol left, int myLevel)
{
    while (true)
    {
        if (parser.accept("&&"))
        {
            ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

            left = new ASTBinaryOp("&&", left, right, parser.loc);
        } else return left;
    }
}

ASTSymbol parseBoolOr(Parser parser, LexicalContext lexicalContext, mut ASTSymbol left, int myLevel)
{
    while (true)
    {
        if (parser.accept("||"))
        {
            ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

            left = new ASTBinaryOp("||", left, right, parser.loc);
        } else return left;
    }
}

// a .. b
class ASTIota : ASTSymbol
{
    ASTSymbol from;

    ASTSymbol to;

    Loc loc;

    this(this.from, this.to, this.loc) { }

    override Expression compile(Context context)
    {
        auto iotaValue = context.compiler.$expr ({ import package(compiler).std.range.iota : Iota; Iota($from, $to); });

        return iotaValue.compile(context).beExpression;
    }

    override ASTSymbol quote(Quoter quoter) {
        return quoter.compilerCall("astIota", [
            from.quote(quoter),
            to.quote(quoter)], loc);
    }
}

ASTSymbol parseIota(Parser parser, LexicalContext lexicalContext, ASTSymbol left, int myLevel)
{
    if (parser.accept(".."))
    {
        ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

        return new ASTIota(left, right, parser.loc);
    }
    return left;
}

ASTSymbol parseComparison(Parser parser, LexicalContext lexicalContext, ASTSymbol left, int myLevel)
{
    if (parser.accept("=="))
    {
        ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

        return new ASTBinaryOp("==", left, right, parser.loc);
    }
    if (parser.accept("!=")) // same as !(a == b)
    {
        ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

        return new ASTNegation(new ASTBinaryOp("==", left, right, parser.loc), parser.loc);
    }
    if (parser.accept("is"))
    {
        ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

        return new ASTBinaryOp("is", left, right, parser.loc);
    }
    if (parser.accept("!is"))
    {
        ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

        return new ASTNegation(new ASTBinaryOp("is", left, right, parser.loc), parser.loc);
    }
    if (parser.accept(">="))
    {
        ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

        return new ASTBinaryOp(">=", left, right, parser.loc);
    }
    if (parser.acceptButNot(">", ">>"))
    {
        ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

        return new ASTBinaryOp(">", left, right, parser.loc);
    }
    if (parser.accept("<="))
    {
        ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

        return new ASTBinaryOp("<=", left, right, parser.loc);
    }
    if (parser.acceptButNot("<", "<<"))
    {
        ASTSymbol right = expectArithmetic(parser, lexicalContext, myLevel + 1);

        return new ASTBinaryOp("<", left, right, parser.loc);
    }
    return left;
}

ASTSymbol parseArithmetic(Parser parser, LexicalContext lexicalContext, mut ASTSymbol left, int level)
{
    if (level <= 9) left = parseBitAnd(parser, lexicalContext, left, 9);
    if (level <= 8) left = parseBitXor(parser, lexicalContext, left, 8);
    if (level <= 7) left = parseBitOr(parser, lexicalContext, left, 7);
    if (level <= 6) left = parseMulDiv(parser, lexicalContext, left, 6);
    if (level <= 5) left = parseAddSubCat(parser, lexicalContext, left, 5);
    if (level <= 4) left = parseBitShift(parser, lexicalContext, left, 4);
    if (level <= 3) left = parseIota(parser, lexicalContext, left, 3);
    if (level <= 2) left = parseComparison(parser, lexicalContext, left, 2);
    if (level <= 1) left = parseBoolAnd(parser, lexicalContext, left, 1);
    if (level <= 0) left = parseBoolOr(parser, lexicalContext, left, 0);
    return left;
}

ASTSymbol expectArithmetic(Parser parser, LexicalContext lexicalContext, int level)
{
    auto leaf = parseExpressionLeaf(parser, lexicalContext).notNull;

    return parseArithmetic(parser, lexicalContext, leaf, level);
}

nullable ASTSymbol parseExpression(Parser parser, LexicalContext lexicalContext)
{
    auto leaf <- parseExpressionLeaf(parser, lexicalContext);
    return parseArithmetic(parser, lexicalContext, leaf, 0);
}

class ASTReturnStatement : ASTStatement
{
    ASTSymbol value;

    Loc loc;

    this(this.value, this.loc) { }

    override StatementCompileResult compile(Context context)
    {
        mut Expression expr = beExpression3(context, this.value.compile(context), this.loc);
        Type ret = findParent!FunctionScope(context.namespace).ret;
        expr = expectImplicitConvertTo(context, expr, ret, this.loc);

        /**
         * - alloca
         *   - copyInto the value
         *   - or assign the value (if it's a temporary and we can steal its reference)
         * - unwind stack
         * - ret the alloca
         */
        auto temp = new PairedTemporary(ret, Ownership.owned);
        auto init = new UninitializeTemporaryStatement(temp);
        Statement escapeValueStmt()
        {
            if (expr.info.ownership == Ownership.gifted || expr.info.ownership == Ownership.global)
                return new AssignStatement(temp, expr);
            if (auto copyInto = ret.copyInto(context, temp, expr))
                return copyInto;
            return new AssignStatement(temp, expr);
        }
        mut Statement returnStmt = sequence(init, escapeValueStmt);
        returnStmt = unwindScope(context, findParent!FunctionScope(context.namespace), returnStmt);
        returnStmt = sequence(returnStmt, new ReturnStatement(temp));
        // no need for stack cleanup after a return.

        return StatementCompileResult(returnStmt, context);
    }

    override ASTSymbol quote(Quoter quoter) {
        return quoter.compilerCall("astReturn", [value.quote(quoter)], loc);
    }
}

// the return expression of "return;"
class ASTVoidExpression : ASTSymbol
{
    Loc loc;

    this(this.loc) { }

    override Expression compile(Context context) { return once new VoidExpression; }

    override ASTSymbol quote(Quoter quoter) {
        ASTSymbol[] args;
        return quoter.compilerCall("astVoidLiteral", args, loc);
    }
}

class VoidExpression : Expression
{
    this() { this.type = once new Void; }
    override int emit(Generator generator)
    {
        return generator.fun.voidLiteral;
    }
    override ExprInfo info() { return ExprInfo(false, Ownership.owned); }
    override void hash(Hash hash) { hash.adds("VoidExpression"); }
}

nullable ASTReturnStatement parseReturn(Parser parser, LexicalContext lexicalContext)
{
    parser.begin;
    string identifier = parser.parseIdentifier;
    if (identifier != "return")
    {
        parser.revert;
        return null;
    }
    mut uninitialized ASTSymbol expr;
    if (parser.accept(";")) // return;
    {
        expr = new ASTVoidExpression(parser.loc);
    }
    else
    {
        expr = parseExpression(parser, lexicalContext).notNull;
        parser.expect(";");
    }
    parser.commit;
    return new ASTReturnStatement(expr, parser.loc);
}

Statement unwindScope(Context context, nullable Namespace target, Statement body_)
{
    mut Statement result = body_;
    mut nullable Namespace current = context.namespace;
    while (current && current !is target) {
        if (auto declHolder = current.instanceOf(DeclarationHolder)) {
            auto var = declHolder.accessDecl(context.compiler);
            if (var.info.ownership == Ownership.owned) {
                auto type = var.type;
                if (auto destroy = type.endLifetime(context, var)) {
                    result = sequence(result, destroy);
                }
            }
        }
        current = current.parent;
    }
    return result;
}

(Expression test, Expression value) pickEither(
    Context context, Either either, Expression source, Type extractType, Loc loc)
{
    if (auto extractEither = extractType.instanceOf(Either))
        return pickEitherTuple(context, either, source, extractEither, loc);
    int eithermember = [first cast(int) i for i, a in either.types where a.type.same(extractType) else -1];
    loc.assert2s(eithermember != -1, "type $(extractType.repr) not in sumtype $(either.repr)");
    auto test = context.compiler.binaryOp(
        "==", context, new EitherType(source), new IntLiteral(eithermember + 1), __HERE__);
    auto value = new EitherMember(source, eithermember);
    return (test, value);
}

/**
 * if the thing on the left of the pick operator is an either:
 *
 * - create a result temporary for it
 * - create a temporary bool (false)
 * - for each type in the either:
 *   - build an iftree with the source either
 *   - on success, init the result and set the bool to true
 * - return (bool, result)
 */
(Expression test, Expression value) pickEitherTuple(
    Context context, Either either, Expression source, Either extractEither, Loc loc)
{
    auto result = new PairedTemporary(extractEither, source.info.ownership);
    auto init = new UninitializeTemporaryStatement(result);
    auto resBool = new PairedTemporary(new Integer, Ownership.global);
    auto init2 = new InitializeTemporaryStatement(resBool, new IntLiteral(0)); // false
    auto sourceTypeId = new EitherType(source);
    mut nullable Statement iftree = null;
    for (i, entry in extractEither.types) {
        auto extractType = entry.type;
        int eithermember = [first cast(int) i for i, a in either.types where a.type.same(extractType) else -1];
        loc.assert2s(eithermember != -1, "sumtype $(extractType.repr) not in source $(either.repr)");
        auto cmpTypeId = new IntLiteral(eithermember + 1);
        auto test = context.compiler.binaryOp("==", context, sourceTypeId, cmpTypeId, __HERE__);

        auto extractValue = new EitherMember(source, eithermember);
        auto repacked = new EitherExpr(extractEither, extractValue, cast(int) i);
        auto setStmt = new AssignStatement(result, repacked);
        auto setBoolStmt = new AssignStatement(resBool, new IntLiteral(1)); // true

        iftree = context.compiler.ifStatement(test, sequence(setStmt, setBoolStmt), iftree);
    }
    auto test = new StatementExpression(
        sequence(sequence(init, init2), iftree),
        resBool,
        gifted=(source.info.ownership == Ownership.gifted));
    return (test, result);
}

private Type typeWithoutFail(Either source)
{
    if ([count a in source.types where !a.fail] == 1) {
        return [first a.type for a in source.types where !a.fail];
    }
    return new Either([a for a in source.types where !a.fail]);
}

class InfoExpression : Expression
{
    Expression base;

    ExprInfo info_;

    this(this.base, this.info_) { this.type = base.type; }

    override int emit(Generator output) { return base.emit(output); }
    // TODO hash ExprInfo
    override void hash(Hash hash) { hash.adds("InfoExpression"); base.hash(hash); }
    override ExprInfo info() { return this.info_; }
}

Type makeNotNullable(Type type) {
    if (auto classType = type.instanceOf(ClassType))
        return classType.makeNotNullable;
    else if (auto intfType = type.instanceOf(IntfType))
        return intfType.makeNotNullable;
    else assert(false);
}

class ASTIfStatement : ASTStatement
{
    (ASTSymbol | ASTVarDeclStatement | ASTVarExtractDeclStatement) test;

    ASTStatement then;

    nullable ASTStatement else_;

    Loc loc;

    this(this.test, this.then, this.else_, this.loc) { }

    override StatementCompileResult compile(Context context)
    {
        mut nullable Statement prelude; // var decl statement
        mut nullable Expression test;
        mut nullable Statement epilog; // either decl value assignment
        mut auto testSucceedsContext = context;
        this.test.case {
            ASTSymbol symbol: {
                test = beExpression3(context, symbol.compile(context), this.loc);
            }
            ASTVarDeclStatement vardecl: {
                auto initial = vardecl.initial.compile(context).beExpressionImplCall(context, vardecl.loc);

                // `if (Foo var = some nullable Foo)`:
                //   - cache `some`, test for nullness, then declare Foo var.
                if (initial.type.zeroInitializable && context.compiler.isClassType(initial.type)) {
                    Type notNullableType = initial.type.makeNotNullable;

                    auto tempInitial = new PairedTemporary(initial.type, initial.info.ownership);
                    auto initTemp = new InitializeTemporaryStatement(tempInitial, initial);

                    prelude = initTemp;
                    // don't worry about memory management - if it's non-null, we'll stick it in a variable,
                    // and that'll take care of refcounting for us.
                    test = new InfoExpression(tempInitial, ExprInfo(false, Ownership.global));

                    auto actualVarDecl = new ASTVarDeclStatement(
                        vardecl.name, mutable=vardecl.mutable, infer=false, uninitialized=false,
                        type=new ASTSymbolHelper(notNullableType),
                        initial=new ASTSymbolHelper(new PointerCast(notNullableType, tempInitial)),
                        vardecl.loc);
                    auto pair = actualVarDecl.compile(context);
                    epilog = pair.statement;
                    testSucceedsContext = pair.context;
                } else {
                    auto pair = vardecl.compile(context);
                    prelude = pair.statement;
                    testSucceedsContext = pair.context;
                    auto stackframe = once new ReferenceExpression(new StackFrame(new Void));
                    auto var = testSucceedsContext.namespace.lookup(
                            vardecl.name, context, this.loc, stackframe)
                        .notNull
                        .beExpression;
                    // TODO lookup() that handles the stackframe thing
                    test = beExpression3(context, var, this.loc);
                }
            }
            ASTVarExtractDeclStatement extractdecl: {
                // validate source is Either
                // TODO extract interface
                auto source = extractdecl.source.compile(context).beExpressionImplCall(context, extractdecl.loc);
                auto sourceType = source.type.instanceOf(Either);
                extractdecl.loc.assert2s(!!sourceType, "source of <- decl must be sumtype, not $(source.type.repr)");
                auto sourceType = sourceType.notNull;
                // FIXME check lifetime here
                auto tempSource = new PairedTemporary(source.type, source.info.ownership);
                auto initTemp = new InitializeTemporaryStatement(tempSource, source);
                // check expected type
                mut uninitialized Type extractType;
                if (extractdecl.infer) {
                    extractType = typeWithoutFail(sourceType);
                } else {
                    extractType = beType(extractdecl.loc, extractdecl.type.compile(context));
                }

                auto ret = pickEither(context, sourceType, tempSource, extractType, extractdecl.loc);
                prelude = initTemp;
                test = ret.test;

                auto actualVarDecl = new ASTVarDeclStatement(
                    extractdecl.name, mutable=extractdecl.mutable, infer=false, uninitialized=false,
                    type=new ASTSymbolHelper(extractType), initial=new ASTSymbolHelper(ret.value),
                    extractdecl.loc);
                auto pair = actualVarDecl.compile(context);
                epilog = pair.statement;
                testSucceedsContext = pair.context;
            }
        }
        auto thenPair = this.then.compile(testSucceedsContext);
        Statement then = unwindScope(thenPair.context, context.namespace, thenPair.statement);
        mut nullable Statement else_;
        if (this.else_) {
            auto elsePair = this.else_.compile(context);
            else_ = unwindScope(elsePair.context, context.namespace, elsePair.statement);
        }

        auto test = truthy(context, test.notNull, this.loc);
        auto ifStmt = new IfStatement(test, sequence2(epilog, then), else_);

        return StatementCompileResult(sequence2(prelude, ifStmt), context);
    }

    override ASTSymbol quote(Quoter quoter) {
        if (!else_) {
            return quoter.compilerCall("astIf", [
                test.case(
                    ASTSymbol symbol: symbol.quote(quoter),
                    ASTVarDeclStatement stmt: stmt.quote(quoter),
                    ASTVarExtractDeclStatement stmt: stmt.quote(quoter)),
                then.quote(quoter),
                quoter.compiler.astIdentifier("null", loc)], loc);
        }
        return quoter.compilerCall("astIf", [
            test.case(
                ASTSymbol symbol: symbol.quote(quoter),
                ASTVarDeclStatement stmt: stmt.quote(quoter),
                ASTVarExtractDeclStatement stmt: stmt.quote(quoter)),
            then.quote(quoter),
            else_.quote(quoter)], loc);
    }
}

nullable ASTIfStatement parseIf(Parser parser, LexicalContext lexicalContext)
{
    parser.begin;
    string identifier = parser.parseIdentifier;
    if (identifier != "if")
    {
        parser.revert;
        return null;
    }
    mut uninitialized (ASTSymbol | ASTVarDeclStatement | ASTVarExtractDeclStatement) test;
    parser.expect("(");
    if (auto vardecl = parseVarDecl(parser, lexicalContext, requireInitializer=true))
        test = vardecl;
    else if (auto varextractdecl = parseVarExtractDecl(parser, lexicalContext))
        test = varextractdecl;
    else test = parseExpression(parser, lexicalContext).notNull;
    parser.expect(")");
    ASTStatement thenStmt = parseStatement(parser, lexicalContext);
    mut nullable ASTStatement elseStatement;
    if (parser.accept("else"))
    {
        elseStatement = parseStatement(parser, lexicalContext);
    }
    parser.commit;
    return new ASTIfStatement(test, thenStmt, elseStatement, parser.loc);
}

nullable ASTStatement parseBreakCont(Parser parser, LexicalContext lexicalContext)
{
    parser.begin;
    auto loc = parser.loc;
    string identifier = parser.parseIdentifier;
    if (identifier == "break")
    {
        parser.expect(";");
        parser.commit;
        return new ASTBreakStatement(loc);
    }
    if (identifier == "continue")
    {
        parser.expect(";");
        parser.commit;
        return new ASTContinueStatement(loc);
    }
    parser.revert;
    return null;
}

class ASTScopeStatement : ASTStatement
{
    ASTStatement[] statements;

    Loc loc;

    this(this.statements, this.loc) { }

    override StatementCompileResult compile(Context context)
    {
        auto statements = new Statement[](this.statements.length);
        mut auto subContext = context;
        for (i, statement in this.statements) {
            auto pair = statement.compile(subContext);

            subContext = pair.context;
            statements[i] = pair.statement;
        }
        return StatementCompileResult(
            unwindScope(subContext, context.namespace, new SequenceStatement(statements)),
            context); // scope ends here
    }

    override ASTSymbol quote(Quoter quoter) {
        auto statements = [statement.quote(quoter) for statement in this.statements];
        return quoter.compilerCall(
            "astScope", [quoter.compiler.astArrayLiteral(statements, this.loc)], this.loc);
    }
}

// just a bunch of statements after each other
// (used for int a, b)
class ASTSequenceStatement : ASTStatement
{
    ASTStatement[] statements;

    Loc loc;

    this(this.statements, this.loc) { }

    override StatementCompileResult compile(Context context)
    {
        auto statements = new Statement[](this.statements.length);
        mut auto subContext = context;
        for (i, statement in this.statements) {
            auto pair = statement.compile(subContext);

            subContext = pair.context;
            statements[i] = pair.statement;
        }
        // not a scope!
        return StatementCompileResult(new SequenceStatement(statements), subContext);
    }

    override ASTSymbol quote(Quoter quoter) {
        auto statements = [arg.quote(quoter) for arg in this.statements];
        return quoter.compilerCall(
            "astSequence", [quoter.compiler.astArrayLiteral(statements, this.loc)], this.loc);
    }
}

nullable ASTScopeStatement parseScope(Parser parser, LexicalContext lexicalContext)
{
    auto loc = parser.loc;
    if (!parser.accept("{"))
    {
        return null;
    }
    mut ASTStatement[] statements;
    while (!parser.accept("}"))
    {
        ASTStatement stmt = parseStatement(parser, lexicalContext);

        statements ~= stmt;
    }
    return new ASTScopeStatement(statements, loc);
}

nullable ASTAssignStatement parseAssignment(Parser parser, LexicalContext lexicalContext)
{
    parser.begin;
    nullable ASTSymbol lhs = parseExpressionLeaf(parser, lexicalContext);
    if (!lhs) {
        parser.revert;
        return null;
    }
    auto loc = parser.loc;
    mut string operator;
    if (parser.accept("=")) { }
    else if (parser.accept("+=")) operator = "+";
    else if (parser.accept("-=")) operator = "-";
    else if (parser.accept("*=")) operator = "*";
    else if (parser.accept("/=")) operator = "/";
    else if (parser.accept("~=")) operator = "~";
    else if (parser.accept("&=")) operator = "&";
    else if (parser.accept("|=")) operator = "|";
    else if (parser.accept("^=")) operator = "^";
    else {
        parser.revert;
        return null;
    }
    mut nullable ASTSymbol expr = parseExpression(parser, lexicalContext);
    if (!expr) {
        parser.fail("expression expected");
    }
    mut ASTSymbol expr = expr.notNull;
    parser.commit;
    if (operator.length) {
        // TODO separate AST construct - avoid double compiling lhs?
        expr = new ASTBinaryOp(operator, lhs.notNull, expr, loc);
    }
    return new ASTAssignStatement(lhs.notNull, expr, parser.loc);
}

class ASTAssignStatement : ASTStatement
{
    ASTSymbol target;

    ASTSymbol value;

    Loc loc;

    this(this.target, this.value, this.loc) {}

    override StatementCompileResult compile(Context context)
    {
        mut Expression value = this.value.compile(context).beExpressionImplCall(context, this.loc);

        // `a[b] = c` special handling
        if (auto astIndex = target.instanceOf(ASTIndexAccess))
        {
            auto base = astIndex.base.compile(context).beExpressionImplCall(context, astIndex.loc);
            auto index = astIndex.index.compile(context).beExpressionImplCall(context, astIndex.loc);
            if (auto assignment = base.type.indexAssignment(context, base, index, value, this.loc))
                return StatementCompileResult(assignment, context);
        }

        auto compiler = context.compiler;

        Symbol target = this.target.compile(context);
        nullable Reference targetref = target.instanceOf(Reference);
        this.loc.assert2s(!!targetref, "target of assignment must be a reference");
        this.loc.assert2s(targetref.info.mutable, "target of assignment must be mutable");
        auto targetref = targetref.notNull;
        // Broadly, this check is the reason that 'borrowed' exists.
        // FIXME hasDestructor
        bool hasDtor = !!targetref.type.endLifetime(context, targetref);
        this.loc.assert2s(
            targetref.info.ownership != Ownership.borrowed || !hasDtor,
            "cannot reassign to borrowed value with destructor");
        auto type = targetref.type;
        value = expectImplicitConvertTo(context, value, type, this.loc);

        /**
         * Order is important here.
         *
         * 1. temporize the RHS. (NO copy ctor!)
         * 2. temporize the old LHS.
         * 3. overwrite the LHS with the temporized RHS. (copy here)
         * 4. destroy the old LHS value.
         *
         * Why this order? Evaluating the RHS might change the LHS address
         * (consider: *f() = *g() - in that case, g() is more likely to
         * invalidate f() than the other way around).
         * Also, the RHS may just *be* the LHS, as in "s = s;"
         * Hence we need to temporize the RHS.
         * The address of LHS is temporized so that the destructor call
         * doesn't lead to a double evaluation.
         * The old LHS value is rescued so that more complex versions of
         * "s = s" don't leave the references in "s" abandoned between
         * endLifetime and moveInto. In other words, since s_2 may depend on s_1,
         * their lifetimes need to overlap.
         *
         * But man, what an ugly-ass dataflow...
         */

        nullable Statement do1(Expression tmpValue) {
            nullable Statement do2(Expression tmpLhsPtr) {
                auto tmpLhs = compiler.dereference(tmpLhsPtr);

                nullable Statement do3(Reference oldLhs) {
                    auto reassign = compiler.move(context, tmpLhs, tmpValue);
                    auto cleanup = oldLhs.type.endLifetime(context, oldLhs);

                    return sequence(reassign, cleanup);
                }
                return compiler.stmtWithTemporary(tmpLhs, &do3);
            }
            return compiler.stmtWithTemporaryExpr(compiler.reference(targetref), &do2);
        }
        return StatementCompileResult(
            compiler.stmtWithTemporaryExpr(value, &do1).notNull,
            context);
    }

    override ASTSymbol quote(Quoter quoter) {
        return quoter.compilerCall("astAssign", [target.quote(quoter), value.quote(quoter)], loc);
    }
}

nullable ASTAssignStatement parseAssignStatement(Parser parser, LexicalContext lexicalContext)
{
    if (auto ret = parseAssignment(parser, lexicalContext))
    {
        parser.expect(";");
        return ret;
    }
    return null;
}

class ASTVarDeclStatement : ASTStatement
{
    string name;

    bool mutable;

    bool infer;

    bool uninitialized;

    nullable ASTSymbol type;

    nullable ASTSymbol initial;

    Loc loc;

    this(this.name, this.mutable, this.infer, this.uninitialized,
        this.type, this.initial, this.loc)
    {
        assert(!uninitialized || mutable);
        assert(infer == !type);
        assert(!infer || initial);
        assert(!uninitialized || !initial);
    }

    override StatementCompileResult compile(Context context)
    {
        mut nullable Type type;
        if (this.type) type = beType(this.loc, this.type.compile(context));
        if (this.initial)
        {
            mut auto initial = beExpression3(context, this.initial.compile(context), this.loc);
            if (this.infer) {
                assert(!type);
                type = initial.type;
            } else {
                assert(!!type);
                initial = expectImplicitConvertTo(context, initial, type.notNull, this.loc);
            }
            auto type = type.notNull;

            if (initial.info.ownership == Ownership.owned && !this.mutable)
            {
                // don't double-own the value, it's covered by its own ownership
                // we aren't mutable, so we can safely borrow the initializer.
                auto decl = new VariableDeclaration(
                    context.namespace, this.name, type, ExprInfo(false, Ownership.borrowed), false);
                auto assign = context.compiler.assignStatement(decl.accessDecl(context.compiler), initial);

                return StatementCompileResult(assign, context.withNamespace(decl));
            }

            // otherwise we'll hold a reference ourselves.
            auto decl = new VariableDeclaration(
                context.namespace, this.name, type, ExprInfo(mutable, Ownership.owned), false);

            return StatementCompileResult(
                move(context, decl.accessDecl(context.compiler), initial),
                context.withNamespace(decl));
        }
        else if (this.uninitialized)
        {
            auto type = type.notNull;
            auto decl = new VariableDeclaration(
                context.namespace, this.name, type, ExprInfo(mutable, Ownership.owned), hidden=false);

            // Still zero-init.
            return StatementCompileResult(
                new AssignStatement(decl.accessDecl(context.compiler), new NullExpr(type)),
                context.withNamespace(decl));
        }
        else
        {
            auto type = type.notNull;
            if (!type.zeroInitializable) {
                this.loc.fail("Cannot declare $(this.name) without initializer: $(type.repr) is not zero-initializable");
            }
            auto decl = new VariableDeclaration(
                context.namespace, this.name, type, ExprInfo(mutable, Ownership.owned), hidden=false);

            return StatementCompileResult(
                new AssignStatement(decl.accessDecl(context.compiler), new NullExpr(type)),
                context.withNamespace(decl));
        }
    }

    override ASTSymbol quote(Quoter quoter) {
        mut uninitialized ASTSymbol varName;
        // auto $foo = 5;
        // -> compiler.astDeclareVar(foo, compiler.astIntLiteral(5))
        if (this.name[0 .. 1] == "\$") {
            varName = quoter.compiler.astIdentifier(this.name[1 .. $], this.loc);
        } else {
            varName = quoter.compiler.astStringLiteral(this.name, this.loc);
        }
        if (this.infer) {
            return quoter.compilerCall("astDeclareVar", [
                varName,
                quoter.compiler.astBoolLiteral(this.mutable, this.loc),
                quoter.compiler.astBoolLiteral(this.uninitialized, this.loc),
                this.initial.quote(quoter)], this.loc);
        }
        if (this.initial) {
            return quoter.compilerCall("astDeclareTypedVar", [
                varName,
                quoter.compiler.astBoolLiteral(this.mutable, this.loc),
                quoter.compiler.astBoolLiteral(this.uninitialized, this.loc),
                this.type.quote(quoter),
                this.initial.quote(quoter)], this.loc);
        }
        return quoter.compilerCall("astDeclareTypedVar", [
            varName,
            quoter.compiler.astBoolLiteral(this.mutable, this.loc),
            quoter.compiler.astBoolLiteral(this.uninitialized, this.loc),
            this.type.quote(quoter),
            quoter.compiler.astIdentifier("null", this.loc)], this.loc);
    }
}

nullable ASTVarDeclStatement parseVarInitialization(
    Parser parser, LexicalContext lexicalContext, bool infer, bool mutable, bool uninitialized,
    nullable ASTSymbol type, string operator, bool requireInitializer)
{
    parser.begin;
    mut nullable ASTSymbol initial;
    mut string name;
    // <- is not as ambiguous as =
    if ((infer || operator == "<-") && parser.accept(operator))
    {
        initial = parseExpression(parser, lexicalContext);
        if (!initial) parser.fail("variable initializer expected");
    }
    else
    {
        name = parser.parseIdentifier;
        if (!name.length)
        {
            parser.revert;
            return null;
        }
        if (infer) {
            if (!parser.accept(operator))
            {
                parser.revert;
                return null;
            }
            initial = parseExpression(parser, lexicalContext);
            if (!initial) parser.fail("variable initializer expected");
        }
        else if (parser.accept(operator)) {
            initial = parseExpression(parser, lexicalContext);
            if (!initial) parser.fail("variable initializer expected");
        } else {
            // FIXME this is very hacky
            if (operator == "=" && parser.accept("<-")) {
                // recognize that we're failing to parse a <- instead of ==
                // FIXME check for both here instead of passing a parameter
                parser.revert;
                return null;
            }
        }
    }
    if (requireInitializer && !initial) {
        parser.revert;
        return null;
    }
    parser.commit;
    return new ASTVarDeclStatement(name, mutable, infer, uninitialized=uninitialized, type, initial, parser.loc);
}

nullable ASTVarDeclStatement parseVarDecl(Parser parser, LexicalContext lexicalContext, bool requireInitializer)
{
    parser.begin;
    mut bool infer = false;
    mut bool mutable = false;
    mut bool uninitialized = false;
    mut nullable ASTSymbol type;
    if (parser.acceptIdentifier("mut")) mutable = true;
    if (parser.acceptIdentifier("uninitialized")) uninitialized = true;
    if (acceptIdentifier(parser, "auto")) {
        infer = true;
    } else {
        type = parser.parseType(lexicalContext);
        if (!type)
        {
            if (mutable) infer = true;
            else {
                parser.revert;
                return null;
            }
        }
    }
    auto decl = parseVarInitialization(
        parser, lexicalContext, infer, mutable, uninitialized, type, "=", requireInitializer);
    if (!decl) {
        parser.revert;
        return null;
    }
    parser.commit;
    return decl;
}

// TODO merge with parseVarDecl
nullable ASTStatement parseMultiVarDecl(Parser parser, LexicalContext lexicalContext)
{
    parser.begin;
    auto loc = parser.loc;
    mut bool infer = false;
    mut bool mutable = false;
    mut bool uninitialized = false;
    mut nullable ASTSymbol type;
    if (parser.acceptIdentifier("mut")) mutable = true;
    if (parser.acceptIdentifier("uninitialized")) uninitialized = true;
    if (acceptIdentifier(parser, "auto")) {
        infer = true;
    } else {
        type = parser.parseType(lexicalContext);
        if (!type)
        {
            parser.revert;
            return null;
        }
    }
    mut ASTStatement[] decls;
    while (true) {
        if (decls.length) parser.expect(",");
        if (auto decl = parseVarInitialization(parser, lexicalContext, infer, mutable, uninitialized, type, "=", false)) {
            decls ~= decl;
        } else if (auto decl2 = parseVarInitialization(
            parser, lexicalContext, infer, mutable, uninitialized, type, "<-", false))
        {
            decls ~= extractFromVarDecl(decl2);
        } else {
            parser.assert_(decls.length == 0, "couldn't parse continuing variable declaration");
            parser.revert;
            return null;
        }
        if (parser.accept(";")) break;
    }
    parser.commit;
    return new ASTSequenceStatement(decls, loc);
}

/**
 * auto a <- foo;
 * int i <- find(...);
 */
class ASTVarExtractDeclStatement : ASTStatement
{
    string name;

    bool mutable;

    bool infer;

    nullable ASTSymbol type;

    ASTSymbol source;

    Loc loc;

    this(this.name, this.mutable, this.infer, this.type, this.source, this.loc)
    {
        assert(!!source);
        assert(infer == !type);
    }

    override StatementCompileResult compile(Context context)
    {
        auto source = this.source.compile(context).beExpressionImplCall(context, this.loc);
        nullable Either sourceType = source.type.instanceOf(Either);
        if (!sourceType) {
            // either can also turn nullables into non-nullables
            if (context.compiler.isClassType(source.type) && source.type.zeroInitializable) {
                return compileObjectDenullFallback(context, source);
            }
        }
        this.loc.assert2s(!!sourceType, "Source type for extract decl must be sumtype");
        auto sourceType = sourceType.notNull;

        mut uninitialized Type type;
        if (this.type) type = beType(this.loc, this.type.compile(context));
        else type = typeWithoutFail(sourceType);

        bool inTargetType(Type source) {
            if (auto either = type.instanceOf(Either))
                return [any a.type.same(source) for a in either.types];
            return type.same(source);
        }

        auto decl = new VariableDeclaration(
            context.namespace, this.name, type, ExprInfo(mutable=this.mutable, Ownership.owned), false);
        auto astType = new ASTSymbolHelper(type);

        mut ASTEitherCaseExprCase[] cases;
        for (i, entry in sourceType.types) {
            auto sourceMemberType = entry.type;
            bool return_ = !sourceMemberType.inTargetType;

            mut uninitialized ASTSymbol expr;
            // separate statement so that the loc gets attributed to the source site
            auto ret = context.compiler.astReturn(context.compiler.astIdentifier("a", this.loc), this.loc);
            // TODO T.init
            if (return_) expr = context.compiler.$expr ({ $stmt ret; mut uninitialized $astType x; x; });
            else expr = context.compiler.$expr ({ $astType x = a; x; });

            cases ~= ASTEitherCaseExprCase(
                loc=this.loc,
                type=new ASTSymbolHelper(sourceMemberType),
                identifier="a",
                return_=false,
                expr=expr);
        }

        auto switch_ = new ASTEitherCaseExpr(this.loc, this.source, cases);
        auto switchExpr = switch_.compile(context).beExpression;

        return StatementCompileResult(
            move(context, decl.accessDecl(context.compiler), switchExpr),
            context.withNamespace(decl));
    }

    StatementCompileResult compileObjectDenullFallback(Context context, Expression source) {
        mut uninitialized Type type;
        if (this.type) type = beType(this.loc, this.type.compile(context));
        else type = source.type.makeNotNullable;
        assert(type.same(source.type.makeNotNullable));

        auto astSource = new ASTSymbolHelper(source);
        auto astType = new ASTSymbolHelper(type);
        auto initial = context.compiler.$expr ({
            mut uninitialized $astType result;
            if (auto a = $astSource) result = a;
            else return null;
            result;
        });
        auto decl = new VariableDeclaration(
            context.namespace, this.name, type, ExprInfo(mutable=this.mutable, Ownership.owned), false);

        return StatementCompileResult(
            move(context, decl.accessDecl(context.compiler), initial.compile(context).beExpression),
            context.withNamespace(decl));
    }

    override ASTSymbol quote(Quoter quoter) { print("cannot quote 'ASTVarExtractDeclStatement'!"); assert(false); }

    /*
    FIXME: static in class
    static ASTVarExtractDeclStatement fromVarDecl(ASTVarDeclStatement decl)
    {
        return new ASTVarExtractDeclStatement(
            name=decl.name, auto_=decl.infer, type=decl.type,
            source=decl.initial, loc=decl.loc);
    }
    */
}

ASTVarExtractDeclStatement extractFromVarDecl(ASTVarDeclStatement decl)
{
    return new ASTVarExtractDeclStatement(
        name=decl.name, mutable=decl.mutable, infer=decl.infer, type=decl.type,
        source=decl.initial.notNull, loc=decl.loc);
}

nullable ASTVarExtractDeclStatement parseVarExtractDecl(Parser parser, LexicalContext lexicalContext)
{
    parser.begin;
    mut bool infer = false;
    mut bool mutable = false;
    mut nullable ASTSymbol type;
    // TODO
    // if (parser.acceptIdentifier("mut")) mutable = true;
    if (acceptIdentifier(parser, "auto")) {
        infer = true;
    } else {
        type = parser.parseType(lexicalContext);
        if (!type)
        {
            if (mutable) infer = true;
            else {
                parser.revert;
                return null;
            }
        }
    }
    auto decl = parseVarInitialization(
        parser, lexicalContext, infer, mutable, uninitialized=false, type, "<-", requireInitializer=true);
    if (!decl) {
        parser.revert;
        return null;
    }
    parser.commit;
    return extractFromVarDecl(decl.notNull);
}

class ASTNestedFunctionDeclStatement : ASTStatement
{
    ASTFunction fun;

    this(this.fun) { }

    override StatementCompileResult compile(Context context)
    {
        auto fun = this.fun.compile(context);
        auto latent = fun.instanceOf(LatentNestedFunction);

        assert(!!latent);
        return StatementCompileResult(
            new NoopStatement,
            context.withNamespace(new SymbolAlias(
                context.namespace, latent.fun.name, latent)));
    }

    override ASTSymbol quote(Quoter quoter) {
        print("cannot quote 'ASTNestedFunctionDeclStatement'");
        assert(false);
    }
}

nullable ASTNestedFunctionDeclStatement parseNestedFunctionDecl(Parser parser, LexicalContext lexicalContext)
{
    if (auto fun_ = parseFunction(parser, lexicalContext))
        return new ASTNestedFunctionDeclStatement(fun_);
    return null;
}

class ASTWhile : ASTStatement
{
    ASTSymbol cond;

    ASTStatement body_;

    Loc loc;

    this(this.cond, this.body_, this.loc) { }

    override StatementCompileResult compile(Context context)
    {
        auto label = context.getLabel;
        auto loopContext = context.withNamespace(new LabelLoopScope(label, context.namespace));
        auto condExpr = truthy(context, beExpression3(context, this.cond.compile(loopContext), this.loc), this.loc);
        auto bodyPair = this.body_.compile(loopContext);
        auto bodyStmt = unwindScope(bodyPair.context, context.namespace, bodyPair.statement);

        return StatementCompileResult(
            new Loop(label, null, condExpr, bodyStmt, null),
            context);
    }

    override ASTSymbol quote(Quoter quoter) { print("cannot quote 'ASTWhileStatement'"); assert(false); }
}

nullable ASTWhile parseWhile(Parser parser, LexicalContext lexicalContext)
{
    if (!acceptIdentifier(parser, "while"))
    {
        return null;
    }
    parser.expect("(");
    ASTSymbol cond = parseExpression(parser, lexicalContext).notNull;
    parser.expect(")");
    ASTStatement body_ = parseStatement(parser, lexicalContext);

    return new ASTWhile(cond, body_, parser.loc);
}

nullable ASTExprStatement parseExprStatement(Parser parser, LexicalContext lexicalContext)
{
    parser.begin;
    auto loc = parser.loc;
    nullable ASTSymbol value = parseExpression(parser, lexicalContext);
    if (!value)
    {
        parser.revert;
        return null;
    }
    auto value = value.notNull;
    parser.expect(";");
    parser.commit;
    return new ASTExprStatement(value, loc);
}

class ASTStatementExpression : ASTSymbol
{
    ASTStatement[] statements;

    ASTSymbol expr;

    Loc loc;

    this(this.statements, this.expr, this.loc) {
        for (statement in this.statements)
            assert(!!statement);
        assert(!!expr);
    }

    override Expression compile(Context context) {
        mut Context context = context;
        auto baseNamespace = context.namespace;
        auto statements = new Statement[](this.statements.length);
        for (i, statement in this.statements) {
            auto result = statement.compile(context);

            statements[i] = result.statement;
            context = result.context;
        }
        auto resultValue = beExpression3(context, this.expr.compile(context), loc);
        auto resultType = resultValue.type;

        // as ASTReturn
        auto temp = new PairedTemporary(resultType, Ownership.owned);
        auto init = new UninitializeTemporaryStatement(temp);

        Statement assignEscapeValue() {
            if (resultValue.info.ownership == Ownership.gifted)
                return new AssignStatement(temp, resultValue);
            if (auto stmt = resultType.copyInto(context, temp, resultValue))
                return stmt;
            return new AssignStatement(temp, resultValue);
        }
        auto initAndCopy = sequence(init, assignEscapeValue);

        auto sequence = new SequenceStatement(statements ~ initAndCopy);
        // unwind variables declared in the ({ })
        auto stmtExpr = unwindScope(context, baseNamespace, sequence);

        return new StatementExpression(stmtExpr, temp, true);
    }

    // TODO on CompilerBase?
    override ASTSymbol quote(Quoter quoter) {
        auto quotedStatements = new ASTSymbol[](this.statements.length);
        for (i, statement in this.statements)
            quotedStatements[i] = statement.quote(quoter);
        auto type = quoter.compiler.astIdentifier("ASTStatement", __HERE__);
        return quoter.compilerCall("astStatementExpression", [
            quoter.compiler.astArrayLiteralTypeHint(type, quotedStatements, loc),
            this.expr.quote(quoter)
        ], loc);
    }
}

// ({ ...; ...; })
// value of last expression is value of statement expression
nullable ASTStatementExpression parseStatementExpr(Parser parser, LexicalContext lexicalContext)
{
    auto loc = parser.loc;
    if (!parser.accept("({")) return null;
    mut ASTStatement[] statements;
    while (!parser.accept("})")) {
        statements ~= parseStatement(parser, lexicalContext);
    }
    if (statements.length == 0) parser.fail("expected at least one statement");

    if (auto exprStmt = statements[$ - 1].instanceOf(ASTExprStatement))
        return new ASTStatementExpression(statements[0 .. $ - 1], exprStmt.value, loc);

    parser.fail("last statement in statement expression must be an expression");
}

class ASTForLoop : ASTStatement
{
    ASTStatement declareLoopVar;

    ASTSymbol condition;

    ASTStatement step;

    ASTStatement body_;

    Loc loc;

    this(this.declareLoopVar, this.condition, this.step, this.body_, this.loc) { }

    override StatementCompileResult compile(Context context)
    {
        auto declPair = this.declareLoopVar.compile(context);
        auto forScope = declPair.context;
        Expression condition = beExpression3(context, this.condition.compile(forScope), this.loc);
        auto stepPair = this.step.compile(forScope);

        string label = context.getLabel;
        Namespace declScope = new LabelLoopScope(label, forScope.namespace);
        Context breakContScope = forScope.withNamespace(declScope);
        auto bodyPair = this.body_.compile(breakContScope);

        return StatementCompileResult(
            new Loop(label, declPair.statement, condition, bodyPair.statement, stepPair.statement),
            context);
    }

    override ASTSymbol quote(Quoter quoter) {
        return quoter.compilerCall("astForLoop", [
            declareLoopVar.quote(quoter),
            condition.quote(quoter),
            step.quote(quoter),
            body_.quote(quoter)], loc);
    }
}

nullable ASTForLoop parseFor(Parser parser, LexicalContext lexicalContext)
{
    auto loc = parser.loc;
    if (!acceptIdentifier(parser, "for"))
    {
        return null;
    }
    parser.expect("(");
    nullable ASTVarDeclStatement varDecl = parseVarDecl(parser, lexicalContext, requireInitializer=false);
    if (!varDecl) {
        parser.fail("loop declaration expected");
    }
    parser.expect(";");
    ASTSymbol condition = parseExpression(parser, lexicalContext).notNull;
    parser.expect(";");
    ASTStatement step = parseAssignment(parser, lexicalContext);
    parser.expect(")");
    ASTStatement body_ = parseStatement(parser, lexicalContext);

    return new ASTForLoop(varDecl.notNull, condition, step, body_, loc);
}

class ASTStatementStatement : ASTStatement
{
    Loc loc;

    string variable;

    this(this.loc, this.variable) { }

    override StatementCompileResult compile(Context context) {
        this.loc.fail("\$stmt used outside quasiquote");
    }

    override ASTSymbol quote(Quoter quoter) {
        return quoter.compiler.astIdentifier(variable, loc);
    }
}

/**
 * Parse a statement that indicates to the quoter that an ASTStatement variable should be inserted.
 */
nullable ASTStatementStatement parseStatementStatement(Parser parser, LexicalContext lexicalContext)
{
    auto loc = parser.loc;
    if (!parser.accept("\$stmt")) return null;
    auto variable = parser.parseIdentifier;
    if (!variable.length) parser.fail("ASTStatement variable expected");
    parser.expect(";");
    return new ASTStatementStatement(loc, variable);
}

(ExtForVarDecl | fail :fail) parseExtForVarDecl(Parser parser, LexicalContext lexicalContext)
{
    parser.begin;
    if (parser.acceptIdentifier("auto")) {
        auto loc = parser.loc;
        auto identifier = parser.parseIdentifier;
        if (!identifier.length) {
            parser.revert;
            return :fail;
        }
        parser.commit;
        // (auto i in 0..10)
        return (:auto_, loc, identifier);
    }
    {
        auto loc = parser.loc;
        auto ident = parser.parseIdentifier;
        parser.begin;
        // peek ahead
        if (parser.accept(",") || parser.acceptIdentifier("in") || parser.accept("<-")) {
            // (i in 0..10)
            parser.revert; // revert peek
            parser.commit; // commit identifier
            return (:none, loc, ident);
        }
        parser.revert; // revert failed peek
        parser.revert; // revert identifier parse
        parser.begin;
    }
    // (int i in 0..10)
    auto type = parser.parseType(lexicalContext);
    if (!type) {
        parser.revert;
        return :fail;
    }
    auto type = type.notNull;
    auto loc = parser.loc;
    auto ident = parser.parseIdentifier;
    if (!ident.length) {
        parser.revert;
        return :fail;
    }
    parser.commit;
    return (type, loc, ident);
}

class ASTExtForLoop : ASTStatement
{
    ASTSymbol source;

    ExtForVarDecl var1;

    (ExtForVarDecl | :none) var2;

    ASTStatement body_;

    Loc loc;

    this(this.source, this.var1, this.var2, this.body_, this.loc) { }

    override StatementCompileResult compile(Context context)
    {
        auto source = this.source.compile(context).beExpressionImplCall(context, loc);
        auto compiler = context.compiler;
        auto astSource = new ASTSymbolHelper(source);

        if (source.type.instanceOf(Array)) {
            auto valueMatch = var2.case(ExtForVarDecl a: a, (:none): var1);
            string keyName = var2.case(ExtForVarDecl a: var1.name, (:none): "__loopvar");
            ASTSymbol keyIdent = compiler.astIdentifier(keyName, __HERE__);
            ASTSymbol loopType = var2.case(
                ExtForVarDecl a: var1.type.case(
                    (:auto_): compiler.astIdentifier("size_t", __HERE__),
                    (:none): compiler.astIdentifier("size_t", __HERE__),
                    ASTSymbol type: type),
                (:none): compiler.astIdentifier("size_t", __HERE__));

            string valueName = valueMatch.name;
            ASTStatement declIdentifier = valueMatch.type.case(
                (:auto_): compiler.$stmt auto $valueName = __source[$keyIdent]; ,
                (:none): compiler.$stmt auto $valueName = __source[$keyIdent]; ,
                ASTSymbol type: compiler.$stmt $type $valueName = __source[$keyIdent]; );

            ASTStatement loop = compiler.$stmt {
                auto __source = $astSource;
                for (mut $loopType $keyName = 0; $keyIdent < __source.length; $keyIdent += 1) {
                    $stmt declIdentifier;
                    $stmt body_;
                }
            };
            return loop.compile(context);
        }
        if (var2 != :none) {
            // for (var1, var2 in
            string indexName = var1.name;
            auto index = compiler.astIdentifier(indexName, __HERE__);
            ExtForVarDecl value() {
                var2.case {
                    ExtForVarDecl a: return a;
                    (:none): assert(false);
                }
            }
            auto valueName = value.name, valueType = value.type;

            ASTStatement declIdentifier = valueType.case(
                (:auto_): compiler.$stmt auto $valueName = __source.front; ,
                (:none): compiler.$stmt auto $valueName = __source.front; ,
                ASTSymbol type: compiler.$stmt $type $valueName = __source.front; );
            ASTStatement rangeLoop = compiler.$stmt {
                mut size_t $indexName = 0;
                for (mut auto __source = $astSource; !__source.empty; __source = __source.next) {
                    $stmt declIdentifier;
                    $stmt body_;
                    $index = $index + 1;
                }
            };
            return rangeLoop.compile(context);
        }
        string valueName = var1.name;

        ASTStatement declIdentifier = var1.type.case(
            (:auto_): compiler.$stmt auto $valueName = __source.front; ,
            (:none): compiler.$stmt auto $valueName = __source.front; ,
            ASTSymbol type: compiler.$stmt $type $valueName = __source.front; );
        ASTStatement rangeLoop = compiler.$stmt {
            for (mut auto __source = $astSource; !__source.empty; __source = __source.next) {
                $stmt declIdentifier;
                $stmt body_;
            }
        };
        context.pushErrorLoc(this.loc);
        auto ret = rangeLoop.compile(context);
        context.popErrorLoc;
        return ret;
    }

    override ASTSymbol quote(Quoter quoter) {
        mut ASTSymbol[] args;
        args ~= source.quote(quoter);
        args ~= var1.quoteExtForVarDecl(quoter);
        args ~= var2.case(
            ExtForVarDecl decl: decl.quoteExtForVarDecl(quoter),
            (:none): new ASTSymbolIdentifier("none"));
        args ~= body_.quote(quoter);

        return quoter.compilerCall("astExtForLoop", args, loc);
    }
}

// This is a placeholder for proper range semantics.
nullable ASTStatement parseExtFor(Parser parser, LexicalContext lexicalContext)
{
    parser.begin;
    auto loc = parser.loc;
    if (!acceptIdentifier(parser, "for") || !parser.accept("(")) {
        parser.revert;
        return null;
    }
    auto compiler = lexicalContext.compiler;
    // TODO: parseExtForVarDecl(...).discard((:fail): { parser.revert; return null; });
    auto match1 = parseExtForVarDecl(parser, lexicalContext);
    mut uninitialized ExtForVarDecl match1decl;
    match1.case {
        (:fail): {
            parser.revert;
            return null;
        }
        ExtForVarDecl decl: {
            match1decl = decl;
        }
    }
    mut bool keyValueLoop = false;
    mut uninitialized ExtForVarDecl match2decl;
    if (parser.accept(",")) {
        parseExtForVarDecl(parser, lexicalContext).case {
            (:fail): {
                parser.revert;
                return null;
            }
            ExtForVarDecl decl: {
                match2decl = decl;
            }
        }
        keyValueLoop = true;
    }
    if (parser.accept("<-")) {
        print("warn: $(parser.loc.location): use 'in'");
    } else if (!parser.acceptIdentifier("in")) {
        parser.revert;
        return null;
    }
    auto source = parseExpression(parser, lexicalContext);
    if (!source) parser.fail("expression expected");
    auto source = source.notNull;
    // array loop, for (i, k in array)
    parser.expect(")");
    auto body_ = parseStatement(parser, lexicalContext);
    parser.commit;

    if (keyValueLoop) {
        return new ASTExtForLoop(source, match1decl, match2decl, body_, loc);
    }
    return new ASTExtForLoop(source, match1decl, :none, body_, loc);
}

class ASTDestroy : ASTSymbol
{
    ASTSymbol target;

    Loc loc;

    this(this.target, this.loc) { }

    override Expression compile(Context context) {
        auto expr = beExpression3(context, this.target.compile(context), loc);
        auto stmt = destroy(context, expr);
        if (!stmt) return once new VoidExpression;
        return new StatementExpression(stmt, once new VoidExpression, false);
    }

    override ASTSymbol quote(Quoter quoter) {
        return quoter.compilerCall("astDestroy", [target.quote(quoter)], loc);
    }
}

// manually end lifetime of a value
nullable ASTSymbol parseDestroy(Parser parser, LexicalContext lexicalContext)
{
    auto loc = parser.loc;
    if (!acceptIdentifier(parser, "__destroy"))
        return null;
    parser.expect("(");
    nullable ASTSymbol target = parseExpression(parser, lexicalContext);
    if (!target) parser.fail("expression expected");
    parser.expect(")");

    return new ASTDestroy(target.notNull, loc);
}

nullable ASTDeclaration parseDeclaration(Parser parser, LexicalContext lexicalContext) {
    auto loc = parser.loc;
    if (auto import_ = parseImportStatement(parser, lexicalContext)) {
        loc.assert2s(!import_.isMacroImport, "can't import macro in function");
        auto task = new ImportTask(lexicalContext, import_);
        auto pool = lexicalContext.compiler.workPool;
        auto id = import_.repr;

        pool.queue(id, task);
        return new ASTImport(import_, (pool, id));
    }
    if (auto aliasDecl = parseAliasDecl(parser, lexicalContext)) {
        return aliasDecl;
    }
    if (auto structDecl = parseStructDecl(parser, lexicalContext)) {
        return structDecl;
    }
    if (auto templateDecl = parseTemplateDecl(parser, lexicalContext)) {
        return templateDecl;
    }
    return null;
}

ASTStatement parseStatement(Parser parser, LexicalContext lexicalContext)
{
    auto loc = parser.loc;
    if (ASTStatement stmt = parseReturn(parser, lexicalContext))
        return stmt;
    if (ASTStatement stmt = parseIf(parser, lexicalContext))
        return stmt;
    if (ASTStatement stmt = parseBreakCont(parser, lexicalContext))
        return stmt;
    if (ASTStatement stmt = parseWhile(parser, lexicalContext))
        return stmt;
    if (ASTStatement stmt = parseExtFor(parser, lexicalContext))
        return stmt;
    if (ASTStatement stmt = parseFor(parser, lexicalContext))
        return stmt;
    if (ASTStatement stmt = parseScope(parser, lexicalContext))
        return stmt;
    // FIXME why did this work?!
    // if (ASTStatement decl = parseDeclaration(parser, lexicalContext))
    if (ASTDeclaration decl = parseDeclaration(parser, lexicalContext))
        return new ASTDeclarationStatement(decl);
    if (ASTStatement stmt = parseNestedFunctionDecl(parser, lexicalContext))
        return stmt;
    if (ASTStatement stmt = parseStatementStatement(parser, lexicalContext))
        return stmt;
    if (ASTStatement stmt = parseMultiVarDecl(parser, lexicalContext))
        return stmt;
    if (ASTStatement stmt = parseAssignStatement(parser, lexicalContext))
        return stmt;
    if (ASTStatement stmt = parseWithStatement(parser, lexicalContext))
        return stmt;
    if (ASTStatement stmt = parseEitherCaseStmt(parser, lexicalContext))
        return stmt;
    {
        auto args = new ParseStatementArgs(parser, lexicalContext);
        lexicalContext.macroState.applyMacro(args);
        if (ASTStatement ret = args.statement) return ret;
    }
    if (ASTStatement stmt = parseExprStatement(parser, lexicalContext))
        return stmt;
    parser.fail("statement expected");
}

class ModuleEntry
{
    string name;

    Symbol value;

    this(this.name, this.value) { }
}

struct ASTModuleEntry
{
    Protection protection;

    (ASTImport | ASTSymbol | ASTExternFunction | ASTExternVariable
        | ASTDeclaration | ASTUnitTest
        | ASTPragma
    ) symbol;
}

class ASTModule : ASTModuleBase
{
    Loc loc;

    string name;

    Package pak;

    MacroState macroState;

    // separated from this module with a divergent macro state
    // because we hit a macro declaration
    nullable ASTModule parent;

    int depth; // count from parent = null

    ASTModuleEntry[] entries;

    ASTDeclaration[string] symbolAliases;

    this(this.loc, this.name, this.pak, this.macroState, this.parent) {
        if (this.parent) this.depth = this.parent.depth + 1;
        this.mangledName = mangleModuleName(this.name, this.pak, this.depth);
    }

    void addAliasDecl(string name, Protection protection, ASTSymbol target)
    {
        auto decl = new ASTAliasDecl(name, target);
        this.entries ~= ASTModuleEntry(protection, decl);
        assert(!this.symbolAliases.has(name));
        this.symbolAliases[name] = decl;
    }

    nullable ASTSymbol getSymbolAlias(string name) {
        if (this.symbolAliases.has(name)) {
            auto decl = this.symbolAliases[name];
            if (auto alias_ = decl.instanceOf(ASTAliasDecl)) {
                return alias_.target;
            }
        }
        return null;
    }
}

string mangleModuleName(string name, Package pak, int depth)
{
    // TODO non-nullable by default
    assert(!!pak);
    mut string mangledName;
    // mangledName = [(a == '.') ? '_' : a for a in name];
    for (i in 0 .. name.length) {
        if (name[i .. i + 1] == ".") mangledName ~= "_";
        else mangledName ~= name[i .. i + 1];
    }
    mut string patchedPakName;
    if (pak.name == "compiler") patchedPakName = "compiler$(itoa(__GENERATION__))";
    else patchedPakName = pak.name;

    string combined = "$(patchedPakName)_module_$mangledName";

    if (depth) return "$(combined)_d$depth";
    return combined;
}

struct Nothing { }

class NotYetResolvedSymbol : PendingSymbol
{
    Resolver resolver;

    int index;

    Loc loc;

    this(this.resolver, this.index, this.loc) { }

    override Symbol resolve() {
        auto entry = resolver.entries[index];
        if (auto resolution = entry.resolution) {
            return resolution;
        }
        loc.fail("Dependency cycle while resolving $(entry.name).");
    }
}

struct ResolvingModuleEntry
{
    string name;

    Protection protection;

    bool resolving;

    nullable Symbol resolution;

    (ASTFunction | ASTClassDecl | ASTIntfDecl | ASTStruct | ASTAliasDecl | ASTUnitTest | Nothing) symbol;
}

class Resolver
{
    ResolvingModuleEntry[] entries;

    this() { }

    void add(
        string name,
        Protection protection,
        (ASTFunction | ASTClassDecl | ASTIntfDecl | ASTStruct | ASTAliasDecl | ASTUnitTest) astSymbol)
    {
        this.entries ~= ResolvingModuleEntry(name, protection, false, null, astSymbol);
    }

    void register(string name, Protection protection, Symbol symbol)
    {
        int hit = cast(int) [first i for i, a in this.entries where a.name == name else -1];
        if (hit == -1) {
            entries ~= ResolvingModuleEntry(name, protection, resolving=false, symbol, Nothing());
            return;
        }
        entries[hit].resolution = symbol;
    }

    nullable Symbol lookup(Context context, Loc loc, string name, Protection protection)
    {
        bool allowed(Protection entry) {
            if (protection == Protection.public_)
                return entry == Protection.public_;
            return true;
        }
        int hit = cast(int) [
            first i for i, a in this.entries
            where a.name == name && allowed(a.protection)
            else -1];
        if (hit == -1) return null;
        if (this.entries[hit].resolving) {
            return new NotYetResolvedSymbol(this, hit, loc);
        }
        resolve(context, loc, hit);

        mut nullable Symbol ret = this.entries[hit].resolution;
        if (!ret) return null;
        if (auto latent = ret.instanceOf(LatentSymbol)) {
            // we're at global scope; no frame pointer will be applied.
            ret = latent.resolve(context, null);
        }
        return ret;
    }

    void resolve(Context context, Loc loc, int i)
    {
        if (!this.entries[i].resolution) {
            if (this.entries[i].resolving)
            {
                loc.fail("Dependency cycle while resolving $(this.entries[i].name).");
            }
            this.entries[i].resolving = true;
            this.entries[i].resolution = this.entries[i].symbol.case(
                // TODO compile function statement immediately too
                ASTFunction fun: fun.compile(context)
                    .instanceOf(FunctionDeclaration).notNull,
                ASTUnitTest ut: ut.compile(context),
                ASTClassDecl decl: decl.compile(context),
                ASTIntfDecl decl: decl.compile(context),
                ASTStruct struct_: struct_.compile(context),
                ASTAliasDecl decl: decl.target.compile(context),
                Nothing _: ({ assert(false); cast(Symbol) null; }));
            this.entries[i].resolving = false;
        }
    }

    void resolveAll(Context context)
    {
        for (i in 0 .. this.entries.length) {
            resolve(context, __HERE__, cast(int) i);
        }
    }
}

extern(C) size_t poly_hash_whole_string(string s);

class Module : ModuleBase
{
    Loc loc;

    // whenever macro state changes, we begin a new submodule
    // when this happens, this member references the previous module.
    nullable Module parentModule;

    Resolver resolver;

    (FinishedSymbol | Symbol)[] anonymousEntries;

    // filled in by compile()
    FinishedSymbol[] finishedSymbols;

    size_t hash; // hash of mangled name

    // Inert imports have no lookup effect, they solely define a module dependency
    (Module mod, (:inert|:private_|:public_) visibility, string[] symbols)[] imports;

    this(this.loc, this.name, this.mangledName, this.parentModule) {
        this.isContextScope = false;
        this.resolver = new Resolver;
        this.hash = poly_hash_whole_string(mangledName);
    }

    void add(
        string name, Protection protection,
        (ASTFunction | ASTClassDecl | ASTIntfDecl | ASTStruct | ASTAliasDecl | ASTUnitTest) symbol)
    {
        this.resolver.add(name, protection, symbol);
    }

    override void track((Symbol | FinishedSymbol) symbol)
    {
        this.anonymousEntries ~= symbol;
    }

    void register(string name, Protection protection, Symbol symbol)
    {
        this.resolver.register(name, protection, symbol);
    }

    override void addImport(ModuleBase module_, bool public_, string[] symbols) {
        auto module_ = module_.instanceOf(Module).notNull;
        // TODO error?
        // TODO merge symbols?
        if ([any a.mod.mangledName == module_.mangledName for a in this.imports])
            return;
        if (public_)
            this.imports ~= (module_, :public_, symbols);
        else
            this.imports ~= (module_, :private_, symbols);
    }

    override void addModuleDependency(ModuleBase module_) {
        string[] empty;
        // TODO , [])
        this.imports ~= (module_.instanceOf(Module).notNull, :inert, empty);
    }

    override string mangle() { return this.mangledName; }

    void walkModules(bool delegate(Module) cb)
    {
        if (this.parentModule)
            this.parentModule.walkModules(cb);
        if (!cb(this)) return;
        [import_.mod.walkModules(cb) for import_ in imports];
    }

    override void compile(Context context)
    {
        auto compiler = context.compiler, platform = context.platform;

        this.resolver.resolveAll(context);

        void walkSymbols(void delegate((Symbol | FinishedSymbol)) dg) {
            for (entry in this.resolver.entries) {
                dg(entry.resolution.notNull);
            }
            // must be a regular for loop so we can add entries during iteration
            for (mut int i = 0; i < this.anonymousEntries.length; i += 1) {
                dg(anonymousEntries[i]);
            }
        }

        void resolveClassIntf((Symbol | FinishedSymbol) sym) {
            sym.case {
                Symbol sym: {
                    if (auto classType = sym.instanceOf(ClassType)) {
                        classType.class_.resolve(context);
                    }
                    if (auto intfType = sym.instanceOf(IntfType)) {
                        intfType.intf.resolve(context);
                    }
                }
                FinishedSymbol sym: { }
            }
        }

        assert(finishedSymbols.length == 0);
        void compileSymbol((Symbol | FinishedSymbol) symbol) {
            symbol.case {
                Symbol symbol: {
                    if (Function fun = symbol.instanceOf(Function)) {
                        finishedSymbols ~= fun.compile(compiler, platform);
                    }
                    if (IntfType intfType = symbol.instanceOf(IntfType)) {
                        finishedSymbols ~= new DefineClassIntf(intfType.intf);
                    }
                    if (ClassType classType = symbol.instanceOf(ClassType)) {
                        finishedSymbols ~= new DefineClassIntf(classType.class_);
                        for (method in classType.class_.methods)
                            finishedSymbols ~= method.compile(compiler, platform);
                    }
                    if (Struct struct_ = symbol.instanceOf(Struct)) {
                        for (method in struct_.methods)
                            finishedSymbols ~= method.instanceOf(StructMethod).compile(compiler, platform);
                    }
                }
                FinishedSymbol symbol:
                    finishedSymbols ~= symbol;
            }
        }

        /**
         * TODO
         * This is hacky as heck.
         * It's written by just poking things until it works.
         * Work out a saner way.
         */
        walkSymbols(&resolveClassIntf);
        walkSymbols(&compileSymbol);
    }

    void emit(Generator generator)
    {
        if (!generator.once(mangle))
            return;

        assert(!generator.fun);

        [symbol.emit(generator) for symbol in this.finishedSymbols];
    }

    // TODO merge import search with lookup()
    nullable Symbol lookupPublic(Context context, Loc loc, string name)
    {
        // not counting imports (non-transitive)
        auto ret = this.resolver.lookup(context, loc, name, Protection.public_);
        if (ret) return ret;
        // transitive public import search
        for (import_ in this.imports) {
            // FIXME !=
            if (!import_.visibility.case((:inert): false, (:private_): false, (:public_): true))
                continue;
            // TODO yet another import class? struct? we've written this line three times now...
            auto importMod = import_.mod;
            assert(!!importMod);
            if (!import_.symbols.length || [any a == name for a in import_.symbols]) {
                if (Symbol entry = importMod.lookupPublic(context, loc, name))
                    return entry;
            }
        }
        return null;
    }

    override nullable Symbol lookup(string name, Context context, Loc loc, nullable Expression frame)
    {
        if (!!frame) { print("error: lookup with frame propagated to Module"); assert(false); }

        if (auto symbol = this.resolver.lookup(context, loc, name, Protection.private_))
            return symbol;
        for (import_ in this.imports) {
            // FIXME !=
            if (import_.visibility.case((:inert): true, (:private_): false, (:public_): false))
                continue;
            auto importMod = import_.mod;
            assert(!!importMod);
            if (!import_.symbols.length || [any a == name for a in import_.symbols]) {
                if (Symbol entry = importMod.lookupPublic(context, loc, name))
                    return entry;
            }
        }
        assert(!this.parent);
        if (this.parentModule)
            return this.parentModule.lookup(name, context, loc, frame);
        return null;
    }
}

abstract class ParserHelper
{
    abstract void proxyCall(ASTModule astModule, string function_, MacroState macroState)
    {
        assert(false);
    }
}

class ExternCVariable : Expression
{
    string name;

    this(this.type, this.name) { }

    override int emit(Generator output) {
        return output.fun.globalVar(this.name, this.type.emit(output.platform));
    }
    override ExprInfo info() {
        return ExprInfo(true, Ownership.global);
    }
    override void hash(Hash hash) {
        hash.adds("ExternCVariable");
        this.type.hash(hash);
        hash.adds(this.name);
    }
}

class ASTExternVariable
{
    ASTSymbol type;

    string name;

    Loc loc;

    this(this.type, this.name) { }

    ExternCVariable compile(Context context)
    {
        return new ExternCVariable(beType(this.loc, this.type.compile(context)), this.name);
    }
}

class ASTExternFunction
{
    string name;

    ASTSymbol ret;

    ASTParameter[] params;

    bool variadic;

    Loc retLoc;

    this(this.name, this.ret, this.params, this.variadic, this.retLoc) { }

    FunctionDeclaration compile(Context context)
    {
        auto params = [
            Parameter(false, param.name, false, beType(param.loc, param.type.compile(context)))
            for param in this.params];

        auto decl = new FunctionDeclaration(this.name, beType(this.retLoc, this.ret.compile(context)), params);
        decl.variadic = this.variadic;

        return decl;
    }

    string repr() {
        string reprParam(ASTParameter param) {
            if (param.name.length == 0) return param.type.repr;
            return param.type.repr ~ " " ~ param.name;
        }
        return "extern(C) $(ret.repr) $name(" ~ [join ", " reprParam(param) for param in params] ~ ")";
    }
}

(ASTExternFunction | ASTExternVariable | :none) parseExtern(Parser parser, LexicalContext lexicalContext)
{
    if (parser.accept("extern"))
    {
        parser.expect("(");
        parser.expect("C");
        parser.expect(")");
        auto retLoc = parser.loc;
        ASTSymbol type = parser.parseType(lexicalContext).notNull;
        string name = parser.parseIdentifier;
        if (!name.length) parser.fail("identifier expected");
        if (parser.accept("(")) {
            auto params = parseIdentifierList(parser, lexicalContext, variadics=true);
            parser.expect(";");
            return new ASTExternFunction(name, type, params=params.params, variadic=params.variadic, retLoc);
        } else {
            parser.accept(";");
            return new ASTExternVariable(type, name);
        }
    }
    return :none;
}

string moduleToFile(string module_)
{
    mut string modulePath;
    for (i in 0 .. module_.length) {
        if (module_[i .. i + 1] == ".") modulePath ~= "/";
        else modulePath ~= module_[i .. i + 1];
    }
    return modulePath ~ ".nt";
}

(string, Package) findFileInPackage(string filename, Package pak, Loc loc)
{
    string fileInPackage = pak.path ~ "/" ~ filename;
    if (exists(fileInPackage)) {
        // print(filename ~ " in package '" ~ pak.name ~ "'");
        return (fileInPackage, pak);
    }
    // print(filename ~ " not in package '" ~ pak.name ~ "'");
    for (dep in pak.deps) {
        string fileInDep = dep.path ~ "/" ~ filename;

        if (exists(fileInDep)) {
            // print(filename ~ " in dep package '" ~ dep.name ~ "' of '" ~ pak.name ~ "'");
            return (fileInDep, dep);
        }
        // print(filename ~ " not in dep package '" ~ dep.name ~ "' of '" ~ pak.name ~ "'");
    }
    loc.assert2s(false, "cannot find file '$filename' in package '$(pak.name)' or dependencies");
}

// TODO rewrite this once we have a std.path
(string, Package) findResidualFilename(string filename, Package[] packages)
{
    import std.file : realpath;

    // TODO split then check
    if (!exists(filename)) {
        print("file '$filename' could not be found!");
        assert(false);
    }
    string realFilename = realpath(filename);
    // scan packages in reverse order, so that latter arguments override earlier ones
    for (mut int i = cast(int) packages.length - 1; i >= 0; i -= 1) {
        auto package_ = packages[i];
        string path = package_.path;
        if (realFilename.length >= path.length
            && realFilename[0 .. path.length] == path) {
            // print("file " ~ filename ~ " found as " ~ realFilename[path.length + 1 .. $] ~ " in " ~ package_.name);
            return (realFilename[path.length + 1 .. $], package_);
        }
    }
    for (package_ in packages) {
        print(": $(package_.path)");
    }
    print("file '$filename' is not in folder of any package");
    assert(false);
}

class ImportNamespace : Namespace
{
    Module module_;

    string[] symbols;

    this(this.parent, this.module_, this.symbols) {
        this.isContextScope = this.parent.isContextScope;
    }

    override nullable Symbol lookup(string name, Context context, Loc loc, nullable Expression frame) {
        if (!symbols.length || [any symbol == name for symbol in symbols]) {
            if (auto ret = module_.lookupPublic(context, loc, name)) {
                return ret;
            }
        }
        return this.parent.lookup(name, context, loc, frame);
    }
}

class AstCompileTask : ITask
{
    ASTModule astModule;
    (:none | Module) result;
    Context context;

    this(this.astModule, this.context) {
        this.result = :none;
    }

    Module module_() {
        if (Module m <- result) return m;
        assert(false);
    }

    override void run() {
        this.result = compileModule(context, astModule, true);
    }
}

// Classes do double duty for both module-level and statement-level imports
class ASTImport : ASTDeclaration
{
    ASTImportStatement statement;

    // running in the background
    (WorkPoolBase pool, string id | ASTModule) moduleSource;

    ASTModule module_() {
        return this.moduleSource.case(
            (WorkPoolBase pool, string id):
                pool.wait(id).instanceOf(ImportTask).module_,
            ASTModule mod: mod);
    }

    this(this.statement, this.moduleSource) { }

    override Context compile(Context context) {
        auto astModule_ = this.module_;
        auto id = "compile ast $(astModule_.mangledName)";
        auto task = new AstCompileTask(astModule_, context);

        context.compiler.workPool.queue(id, task);
        auto module_ = context.compiler.workPool.wait(id)
            .instanceOf(AstCompileTask).module_;
        auto namespace = new ImportNamespace(context.namespace, module_, this.statement.symbols);
        auto contextModule = findParent!Module(context.namespace);

        contextModule.addModuleDependency(module_);

        return context.withNamespace(namespace);
    }

    override ASTSymbol quote(Quoter quoter) {
        auto importStatement = this.statement.quote(quoter);
        auto mod = quoter.compilerCall("resolveImport", [
            quoter.compiler.astStringLiteral(this.module_.pak.name, __HERE__),
            importStatement], __HERE__);

        return quoter.compilerCall("astImport", [
            importStatement,
            mod], __HERE__);
    }
}

class ImportTask : ITask
{
    LexicalContext lexicalContext;

    ASTImportStatement import_;

    (ASTModule | :none) result;

    this(this.lexicalContext, this.import_) {
        this.result = :none;
    }

    ASTModule module_() {
        if (ASTModule mod <- result) return mod;
        assert(false);
    }

    override void run() {
        this.result = lexicalContext.resolveImport(import_)
            .instanceOf(ASTModule).notNull;
    }
}

nullable ASTImportStatement parseImportStatement(Parser parser, LexicalContext lexicalContext)
{
    parser.begin;
    bool isMacroImport = acceptIdentifier(parser, "macro");
    if (!acceptIdentifier(parser, "import"))
    {
        parser.revert;
        return null;
    }

    mut uninitialized (:normal, string name, string pak | :macro_, ASTModuleBase module_) target;
    parser.commit;

    auto macroArgs = new ParseImportArgs(parser, lexicalContext);
    lexicalContext.macroState.applyMacro(macroArgs);
    if (macroArgs.done) target = (:macro_, macroArgs.module_.notNull);
    else {
        mut string pak;
        if (acceptIdentifier(parser, "package"))
        {
            parser.expect("(");
            pak = parser.parseIdentifier;
            parser.expect(")");
            parser.expect(".");
        }

        string modname = parseIdentifier2(parser, ".", allowKeywords=false);
        target = (:normal, modname, pak);
    }

    mut string[] symbols;
    if (!isMacroImport && parser.accept(":")) {
        string expectIdentifier(Parser parser) {
            auto ident = parser.parseIdentifier;
            auto loc = parser.loc;
            // TODO parser.loc
            loc.assert2s(ident.length > 0, "identifier expected");
            return ident;
        }
        bool peek(Parser parser, string str) {
            parser.begin;
            auto ret = parser.accept(str);
            parser.revert;
            return ret;
        }
        // : [symbol] [, symbol]* ,? ;
        symbols ~= parser.expectIdentifier;
        while (parser.accept(",")) {
            if (parser.peek(";")) { break; }
            symbols ~= parser.expectIdentifier;
        }
    }
    parser.expect(";");
    return new ASTImportStatement(target, isMacroImport, symbols, parser.loc);
}

class ModuleParserConfig
{
    Package[] packages;

    ASTModule[] defaultImports;

    this(this.packages, this.defaultImports) { }
}

nullable ASTModule parseMacroContinuation(CompilerBase compiler, Parser parser, ParserHelper helper, ASTModule astModule,
    ModuleParserConfig config)
{
    parser.begin;
    if (!acceptIdentifier(parser, "macro") || !parser.accept("("))
    {
        parser.revert;
        return null;
    }
    parser.commit;
    string identifier = parser.parseIdentifier;
    parser.expect(")");
    parser.expect(";");

    auto newMacroState = astModule.macroState.dup;

    helper.proxyCall(astModule, identifier, newMacroState);

    auto subModule = new ASTModule(astModule.loc, astModule.name, astModule.pak, newMacroState, astModule);

    return parseModuleBody(compiler, parser, helper, subModule, config);
}

Package selectPackage(Package[] packages, string name, Loc loc)
{
    auto result = [first a for a in packages where a.name == name else null];
    loc.assert2s(!!result, "Undefined package: '$name'");
    return result.notNull;
}

class LexicalContextImpl : LexicalContext
{
    ParserHelper helper;

    ModuleParserConfig config;

    this(this.compiler, this.pak, this.macroState, this.helper, this.config) { }

    override ASTModuleBase resolveImport(ASTImportStatement importStmt) {
        return .resolveImport(compiler, pak.name, importStmt, helper, config);
    }
}

ASTModule parseModuleBody(CompilerBase compiler, Parser parser, ParserHelper helper, ASTModule module_,
    ModuleParserConfig config)
{
    mut LexicalContext lexicalContext = new LexicalContextImpl(compiler, module_.pak, module_.macroState, helper, config);

    while (!parser.eof)
    {
        mut Protection protection = Protection.public_;
        if (parser.accept("public")) {
            protection = Protection.public_;
        } else if (parser.accept("private")) {
            protection = Protection.private_;
        }
        // Hack. :(
        void add(typeof(({ mut uninitialized ASTModuleEntry e; e; }).symbol) symbol) {
            module_.entries ~= ASTModuleEntry(protection, symbol);
        }
        if (auto import_ = parseImportStatement(parser, lexicalContext)) {
            auto task = new ImportTask(lexicalContext, import_);
            auto pool = compiler.workPool;
            auto id = import_.repr;

            pool.queue(id, task);

            if (import_.isMacroImport)
            {
                module_.macroState = module_.macroState.dup;
                lexicalContext = new LexicalContextImpl(compiler, module_.pak, module_.macroState, helper, config);

                lexicalContext.macroState.addImport(
                    pool.wait(id).instanceOf(ImportTask).module_.macroState);
            }
            else
            {
                add(new ASTImport(import_, (pool, id)));
            }
            continue;
        }
        if (auto sym = parseTemplateContents(parser, lexicalContext)) {
            add(sym);
            continue;
        }
        if (ASTDeclaration decl = lexicalContext.compiler.parseDeclaration(parser, lexicalContext)) {
            add(decl);
            continue;
        }
        parseExtern(parser, lexicalContext).case {
            ASTExternFunction fun: {
                add(fun);
                continue;
            }
            ASTExternVariable var: {
                add(var);
                continue;
            }
            (:none): {}
        }
        if (ASTModule submod = parseMacroContinuation(compiler, parser, helper, module_, config))
            return submod;
        if (ASTUnitTest ut = parseUnitTest(parser, lexicalContext)) {
            add(ut);
            continue;
        }
        if (ASTPragma pragma_ = parsePragma(parser, lexicalContext)) {
            add(pragma_);
            continue;
        }

        parser.fail("couldn't parse function or struct");
    }
    assert(parser.level == 0);
    return module_;
}

ASTModule resolveImport(CompilerBase compiler, string pak, ASTImportStatement import_, ParserHelper helper,
    ModuleParserConfig config)
{
    (string name, string pak) target = import_.target.case(
        (:normal, string name, string pak): (name, pak),
        (:macro_, ASTModuleBase module_): return module_.instanceOf(ASTModule));
    mut Package pak = selectPackage(config.packages, pak, import_.loc);
    // TODO enum SearchMode { Transitive, Direct, Commandline }
    mut bool transitive = true;
    if (target.pak.length) {
        pak = selectPackage(config.packages, target.pak, import_.loc);
        transitive = false;
    }
    return getModule(
        compiler, moduleToFile(target.name), pak, true, helper, config, import_.loc);
}

Module compileModule(Context context, ASTModule astModule, bool addDefaultImports)
{
    mut nullable Module parent = null;
    if (auto astParent = astModule.parent)
    {
        parent = compileModule(context, astParent, addDefaultImports);
    }

    Module module_ = new Module(astModule.loc, astModule.name, astModule.mangledName, parent);
    auto context = context.withNamespace(module_);

    if (addDefaultImports) {
        context.compiler.addDefaultImports(context, module_);
    }

    // Handle all imports in parallel.
    for (entry in astModule.entries) {
        if (ASTImport import_ <- entry.symbol) {
            auto id = "compile ast $(import_.module_.mangledName)";
            auto task = new AstCompileTask(import_.module_, context);

            context.compiler.workPool.queue(id, task);
        }
    }

    for (entry in astModule.entries) {
        // TODO lazily resolve members only once they're looked up
        // (allows compilation in any order)
        entry.symbol.case {
            ASTImport import_:
            {
                auto id = "compile ast $(import_.module_.mangledName)";
                auto importedModule = context.compiler.workPool.wait(id)
                    .instanceOf(AstCompileTask).module_;

                bool public_ = entry.protection == Protection.public_;

                module_.addImport(
                    importedModule, public_=public_, import_.statement.symbols);
            }
            ASTDeclaration astDecl: {
                if (auto structDecl = astDecl.instanceOf(ASTStructDecl)) {
                    module_.add(structDecl.struct_.name, entry.protection, structDecl.struct_);
                } else if (auto aliasDecl = astDecl.instanceOf(ASTAliasDecl)) {
                    module_.register(aliasDecl.name, entry.protection, aliasDecl.target.compile(context));
                } else if (auto templateDecl = astDecl.instanceOf(ASTTemplateDecl)) {
                    module_.register(templateDecl.name, entry.protection, templateDecl.compileSymbol(context));
                } else {
                    print("declaration $(astDecl.repr)");
                    assert(false);
                }
            }
            ASTSymbol symbol: {
                // TODO just handle all as ASTDeclaration
                if (auto classDecl = symbol.instanceOf(ASTClassDecl)) {
                    module_.add(classDecl.name, entry.protection, classDecl);
                }
                else if (auto intfDecl = symbol.instanceOf(ASTIntfDecl)) {
                    module_.add(intfDecl.name, entry.protection, intfDecl);
                }
                else if (auto enumDecl = symbol.instanceOf(ASTEnumDecl)) {
                    module_.register(enumDecl.name, entry.protection, enumDecl.compile(context));
                }
                else if (auto aliasDecl = symbol.instanceOf(ASTAliasDecl)) {
                    module_.add(aliasDecl.name, entry.protection, aliasDecl);
                }
                else if (auto struct_ = symbol.instanceOf(ASTStruct)) {
                    module_.add(struct_.name, entry.protection, struct_);
                }
                else if (auto funDef = symbol.instanceOf(ASTFunction)) {
                    module_.add(funDef.name, entry.protection, funDef);
                }
                else {
                    print("symbol $(symbol.repr)");
                    assert(false);
                }
            }
            ASTExternFunction extern_:
                module_.register(extern_.name, entry.protection, extern_.compile(context));
            ASTExternVariable extern_:
                module_.register(extern_.name, entry.protection, extern_.compile(context));
            ASTUnitTest ut:
                module_.add("", entry.protection, ut);
            ASTPragma pragma_:
                module_.track(pragma_.compile(context));
        }
    }
    module_.compile(context);

    return module_;
}

ASTModule getModule(CompilerBase compiler, mut string filename, mut nullable Package pak, bool search,
    ParserHelper helper, ModuleParserConfig config, Loc loc)
{
    mut string path = filename;
    if (search) {
        assert2s(!!pak, "search, but no package");
        // TODO destructuring tuple
        auto pair = findFileInPackage(filename, pak.notNull, loc);
        path = pair[0];
        pak = pair[1];
    } else {
        assert2s(!pak, "no search, but package");
        auto pair = findResidualFilename(filename, config.packages);
        filename = pair[0];
        pak = pair[1];
    }
    assert(!!pak);
    auto pak = pak.notNull;
    // FIXME the workpool queueing here is 100% redundant with the ImportTask queueing, p sure
    compiler.workPool.queue(path, new ParseAstModuleTask(compiler, filename, path, pak, helper, config, loc));
    return compiler.workPool.wait(path).instanceOf(ParseAstModuleTask).module_;
}

class ParseAstModuleTask : ITask
{
    CompilerBase compiler;
    string filename;
    string path;
    Package pak;
    ParserHelper helper;
    ModuleParserConfig config;
    Loc loc;
    (ASTModule | :none) result;
    this(this.compiler, this.filename, this.path, this.pak, this.helper, this.config, this.loc) {
        this.result = :none;
    }
    ASTModule module_() {
        if (ASTModule mod <- result) return mod;
        assert(false);
    }
    override void run() {
        string code = read(path);
        // do not count references on the file source - it will end
        // up with lots of small inc/decs to fragments that we never free anyway
        (cast(size_t*) &code)[2] = 0;
        Parser parser = new Parser(path, code);

        auto locAtModule = parser.loc;
        parser.expect("module");
        string modname = parseIdentifier2(parser, ".", allowKeywords=false);
        parser.expect(";");

        import std.string : endsWith;

        locAtModule.assert2s2(filename.endsWith(modname.moduleToFile), filename, ": module doesn't match filename");

        ASTModule baseModule = new ASTModule(locAtModule, modname, pak, new MacroState, null);
        result = parseModuleBody(compiler, parser, helper, baseModule, config);
    }
}
