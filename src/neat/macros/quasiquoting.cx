// TODO: $var escaping
module neat.macros.quasiquoting;

import package(compiler).cx.base;
import package(compiler).cx.parser_base;
import package(compiler).helpers;

class QuasiQuoterImpl : Quoter
{
    ASTSymbol compilerExpr;

    this(this.compiler, this.compilerExpr) { }

    override ASTSymbol compilerCall(string name, ASTSymbol[] parameters, Loc loc) {
        return compiler.astCall(compiler.astMember(compilerExpr, name, loc), parameters ~ quoteLoc(loc), loc);
    }

    override ASTSymbol quoteLoc(Loc loc) {
        return compiler.astCall(
            compiler.astIdentifier("Loc", __HERE__), [
                compiler.astStringLiteral(loc.filename, __HERE__),
                compiler.astIntLiteral(loc.row, __HERE__),
                compiler.astIntLiteral(loc.column, __HERE__),
            ], __HERE__);
    }
}

class QuasiQuoting : Macro
{
    this() { }
    override void apply(MacroArgs args) {
        auto args = args.instanceOf(ParsePropertyArgs);
        if (args) {
            args.result = this.parse(args.parser, args.lexicalContext, args.left);
        }
    }

    ASTSymbol parse(Parser parser, LexicalContext lexicalContext, ASTSymbol compilerExpr)
    {
        auto compiler = lexicalContext.compiler;

        auto quoter = new QuasiQuoterImpl(compiler, compilerExpr);
        {
            parser.begin();
            if (parser.accept(".") && parser.accept("$stmt")) {
                parser.commit();
                auto stmt = compiler.parseStatement(parser, lexicalContext);
                parser.assert_(!!stmt, "statement expected");
                return stmt.quote(quoter);
            }
            parser.revert();
        }
        {
            parser.begin();
            if (parser.accept(".") && parser.accept("$expr")) {
                parser.commit();
                auto expr = compiler.parseExpression(parser, lexicalContext);
                parser.assert_(!!expr, "expression expected");
                return expr.quote(quoter);
            }
            parser.revert();
        }
        {
            parser.begin();
            if (parser.accept(".") && parser.accept("$type")) {
                parser.commit();
                auto type = compiler.parseType(parser, lexicalContext);
                parser.assert_(!!type, "type expected");
                return type.quote(quoter);
            }
            parser.revert();
        }
        return null;
    }
}

void addQuasiQuotingMacro(MacroState macroState)
{
    macroState.addMacro(new QuasiQuoting);
}

macro(addQuasiQuotingMacro);
