module neat.formatstring;

macro import neat.macros.listcomprehension;
macro import neat.macros.quasiquoting;

import neat.array;
import neat.base;
import neat.class_;
import neat.decl;
import neat.expr;
import neat.struct_;
import neat.types;
import neat.util;

class ASTFormatString : ASTSymbol
{
    ASTSymbol[] parts;

    this(this.parts, this.locRange) { }

    override (Symbol | fail Error) compile(Context context) {
        mut auto context = context;
        auto compiler = context.compiler;
        mut ASTSymbol result = compiler.astStringLiteral("", __RANGE__);
        bool isString(Type type) {
            if (auto array = type.instanceOf(Array))
                return !!array.elementType.instanceOf(Character);
            return false;
        }
        for (auto part in parts) {
            auto sym = part.compile(context)?.beExpressionImplCall(context, part.locRange)?;
            ASTSymbol astSym = new ASTSymbolHelper(sym);
            if (sym.type.isString) {
                result = compiler.$expr $result ~ $astSym;
            } else if (sym.type.instanceOf(Integer)) {
                result = compiler.$expr $result ~ itoa($astSym);
            } else if (sym.type.instanceOf(Float) || sym.type.instanceOf(Double)) {
                result = compiler.$expr $result ~ ftoa($astSym);
            } else if (sym.type.instanceOf(Long)) {
                result = compiler.$expr $result ~ ltoa($astSym);
            } else if (sym.type.instanceOf(Struct) || sym.type.instanceOf(Class)) {
                result = compiler.$expr $result ~ $astSym.toString;
            } else {
                return this.locRange.fail("Don't know how to format $(sym.type.repr)");
            }
        }
        result = compiler.$expr ({ import package(compiler).neat.runtime : itoa, ftoa, ltoa; $result; });

        return result.compile(context);
    }

    override ASTSymbol quote(Quoter quoter) {
        auto parts = [a.quote(quoter) for a in this.parts];
        auto partsArray = quoter.compiler.astArrayLiteralTypeHint(
            quoter.compiler.astIdentifier("ASTSymbol", __RANGE__),
            parts,
            __RANGE__);

        return quoter.compilerCall("astFormatString", [
            partsArray], this.locRange);
    }
}

