module neat.decl;

import neat.base;
import neat.parser;
import neat.parser_base;
import neat.statements;

class ASTAliasDecl : ASTDeclaration
{
    string name;

    ASTSymbol target;

    this(this.name, this.target) { }

    // FIXME should return a Namespace?
    override (Context | fail Error) compile(Context context) {
        auto target = this.target.compile(context)?;
        return context.withNamespace(new SymbolAlias(context.namespace, this.name, target));
    }

    override ASTSymbol quote(Quoter quoter) {
        with (quoter.compiler) {
            return astNewExpression(
                astIdentifier("ASTAliasDecl", __RANGE__), [
                    astStringLiteral(this.name, __RANGE__),
                    this.target.quote(quoter)], __RANGE__);
        }
    }
}

(nullable ASTAliasDecl | fail Error) parseAliasDecl(Parser parser, LexicalContext lexicalContext) {
    if (!parser.accept("alias")?) return null;
    auto loc = parser.loc;
    string name = parser.parseIdentifier?;
    loc.assert(name.length > 0, "alias name expected")?;
    parser.expect("=")?;
    mut nullable ASTSymbol target = lexicalContext.compiler.parseType(parser, lexicalContext)?;
    if (!target) target = lexicalContext.compiler.parseExpression(parser, lexicalContext)?;
    parser.expect(";")?;
    return new ASTAliasDecl(name, target.notNull);
}
