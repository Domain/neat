module sparkline;

macro import cx.macros.assert;

string[] default_ticks() {
    return ["▁", "▂", "▃", "▄", "▅", "▆", "▇", "█"];
}

string sparkline(string[] ticks, int[] data) {
    assert(ticks.length > 0);
    assert(data.length > 0);

    int n = (cast(int) ticks.length) - 1;
    mut int min = data[0];
    mut int max = data[0];

    for (int point <- data) {
        if (point < min) min = point;
        if (point > max) max = point;
    }

    mut string sparkline = "";

    if (min == max) {
        // All points are the same.
        for (int _ <- data) {
            sparkline = sparkline ~ ticks[n / 2];
        }
    } else {
        for (int point <- data) {
            int i = n * (point - min) / (max - min);
            sparkline = sparkline ~ ticks[i];
        }
    }

    return sparkline;
}
